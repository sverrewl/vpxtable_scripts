' ****************************************************************
'               VISUAL PINBALL X EM Script por JPSalas
'         Script Básico para juegos EM Script hasta 4 players
'        usa el core.vbs para funciones extras
'                     VPX 10.8 Version 6.0.0
' North Pole / IPD No. 6310 / Playmatic 1967 / 1 Player
' ****************************************************************

Option Explicit
Randomize

' Valores Constantes de las físicas de los flippers - se usan en la creación de las bolas, tienen que cargarse antes del core.vbs
Const BallSize = 25 ' el radio de la bola. el tamaño normal es 50 unidades de VP
Const BallMass = 1  ' la pesadez de la bola, este valor va de acuerdo a la fuerza de los flippers y el plunger

' Carga el core.vbs para poder usas sus funciones, sobre todo el vpintimer.addtimer
LoadCoreFiles

Sub LoadCoreFiles
    On Error Resume Next
    ExecuteGlobal GetTextFile("core.vbs")
    If Err Then MsgBox "Can't open core.vbs"
    On Error Resume Next
    ExecuteGlobal GetTextFile("controller.vbs")
    If Err Then MsgBox "Can't open controller.vbs"
End Sub

' Valores Constants
Const TableName = "northpole" ' se usa para cargar y grabar los highscore y creditos
Const cGameName = "northpole" ' para el B2S
Const MaxPlayers = 1          ' solo 1 jugador
Const Special1 = 900          ' puntuación a obtener para partida extra


' Variables Globales
Dim PlayersPlayingGame
Dim CurrentPlayer
Dim Credits
Dim YBonus
Dim RBonus
Dim YBonusEnabled
Dim RBonusEnabled
Dim DoubleBonus
Dim BallsRemaining(4)
Dim Special1Awarded(4)
Dim ExtraBallsAwards(4)
Dim Score(4)
Dim HighScore
Dim Match
Dim Tilt
Dim TiltSensitivity
Dim Tilted
Dim Add1
Dim Add10
Dim Add100
Dim x

' Variables de control
Dim LastSwitchHit
Dim BallsOnPlayfield
Dim BallsToEject

' Variables de tipo Boolean (verdadero ó falso, True ó False)
Dim bAttractMode
Dim bFreePlay
Dim bGameInPlay
Dim bOnTheFirstBall
Dim bExtraBallWonThisBall
Dim bJustStarted
Dim bBallInPlungerLane
Dim bBallSaverActive

' core.vbs variables, como imanes, impulse plunger

' *********************************************************************
'                Rutinas comunes para todas las mesas
' *********************************************************************

Sub Table1_Init()
    Dim x

    ' Inicializar diversos objetos de la mesa, como droptargets, animations...
    VPObjects_Init
    LoadEM

    ' Carga los valores grabados highscore y créditos
    Credits = 1
    Loadhs

    UpdateCredits

    ' Juego libre o con monedas: si es True entonces no se usarán monedas
    bFreePlay = False 'queremos monedas

    If bFreePlay Then DOF 123, DOFOn

    ' Inicialiar las variables globales de la mesa
    bAttractMode = False
    bOnTheFirstBall = False
    bGameInPlay = False
    bBallInPlungerLane = False
    LastSwitchHit = ""
    BallsOnPlayfield = 0
    Tilt = 0
    TiltSensitivity = 6
    Tilted = False
    Match = 0
    bJustStarted = True
    Add1 = 0
    Add10 = 0
    Add100 = 0
    BallsToEject = 0

    ' pone la mesa en modo de espera
    EndOfGame

    'Enciende las luces GI despues de un segundo
    vpmtimer.addtimer 1000, "GiOn '"

    ' Quita los laterales y las puntuaciones cuando la mesa se juega en modo FS
    If Table1.ShowDT then
        For each x in aReels
            x.Visible = 1
        Next
    Else
        For each x in aReels
            x.Visible = 0
        Next
    End If
End Sub

'******
' Keys
'******

Sub Table1_KeyDown(ByVal Keycode)
    If EnteringInitials then
        CollectInitials(keycode)
        Exit Sub
    End If

    ' añade monedas
    If Keycode = AddCreditKey OR Keycode = AddCreditKey2 Then
        If(Tilted = False)Then
            If Not bFreePlay Then DOF 123, DOFOn
            AddCredits 1
            PlaySound "fx_coin"
        End If
    End If

    ' el plunger
    If keycode = PlungerKey Then
        Plunger.Pullback
        PlaySoundAt "fx_plungerpull", plunger
    End If

    ' teclas de la falta
    If keycode = LeftTiltKey Then Nudge 90, 8:PlaySound "fx_nudge", 0, 1, -0.1, 0.25
    If keycode = RightTiltKey Then Nudge 270, 8:PlaySound "fx_nudge", 0, 1, 0.1, 0.25
    If keycode = CenterTiltKey Then Nudge 0, 9:PlaySound "fx_nudge", 0, 1, 1, 0.25

    ' Funcionamiento normal de los flipers y otras teclas durante el juego

    If bGameInPlay AND NOT Tilted Then
        If keycode = LeftTiltKey Then CheckTilt
        If keycode = RightTiltKey Then CheckTilt
        If keycode = CenterTiltKey Then CheckTilt
        If keycode = MechanicalTilt Then CheckTilt

        ' teclas de los flipers
        If keycode = LeftFlipperKey Then SolLFlipper 1
        If keycode = RightFlipperKey Then SolRFlipper 1

        ' tecla de empezar el juego
        ' si el juego ha empezado saca una bola nueva
        If keycode = RightMagnaSave Then
            EjectBall
        End If
        Else ' If (GameInPlay)

            If keycode = StartGameKey Then
                If(bFreePlay = True)Then
                    If(BallsOnPlayfield = 0)Then
                        ResetScores
                        ResetForNewGame()
                    End If
                Else
                    If(Credits> 0)Then
                        If(BallsOnPlayfield = 0)Then
                            Credits = Credits - 1
                            If Credits < 1 Then DOF 123, DOFOff
                            UpdateCredits
                            ResetScores
                            ResetForNewGame()
                        End If
                    Else
                    ' Not Enough Credits to start a game.
                    'PlaySound "so_nocredits"
                    End If
                End If
            End If
    End If ' If (GameInPlay)
End Sub

Sub Table1_KeyUp(ByVal keycode)

    If EnteringInitials then
        Exit Sub
    End If

    If bGameInPlay AND NOT Tilted Then
        ' teclas de los flipers
        If keycode = LeftFlipperKey Then SolLFlipper 0
        If keycode = RightFlipperKey Then SolRFlipper 0
    End If

    If keycode = PlungerKey Then
        Plunger.Fire
        If bBallInPlungerLane Then
            PlaySoundAt "fx_plunger", plunger
        Else
            PlaySoundAt "fx_plunger_empty", plunger
        End If
    End If
End Sub

'*************
' Para la mesa
'*************

Sub table1_Paused
End Sub

Sub table1_unPaused
End Sub

Sub table1_Exit
    Savehs
'Controller.Stop
End Sub

'*******************
' Flipper Subs Rev3
'*******************

SolCallback(sLRFlipper) = "SolRFlipper"
SolCallback(sLLFlipper) = "SolLFlipper"

Sub SolLFlipper(Enabled)
    If Enabled Then
        PlaySoundAt SoundFXDOF("fx_flipperup", 101, DOFOn, DOFFlippers), LeftFlipper
        LeftFlipper.RotateToEnd
        LeftFlipperOn = 1
    Else
        PlaySoundAt SoundFXDOF("fx_flipperdown", 101, DOFOff, DOFFlippers), LeftFlipper
        LeftFlipper.RotateToStart
        LeftFlipperOn = 0
    End If
End Sub

Sub SolRFlipper(Enabled)
    If Enabled Then
        PlaySoundAt SoundFXDOF("fx_flipperup", 102, DOFOn, DOFFlippers), RightFlipper
        RightFlipper.RotateToEnd
        RightFlipperOn = 1
    Else
        PlaySoundAt SoundFXDOF("fx_flipperdown", 102, DOFOff, DOFFlippers), RightFlipper
        RightFlipper.RotateToStart
        RightFlipperOn = 0
    End If
End Sub

' flippers animations

Sub LeftFlipper_Animate: LeftFlipperP.RotZ = LeftFlipper.CurrentAngle: LeftFlipperRubber.Rotz = LeftFlipper.CurrentAngle:End Sub
Sub RightFlipper_Animate: RightFlipperP.RotZ = RightFlipper.CurrentAngle:RightFlipperRubber.Rotz = RightFlipper.CurrentAngle: End Sub

' flippers hit Sound

Sub LeftFlipper_Collide(parm)
    PlaySound "fx_rubber_flipper", 0, Vol(ActiveBall), pan(ActiveBall), 0.1, 0, 0, 0, AudioFade(ActiveBall)
End Sub

Sub RightFlipper_Collide(parm)
    PlaySound "fx_rubber_flipper", 0, Vol(ActiveBall), pan(ActiveBall), 0.1, 0, 0, 0, AudioFade(ActiveBall)
End Sub

'*********************************************************
' Real Time Flipper adjustments - by JLouLouLou & JPSalas
'        (to enable flipper tricks)
'*********************************************************

Dim FlipperPower
Dim FlipperElasticity
Dim SOSTorque, SOSAngle
Dim FullStrokeEOS_Torque, LiveStrokeEOS_Torque
Dim LeftFlipperOn
Dim RightFlipperOn

Dim LLiveCatchTimer
Dim RLiveCatchTimer
Dim LiveCatchSensivity

FlipperPower = 3600
FlipperElasticity = 0.6
FullStrokeEOS_Torque = 0.6 ' EOS Torque when flipper hold up ( EOS Coil is fully charged. Ampere increase due to flipper can't move or when it pushed back when "On". EOS Coil have more power )
LiveStrokeEOS_Torque = 0.3 ' EOS Torque when flipper rotate to end ( When flipper move, EOS coil have less Ampere due to flipper can freely move. EOS Coil have less power )

LeftFlipper.EOSTorqueAngle = 10
RightFlipper.EOSTorqueAngle = 10

SOSTorque = 0.2
SOSAngle = 6

LiveCatchSensivity = 10

LLiveCatchTimer = 0
RLiveCatchTimer = 0

LeftFlipper.TimerInterval = 1
LeftFlipper.TimerEnabled = 1

Sub LeftFlipper_Timer 'flipper's tricks timer
'Start Of Stroke Flipper Stroke Routine : Start of Stroke for Tap pass and Tap shoot
    If LeftFlipper.CurrentAngle >= LeftFlipper.StartAngle - SOSAngle Then LeftFlipper.Strength = FlipperPower * SOSTorque else LeftFlipper.Strength = FlipperPower : End If

'End Of Stroke Routine : Livecatch and Emply/Full-Charged EOS
  If LeftFlipperOn = 1 Then
    If LeftFlipper.CurrentAngle = LeftFlipper.EndAngle then
      LeftFlipper.EOSTorque = FullStrokeEOS_Torque
      LLiveCatchTimer = LLiveCatchTimer + 1
      If LLiveCatchTimer < LiveCatchSensivity Then
        LeftFlipper.Elasticity = 0
      Else
        LeftFlipper.Elasticity = FlipperElasticity
        LLiveCatchTimer = LiveCatchSensivity
      End If
    End If
  Else
    LeftFlipper.Elasticity = FlipperElasticity
    LeftFlipper.EOSTorque = LiveStrokeEOS_Torque
    LLiveCatchTimer = 0
  End If


'Start Of Stroke Flipper Stroke Routine : Start of Stroke for Tap pass and Tap shoot
    If RightFlipper.CurrentAngle <= RightFlipper.StartAngle + SOSAngle Then RightFlipper.Strength = FlipperPower * SOSTorque else RightFlipper.Strength = FlipperPower : End If

'End Of Stroke Routine : Livecatch and Emply/Full-Charged EOS
  If RightFlipperOn = 1 Then
    If RightFlipper.CurrentAngle = RightFlipper.EndAngle Then
      RightFlipper.EOSTorque = FullStrokeEOS_Torque
      RLiveCatchTimer = RLiveCatchTimer + 1
      If RLiveCatchTimer < LiveCatchSensivity Then
        RightFlipper.Elasticity = 0
      Else
        RightFlipper.Elasticity = FlipperElasticity
        RLiveCatchTimer = LiveCatchSensivity
      End If
    End If
  Else
    RightFlipper.Elasticity = FlipperElasticity
    RightFlipper.EOSTorque = LiveStrokeEOS_Torque
    RLiveCatchTimer = 0
  End If
End Sub

'*******************
' Luces GI
'*******************

Sub GiOn 'enciende las luces GI
    DOF 125, DOFOn
  PlaySound"fx_gion"
    Dim bulb
    For each bulb in aGiLights
        bulb.State = 1
    Next
End Sub

Sub GiOff 'apaga las luces GI
    DOF 125, DOFOff
  PlaySound"fx_gioff"
    Dim bulb
    For each bulb in aGiLights
        bulb.State = 0
    Next
End Sub

'**************
' TILT - Falta
'**************

'el "timer" TiltDecreaseTimer resta .01 de la variable "Tilt" cada ronda

Sub CheckTilt                     'esta rutina se llama cada vez que das un golpe a la mesa
    Tilt = Tilt + TiltSensitivity 'añade un valor al contador "Tilt"
    TiltDecreaseTimer.Enabled = True
    If Tilt> 30 Then              'Si la variable "Tilt" es más de 30 entonces haz falta. Normalmente es 15 el valor
        Tilted = True
        TiltReel.SetValue 1       'muestra Tilt en la pantalla
        If B2SOn then
            Controller.B2SSetTilt 1
        end if
        DisableTable True
        TiltRecoveryTimer.Enabled = True 'empieza una pausa a fin de que todas las bolas se cuelen
    End If
End Sub

Sub TiltDecreaseTimer_Timer
    ' DecreaseTilt
    If Tilt> 0 Then
        Tilt = Tilt - 0.1
    Else
        TiltDecreaseTimer.Enabled = False
    End If
End Sub

Sub DisableTable(Enabled)
    If Enabled Then
        'Apaga todas las luces Gi de la mesa
        GiOff
        'Disable slings, bumpers etc
        LeftFlipper.RotateToStart
        RightFlipper.RotateToStart
        Bumper001.Threshold = 100
        Bumper002.Threshold = 100
        Bumper003.Threshold = 100
        LeftSlingshot.Disabled = 1
        RightSlingshot.Disabled = 1
    Else
        'enciende de nuevo todas las luces GI, bumpers y slingshots
        GiOn
        Bumper001.Threshold = 1
        Bumper002.Threshold = 1
        Bumper003.Threshold = 1
        LeftSlingshot.Disabled = 0
        RightSlingshot.Disabled = 0
    End If
End Sub

Sub TiltRecoveryTimer_Timer()
    ' si todas las bolas se han colado, entonces ..
    If(BallsOnPlayfield = 0)Then
        '... haz el fin de bola normal
        ' finaliza la partida con la siguente línea, en esta mesa se penaliza solo la bola
        ' BallsRemaining(CurrentPlayer) = 0
        EndOfBall()
        TiltRecoveryTimer.Enabled = False
    End If
' de lo contrario esta rutina continúa hasta que todas las bolas se han colado
End Sub

'***************************************************************
'             Supporting Ball & Sound Functions v4.0
'***************************************************************

Dim TableWidth, TableHeight

TableWidth = Table1.width
TableHeight = Table1.height

Function Vol(ball) ' Calculates the Volume of the sound based on the ball speed
    Vol = Csng(BallVel(ball) ^2 / 2000)
End Function

Function Pan(ball) ' Calculates the pan for a ball based on the X position on the table. "table1" is the name of the table
    Dim tmp
    tmp = ball.x * 2 / TableWidth-1
    If tmp > 0 Then
        Pan = Csng(tmp ^10)
    Else
        Pan = Csng(-((- tmp) ^10))
    End If
End Function

Function Pitch(ball) ' Calculates the pitch of the sound based on the ball speed
    Pitch = BallVel(ball) * 20
End Function

Function BallVel(ball) 'Calculates the ball speed
    BallVel = (SQR((ball.VelX ^2) + (ball.VelY ^2)))
End Function

Function AudioFade(ball) 'only on VPX 10.4 and newer
    Dim tmp
    tmp = ball.y * 2 / TableHeight-1
    If tmp > 0 Then
        AudioFade = Csng(tmp ^10)
    Else
        AudioFade = Csng(-((- tmp) ^10))
    End If
End Function

Sub PlaySoundAt(soundname, tableobj) 'play sound at X and Y position of an object, mostly bumpers, flippers and other fast objects
    PlaySound soundname, 0, 1, Pan(tableobj), 0.2, 0, 0, 0, AudioFade(tableobj)
End Sub

Sub PlaySoundAtBall(soundname) ' play a sound at the ball position, like rubbers, targets, metals, plastics
    PlaySound soundname, 0, Vol(ActiveBall), pan(ActiveBall), 0.2, Pitch(ActiveBall) * 10, 0, 0, AudioFade(ActiveBall)
End Sub

Function RndNbr(n) 'returns a random number between 1 and n
    Randomize timer
    RndNbr = Int((n * Rnd) + 1)
End Function

'***********************************************
'   JP's VP10 Rolling Sounds + Ballshadow v4.0
'   uses a collection of shadows, aBallShadow
'***********************************************

Const tnob = 19   'total number of balls
Const lob = 0     'number of locked balls
Const maxvel = 25 'max ball velocity
ReDim rolling(tnob)
InitRolling

Sub InitRolling
    Dim i
    For i = 0 to tnob
        rolling(i) = False
    Next
End Sub

Sub RollingUpdate()
    Dim BOT, b, ballpitch, ballvol, speedfactorx, speedfactory
    BOT = GetBalls

    ' stop the sound of deleted balls
    For b = UBound(BOT) + 1 to tnob
        rolling(b) = False
        StopSound("fx_ballrolling" & b)
        aBallShadow(b).Y = 1930
    Next

    ' exit the sub if no balls on the table
    If UBound(BOT) = lob - 1 Then Exit Sub 'there no extra balls on this table

    ' play the rolling sound for each ball and draw the shadow
    For b = lob to UBound(BOT)
        aBallShadow(b).X = BOT(b).X
        aBallShadow(b).Y = BOT(b).Y
        aBallShadow(b).Height = BOT(b).Z -Ballsize/2

        If BallVel(BOT(b))> 1 Then
            If BOT(b).z <0 Then
                ballpitch = Pitch(BOT(b)) - 5000 'decrease the pitch under the playfield
                ballvol = Vol(BOT(b))
            ElseIf BOT(b).z <30 Then
                ballpitch = Pitch(BOT(b))
                ballvol = Vol(BOT(b))
            Else
                ballpitch = Pitch(BOT(b)) + 25000 'increase the pitch on a ramp
                ballvol = Vol(BOT(b)) * 3
            End If
            rolling(b) = True
            PlaySound("fx_ballrolling" & b), -1, ballvol, Pan(BOT(b)), 0, ballpitch, 1, 0, AudioFade(BOT(b))
        Else
            If rolling(b) = True Then
                StopSound("fx_ballrolling" & b)
                rolling(b) = False
            End If
        End If

        ' dropping sounds
        If BOT(b).VelZ <-1 Then
            If BOT(b).z <55 and BOT(b).z> 27 Then PlaySound "fx_balldrop", 0, ABS(BOT(b).velz) / 17, Pan(BOT(b)), 0, Pitch(BOT(b)), 1, 0, AudioFade(BOT(b))
            If BOT(b).z <10 and BOT(b).z> -10 Then PlaySound "fx_hole_enter", 0, ABS(BOT(b).velz) / 17, Pan(BOT(b)), 0, Pitch(BOT(b)), 1, 0, AudioFade(BOT(b))
        End If

        ' jps ball speed & spin control
            BOT(b).AngMomZ = BOT(b).AngMomZ * 0.95
        If BOT(b).VelX AND BOT(b).VelY <> 0 Then
            speedfactorx = ABS(maxvel / BOT(b).VelX)
            speedfactory = ABS(maxvel / BOT(b).VelY)
            If speedfactorx <1 Then
                BOT(b).VelX = BOT(b).VelX * speedfactorx
                BOT(b).VelY = BOT(b).VelY * speedfactorx
            End If
            If speedfactory <1 Then
                BOT(b).VelX = BOT(b).VelX * speedfactory
                BOT(b).VelY = BOT(b).VelY * speedfactory
            End If
        End If
    Next
End Sub

'*****************************
' Ball 2 Ball Collision Sound
'*****************************

Sub OnBallBallCollision(ball1, ball2, velocity)
    PlaySound("fx_collide"), 0, Csng(velocity) ^2 / 2000, Pan(ball1), 0, Pitch(ball1), 0, 0, AudioFade(ball1)
End Sub

'***************************************
' Sonidos de las colecciones de objetos
' como metales, gomas, plásticos, etc
'***************************************

Sub aMetals_Hit(idx):PlaySoundAtBall "fx_MetalHit":End Sub
Sub aRubber_Bands_Hit(idx):PlaySoundAtBall "fx_rubber_band":End Sub
Sub aRubber_Posts_Hit(idx):PlaySoundAtBall "fx_rubber_post":End Sub
Sub aRubber_Pins_Hit(idx):PlaySoundAtBall "fx_rubber_pin":End Sub
Sub aPlastics_Hit(idx):PlaySoundAtBall "fx_PlasticHit":End Sub
Sub aGates_Hit(idx):PlaySoundAtBall "fx_Gate":End Sub
Sub aWoods_Hit(idx):PlaySoundAtBall "fx_Woodhit":End Sub

'************************************************************************************************************************
' Solo para VPX 10.2 y posteriores.
' FlashForMs hará parpadear una luz o un flash por unos milisegundos "TotalPeriod" cada tantos milisegundos "BlinkPeriod"
' Cuando el "TotalPeriod" haya terminado, la luz o el flasher se pondrá en el estado especificado por el valor "FinalState"
' El valor de "FinalState" puede ser: 0=apagado, 1=encendido, 2=regreso al estado anterior
'************************************************************************************************************************

Sub FlashForMs(MyLight, TotalPeriod, BlinkPeriod, FinalState)

    If TypeName(MyLight) = "Light" Then ' la luz es del tipo "light"

        If FinalState = 2 Then
            FinalState = MyLight.State  'guarda el estado actual de la luz
        End If
        MyLight.BlinkInterval = BlinkPeriod
        MyLight.Duration 2, TotalPeriod, FinalState
    ElseIf TypeName(MyLight) = "Flasher" Then ' la luz es del tipo "flash"
        Dim steps
        ' Store all blink information
        steps = Int(TotalPeriod / BlinkPeriod + .5) 'número de encendidos y apagados que hay que ejecutar
        If FinalState = 2 Then                      'guarda el estado actual del flash
            FinalState = ABS(MyLight.Visible)
        End If
        MyLight.UserValue = steps * 10 + FinalState 'guarda el número de parpadeos

        ' empieza los parpadeos y crea la rutina que se va a ejecutar como un timer que se va a ejecutar los parpadeos
        MyLight.TimerInterval = BlinkPeriod
        MyLight.TimerEnabled = 0
        MyLight.TimerEnabled = 1
        ExecuteGlobal "Sub " & MyLight.Name & "_Timer:" & "Dim tmp, steps, fstate:tmp=me.UserValue:fstate = tmp MOD 10:steps= tmp\10 -1:Me.Visible = steps MOD 2:me.UserValue = steps *10 + fstate:If Steps = 0 then Me.Visible = fstate:Me.TimerEnabled=0:End if:End Sub"
    End If
End Sub

'****************************************
' Inicializa la mesa para un juego nuevo
'****************************************

Sub ResetForNewGame()
    'debug.print "ResetForNewGame"
    Dim i, itemp

    bGameInPLay = True
    bBallSaverActive = False

    'pone a cero los marcadores y apaga las luces de espera.
    StopAttractMode
    If B2SOn then
        Controller.B2SSetGameOver 0
    end if
    ' enciende las luces GI si estuvieran apagadas
    GiOn

    CurrentPlayer = 1
    PlayersPlayingGame = 1
    bOnTheFirstBall = True
    For i = 1 To MaxPlayers
        Score(i) = 0
        ExtraBallsAwards(i) = 0
        Special1Awarded(i) = False
        BallsRemaining(i) = BallsPerGame
    Next
    DoubleBonus = 1
    YBonus = 0
    RBonus = 0
    YBonusEnabled = False
    RBonusEnabled = False
    UpdateBallInPlay
    BallsToEject = BallsPerGame

    Clear_Match
    If B2SOn then
        for itemp = 50 to 57
            Controller.B2SSetData itemp, 0
        next
    end if
    ' inicializa otras variables
    Tilt = 0

    ' inicializa las variables del juego
    Game_Init()
    BallsRelease

    ' ahora puedes empezar una música si quieres
    ' empieza la rutina "Firstball" despues de una pequeña pausa
    vpmtimer.addtimer 2000, "FirstBall '"
End Sub

' esta pausa es para que la mesa tenga tiempo de poner los marcadores a cero y actualizar las luces

Sub FirstBall
    'debug.print "FirstBall"
    ' ajusta la mesa para una bola nueva, sube las dianas abatibles, etc
    ResetForNewPlayerBall()
    ' crea una bola nueva en la zona del plunger si está en modo automático
    If AutoBallLoading Then
        BallsToEject = BallsToEject - 1
        CreateNewBall()
    End If
End Sub

' (Re-)inicializa la mesa para una bola nueva, tanto si has perdido la bola, oe le toca el turno al otro jugador

Sub ResetForNewPlayerBall()

    'debug.print "ResetForNewPlayerBall"
    ' Se asegura que los marcadores están activados para el jugador de turno
    AddScore 0

    ' ajusta el multiplicador del bonus multiplier a 1X (si hubiese multiplicador en la mesa)

    ' enciende las luces, reinicializa las variables del juego, etc
    bExtraBallWonThisBall = False
    ResetNewBallVariables
End Sub

' Crea una bola nueva en la mesa

Sub CreateNewBall()
    ' crea una bola nueva basada en el tamaño y la masa de la bola especificados al principio del script
    BallRelease.CreateSizedBallWithMass BallSize, BallMass

    ' incrementa el número de bolas en el tablero, ya que hay que contarlas
    BallsOnPlayfield = BallsOnPlayfield + 1

    ' actualiza las luces del backdrop
    UpdateBallInPlay

    ' y expulsa la bola
    PlaySoundAt SoundFXDOF("fx_Ballrel",113,DOFPulse,DOFContactors), BallRelease
    BallRelease.Kick 270, 2
End Sub

' El jugador ha perdido su bola, y ya no hay más bolas en juego
' Empieza a contar los bonos

Sub EndOfBall()
    'debug.print "EndOfBall"

    ' La primera se ha perdido. Desde aquí ya no se puede aceptar más jugadores
    bOnTheFirstBall = False

    'no count bonus? buaaa

    vpmtimer.addtimer 400, "EndOfBall2 '"
End Sub

' Bonus Count subs

Sub StartBonusCount
    If NOT Tilted Then
        If DoubleBonus = 2 Then
            BonusCountTimer.Interval = 400
        Else
            BonusCountTimer.Interval = 250
        End If
        BonusCountTimer.Enabled = 1
    End If
End Sub

Sub BonusCountTimer_Timer 'añade los bonos y actualiza las luces
    'debug.print "BonusCount_Timer"
    If YBonusEnabled Then
        If YBonus> 0 Then
            YBonus = YBonus -1
            AddScore 10 * DoubleBonus
            UpdateBonusLights
            Exit Sub
        End If
    End If
    If RBonusEnabled Then
        If RBonus> 0 Then
            RBonus = RBonus -1
            AddScore 10 * DoubleBonus
            UpdateBonusLights
            Exit Sub
        End If
    End If
    ' termina la cuenta de los bonos y continúa con el fin de bola
    BonusCountTimer.Enabled = 0
    If BallinCkicker Then
        BallinCkicker = False
        YBonusEnabled = False
        RBonusEnabled = False
        KickoutKickers 'in case of multiball
    Else
        KickoutKickers
    End If
End Sub

Sub UpdateBonusLights 'enciende o apaga las luces de los bonos según la variable "Bonus"
    Select Case YBonus
        Case 0:light001.State = 0:light004.State = 0:light006.State = 0:light008.State = 0:light010.State = 0:light013.State = 0:light015.State = 0:light017.State = 0:light019.State = 0:light021.State = 0
        Case 1:light001.State = 1:light004.State = 0:light006.State = 0:light008.State = 0:light010.State = 0:light013.State = 0:light015.State = 0:light017.State = 0:light019.State = 0:light021.State = 0
        Case 2:light001.State = 0:light004.State = 1:light006.State = 0:light008.State = 0:light010.State = 0:light013.State = 0:light015.State = 0:light017.State = 0:light019.State = 0:light021.State = 0
        Case 3:light001.State = 0:light004.State = 0:light006.State = 1:light008.State = 0:light010.State = 0:light013.State = 0:light015.State = 0:light017.State = 0:light019.State = 0:light021.State = 0
        Case 4:light001.State = 0:light004.State = 0:light006.State = 0:light008.State = 1:light010.State = 0:light013.State = 0:light015.State = 0:light017.State = 0:light019.State = 0:light021.State = 0
        Case 5:light001.State = 0:light004.State = 0:light006.State = 0:light008.State = 0:light010.State = 1:light013.State = 0:light015.State = 0:light017.State = 0:light019.State = 0:light021.State = 0
        Case 6:light001.State = 0:light004.State = 0:light006.State = 0:light008.State = 0:light010.State = 0:light013.State = 1:light015.State = 0:light017.State = 0:light019.State = 0:light021.State = 0
        Case 7:light001.State = 0:light004.State = 0:light006.State = 0:light008.State = 0:light010.State = 0:light013.State = 0:light015.State = 1:light017.State = 0:light019.State = 0:light021.State = 0
        Case 8:light001.State = 0:light004.State = 0:light006.State = 0:light008.State = 0:light010.State = 0:light013.State = 0:light015.State = 0:light017.State = 1:light019.State = 0:light021.State = 0
        Case 9:light001.State = 0:light004.State = 0:light006.State = 0:light008.State = 0:light010.State = 0:light013.State = 0:light015.State = 0:light017.State = 0:light019.State = 1:light021.State = 0
        Case 10:light001.State = 0:light004.State = 0:light006.State = 0:light008.State = 0:light010.State = 0:light013.State = 0:light015.State = 0:light017.State = 0:light019.State = 0:light021.State = 1
    End Select
    Select Case RBonus
        Case 0:light002.State = 0:light003.State = 0:light005.State = 0:light007.State = 0:light009.State = 0:light012.State = 0:light014.State = 0:light016.State = 0:light018.State = 0:light020.State = 0
        Case 1:light002.State = 1:light003.State = 0:light005.State = 0:light007.State = 0:light009.State = 0:light012.State = 0:light014.State = 0:light016.State = 0:light018.State = 0:light020.State = 0
        Case 2:light002.State = 0:light003.State = 1:light005.State = 0:light007.State = 0:light009.State = 0:light012.State = 0:light014.State = 0:light016.State = 0:light018.State = 0:light020.State = 0
        Case 3:light002.State = 0:light003.State = 0:light005.State = 1:light007.State = 0:light009.State = 0:light012.State = 0:light014.State = 0:light016.State = 0:light018.State = 0:light020.State = 0
        Case 4:light002.State = 0:light003.State = 0:light005.State = 0:light007.State = 1:light009.State = 0:light012.State = 0:light014.State = 0:light016.State = 0:light018.State = 0:light020.State = 0
        Case 5:light002.State = 0:light003.State = 0:light005.State = 0:light007.State = 0:light009.State = 1:light012.State = 0:light014.State = 0:light016.State = 0:light018.State = 0:light020.State = 0
        Case 6:light002.State = 0:light003.State = 0:light005.State = 0:light007.State = 0:light009.State = 0:light012.State = 1:light014.State = 0:light016.State = 0:light018.State = 0:light020.State = 0
        Case 7:light002.State = 0:light003.State = 0:light005.State = 0:light007.State = 0:light009.State = 0:light012.State = 0:light014.State = 1:light016.State = 0:light018.State = 0:light020.State = 0
        Case 8:light002.State = 0:light003.State = 0:light005.State = 0:light007.State = 0:light009.State = 0:light012.State = 0:light014.State = 0:light016.State = 1:light018.State = 0:light020.State = 0
        Case 9:light002.State = 0:light003.State = 0:light005.State = 0:light007.State = 0:light009.State = 0:light012.State = 0:light014.State = 0:light016.State = 0:light018.State = 1:light020.State = 0
        Case 10:light002.State = 0:light003.State = 0:light005.State = 0:light007.State = 0:light009.State = 0:light012.State = 0:light014.State = 0:light016.State = 0:light018.State = 0:light020.State = 1
    End Select
End Sub

Sub EndOfBall2()
    'debug.print "EndOfBall2"
    ' si hubiese falta, quítala, y pon la cuenta a cero de la falta para el próximo jugador, ó bola

    Tilted = False
    Tilt = 0
    TiltReel.SetValue 0
    If B2SOn then
        Controller.B2SSetTilt 0
    end if
    DisableTable False 'activa de nuevo los bumpers y los slingshots

    ' ¿ha ganado el jugador una bola extra?
    If(ExtraBallsAwards(CurrentPlayer)> 0)Then
        'debug.print "Extra Ball"

        ' sí? entonces se la das al jugador
        ExtraBallsAwards(CurrentPlayer) = ExtraBallsAwards(CurrentPlayer)- 1

        ' si no hay más bolas apaga la luz de jugar de nuevo
        If(ExtraBallsAwards(CurrentPlayer) = 0)Then
        'LightShootAgain.State = 0
        'If B2SOn then
        '    Controller.B2SSetShootAgain 0
        'end if
        End If

' aquí se podría poner algún sonido de bola extra o alguna luz que parpadee

' En esta mesa hacemos la bola extra igual como si fuese la siguente bola, haciendo un reset de las variables y dianas
        ResetForNewPlayerBall()

        ' creamos una bola nueva en el pasillo de disparo
        CreateNewBall()
    Else ' no hay bolas extras

        BallsRemaining(CurrentPlayer) = BallsRemaining(CurrentPlayer)- 1

        ' ¿Es ésta la última bola?
        If(BallsRemaining(CurrentPlayer) <= 0)Then

            ' miramos si la puntuación clasifica como el Highscore
            CheckHighScore()
        End If

        ' ésta no es la última bola para éste jugador
        ' y si hay más de un jugador continúa con el siguente
        EndOfBallComplete()
    End If
End Sub

' Esta rutina se llama al final de la cuenta del bonus
' y pasa a la siguente bola o al siguente jugador
'
Sub EndOfBallComplete()
    'debug.print "EndOfBallComplete"
    Dim NextPlayer

    'debug.print "EndOfBall - Complete"

    ' ¿hay otros jugadores?
    If(PlayersPlayingGame> 1)Then
        ' entonces pasa al siguente jugador
        NextPlayer = CurrentPlayer + 1
        ' ¿vamos a pasar del último jugador al primero?
        ' (por ejemplo del jugador 4 al no. 1)
        If(NextPlayer> PlayersPlayingGame)Then
            NextPlayer = 1
        End If
    Else
        NextPlayer = CurrentPlayer
    End If

    'debug.print "Next Player = " & NextPlayer

    ' ¿Hemos llegado al final del juego? (todas las bolas se han jugado de todos los jugadores)
    If((BallsRemaining(CurrentPlayer) <= 0)AND(BallsRemaining(NextPlayer) <= 0))Then

        ' aquí se empieza la lotería, normalmente cuando se juega con monedas
        If bFreePlay = False Then
            Verification_Match
        End If

        ' ahora se pone la mesa en el modo de final de juego
        EndOfGame()
    Else
        ' pasamos al siguente jugador
        CurrentPlayer = NextPlayer

        ' nos aseguramos de que el backdrop muestra el jugador actual
        'AddScore 0

        ' hacemos un reset del la mesa para el siguente jugador (ó bola)
        ResetForNewPlayerBall()

        ' y sacamos una bola si está en modo automatico
        If AutoBallLoading Then
            BallsToEject = BallsToEject - 1
            CreateNewBall()
        End If
    End If
End Sub

Sub EjectBall 'only for this table
    If BallsToEject> 0 Then
        BallsToEject = BallsToEject - 1
        CreateNewBall()
    End If
End Sub

' Esta función se llama al final del juego

Sub EndOfGame()
    'debug.print "EndOfGame"
    bGameInPLay = False
    bJustStarted = False
    If B2SOn then
        Controller.B2SSetGameOver 1
        Controller.B2SSetBallInPlay 0
        Controller.B2SSetPlayerUp 0
        Controller.B2SSetCanPlay 0
    end if
    ' asegúrate de que los flippers están en modo de reposo
    SolLFlipper 0
    SolRFlipper 0

    ' pon las luces en el modo de fin de juego
    StartAttractMode
End Sub

' Esta función calcula el no de bolas que quedan
Function Balls
    Dim tmp
    tmp = BallsPerGame - BallsRemaining(CurrentPlayer) + 1
    If tmp> BallsPerGame Then
        Balls = BallsPerGame
    Else
        Balls = tmp
    End If
End Function

' Esta función calcula el Highscore y te da una partida gratis si has conseguido el Highscore
Sub CheckHighscore
    Dim playertops, si, sj, i, stemp, stempplayers
    For i = 1 to 4
        sortscores(i) = 0
        sortplayers(i) = 0
    Next
    playertops = 0
    For i = 1 to PlayersPlayingGame
        sortscores(i) = Score(i)
        sortplayers(i) = i
    Next
    For si = 1 to PlayersPlayingGame
        For sj = 1 to PlayersPlayingGame-1
            If sortscores(sj)> sortscores(sj + 1)then
                stemp = sortscores(sj + 1)
                stempplayers = sortplayers(sj + 1)
                sortscores(sj + 1) = sortscores(sj)
                sortplayers(sj + 1) = sortplayers(sj)
                sortscores(sj) = stemp
                sortplayers(sj) = stempplayers
            End If
        Next
    Next
    HighScoreTimer.interval = 100
    HighScoreTimer.enabled = True
    ScoreChecker = 4
    CheckAllScores = 1
    NewHighScore sortscores(ScoreChecker), sortplayers(ScoreChecker)
End Sub

'******************
'  Match - Loteria
'******************

Sub IncreaseMatch
    Match = (Match + 1)MOD 10
End Sub

Sub Verification_Match()
    PlaySound "fx_match"
    Display_Match
    If(Score(CurrentPlayer)MOD 10) = Match Then
        PlaySound SoundFXDOF("fx_knocker",124,DOFPulse,DOFKnocker)
        If(bFreePlay = False) Then DOF 123, DOFOn
        AddCredits 1
    End If
End Sub

Sub Clear_Match()
    MatchReel.SetValue 0
    If B2SOn then
        Controller.B2SSetMatch 0
    end if
End Sub

Sub Display_Match()
    MatchReel.SetValue Match + 1
    If B2SOn then
        If Match = 0 then
            Controller.B2SSetMatch 100
        else
            Controller.B2SSetMatch Match
        end if
    end if
End Sub

' *********************************************************************
'                      Drain / Plunger Functions
' *********************************************************************

' has perdido la bola ;-( mira cuantas bolas hay en el tablero.
' si solamente hay una entonces reduce el número de bola y mira si es la última para finalizar el juego
' si hay más de una, significa que hay multiball, entonces continua con la partida
'
Sub Drain_Hit()
    ' destruye la bola
    Drain.DestroyBall

    BallsOnPlayfield = BallsOnPlayfield - 1

    ' haz sonar el ruido de la bola
    PlaySoundAt "fx_drain", Drain

    ' haz la bola visible en el apron,
    kicker003.CreateBall
    kicker007.Enabled = 1 'asegúrate de que este está activado
    if NOT kicker003.Enabled then kicker003.kick 76, 3

    'si hay falta el sistema de tilt se encargará de continuar con la siguente bola/jugador
    If Tilted Then
        StopEndOfBallMode
    End If

    ' si estás jugando y no hay falta
    If(bGameInPLay = True)AND(Tilted = False)Then

        ' ¿está el salva bolas activado?
        If(bBallSaverActive = True)Then

            ' ¿sí?, pues creamos una bola
            CreateNewBall()
        Else
            ' ¿es ésta la última bola en juego?
            If(BallsOnPlayfield = 0)Then
                StopEndOfBallMode
                vpmtimer.addtimer 500, "EndOfBall '" 'hacemos una pequeña pausa anter de continuar con el fin de bola
                Exit Sub
            Else
                BallsRemaining(CurrentPlayer) = BallsRemaining(CurrentPlayer)- 1 'count the lost ball
            End If
        End If
    End If
End Sub

Sub swPlungerRest_Hit()
    bBallInPlungerLane = True
End Sub

' La bola ha sido disparada, así que cambiamos la variable, que en esta mesa se usa solo para que el sonido del disparador cambie según hay allí una bola o no
' En otras mesas podrá usarse para poner en marcha un contador para salvar la bola

Sub swPlungerRest_UnHit()
    bBallInPlungerLane = False
End Sub

' *********************************************************************
'               Funciones para la cuenta de los puntos
' *********************************************************************

' Añade puntos al jugador, hace sonar las campanas y actualiza el backdrop

Sub AddScore(Points)
    If Tilted Then Exit Sub
    IncreaseMatch
    Select Case Points
        Case 1, 10, 100
            ' añade los puntos a la variable del actual jugador
            Score(CurrentPlayer) = Score(CurrentPlayer) + points
            ' actualiza los contadores
            UpdateScore points
            ' hace sonar las campanillas
            PlaySound SoundFXDOF(Points,130 + Points,DOFPulse,DOFChimes)
        Case 2, 3, 4, 5
            Add1 = Add1 + Points
            AddScore1Timer.Enabled = TRUE
        Case 20, 30, 40, 50
            Add10 = Add10 + Points \ 10
            AddScore10Timer.Enabled = TRUE
        Case 200, 300, 400, 500
            Add100 = Add100 + Points \ 100
            AddScore100Timer.Enabled = TRUE
    End Select

    If Score(CurrentPlayer)> 999 then
        ScoreReel1000.SetValue 1
    End If

    ' ' aquí se puede hacer un chequeo si el jugador ha ganado alguna puntuación alta y darle un crédito ó bola extra
    If Score(CurrentPlayer) >= Special1 AND Special1Awarded(CurrentPlayer) = False Then
        AwardSpecial
        Special1Awarded(CurrentPlayer) = True
    End If
End Sub

'******************************
'TIMER DE 1, 10 y 100 PUNTOS
'******************************

' hace sonar las campanillas según los puntos

Sub AddScore1Timer_Timer()
    if Add1> 0 then
        AddScore 1
        Add1 = Add1 - 1
    Else
        Me.Enabled = FALSE
    End If
End Sub

Sub AddScore10Timer_Timer()
    if Add10> 0 then
        AddScore 10
        Add10 = Add10 - 1
    Else
        Me.Enabled = FALSE
    End If
End Sub

Sub AddScore100Timer_Timer()
    if Add100> 0 then
        AddScore 100
        Add100 = Add100 - 1
    Else
        Me.Enabled = FALSE
    End If
End Sub

'*******************
'     BONOS
'*******************

Sub AddYBonus(bonuspoints)
    If(Tilted = False)Then
        ' añade los bonos al jugador actual
        YBonus = YBonus + bonuspoints
        If YBonus> 10 Then
            YBonus = 10
        End If
        ' actualiza las luces
        UpdateBonusLights
    End if
End Sub

Sub AddRBonus(bonuspoints)
    If(Tilted = False)Then
        ' añade los bonos al jugador actual
        RBonus = RBonus + bonuspoints
        If RBonus> 10 Then
            RBonus = 10
        End If
        ' actualiza las luces
        UpdateBonusLights
    End if
End Sub

'***********************************************************************************
'        Score reels - puntuaciones - y actualiza otras luces del backdrop
'***********************************************************************************
'esta es al rutina que actualiza la puntuación del jugador

Sub UpdateScore(playerpoints)
    Select Case CurrentPlayer
        Case 1:ScoreReel1.Addvalue playerpoints
    ' Case 2:ScoreReel2.Addvalue playerpoints
    ' Case 3:ScoreReel3.Addvalue playerpoints
    ' Case 4:ScoreReel4.Addvalue playerpoints
    End Select
    If B2SOn then
        Controller.B2SSetScorePlayer CurrentPlayer, Score(CurrentPlayer)
        If Score(CurrentPlayer) >= 1000 then
            Controller.B2SSetData 99, 1
        end if
    end if
End Sub

' pone todos los marcadores a 0

Sub ResetScores
    ScoreReel1.ResetToZero
    ScoreReel1000.ResetToZero
    If B2SOn then
        Controller.B2SSetScorePlayer1 0
        Controller.B2SSetScoreRolloverPlayer1 0
        Controller.B2SSetData 99, 0
    end if
End Sub

Sub AddCredits(value)
    If Credits <15 Then
        Credits = Credits + value
        CreditsReel.SetValue Credits
        UpdateCredits
    end if
End Sub

Sub UpdateCredits
    PlaySound "fx_relay"
    CreditsReel.SetValue credits
    If B2SOn then
        Controller.B2SSetCredits Credits
    end if
End Sub

Sub UpdateBallInPlay 'actualiza los marcadores de las bolas, el número de jugador y el número total de jugadores
'Select Case Balls
'    Case 0:BallDisplay.ImageA = "Ballnr0"
'    Case 1:BallDisplay.ImageA = "Ballnr1"
'    Case 2:BallDisplay.ImageA = "Ballnr2"
'    Case 3:BallDisplay.ImageA = "Ballnr3"
'    Case 4:BallDisplay.ImageA = "Ballnr4"
'    Case 5:BallDisplay.ImageA = "Ballnr5"
'End Select
'If B2SOn then
'    Controller.B2SSetBallInPlay Balls
'end if
End Sub

'*************************
' Partidas y bolas extras
'*************************

Sub AwardExtraBall()
    If NOT bExtraBallWonThisBall Then
        PlaySound SoundFXDOF("fx_knocker",124,DOFPulse,DOFKnocker)
        ExtraBallsAwards(CurrentPlayer) = ExtraBallsAwards(CurrentPlayer) + 1
        bExtraBallWonThisBall = True
    'LightShootAgain.State = 1
    'If B2SOn then
    '    Controller.B2SSetShootAgain 1
    'end if
    END If
End Sub

Sub AwardSpecial()
    PlaySound SoundFXDOF("fx_knocker",124,DOFPulse,DOFKnocker)
    If(bFreePlay = False) Then DOF 123, DOFOn
    AddCredits 1
End Sub

' ********************************
'        Attract Mode
' ********************************
' las luces simplemente parpadean de acuerdo a los valores que hemos puesto en el "Blink Pattern" de cada luz

Sub StartAttractMode()
    Dim x
    bAttractMode = True
    'StartLightSeq
    ' enciente la luz de fin de partida, bola en juego, ++
    GameOverR.SetValue 1
'BallDisplay.ImageA = "ballnr0"
End Sub

Sub StopAttractMode()
    Dim x
    bAttractMode = False
    'LightSeqAttract.StopPlay
    ResetScores
    ' apaga la luz de fin de partida
    GameOverR.SetValue 0
End Sub

Sub StartLightSeq()
'lights sequences
'LightSeqAttract.UpdateInterval = 250
'LightSeqAttract.Play SeqRandom, 10, , 50000
End Sub

Sub LightSeqAttract_PlayDone()
    StartLightSeq()
End Sub

'************************************************
'    Load (cargar) / Save (guardar)/ Highscore
'************************************************

Sub Loadhs
    ' Based on Black's Highscore routines
    Dim FileObj
    Dim ScoreFile, TextStr
    Dim temp1
    Dim temp2
    Dim temp3
    Dim temp4
    Dim temp5
    Dim temp6
    Dim temp8
    Dim temp9
    Dim temp10
    Dim temp11
    Dim temp12
    Dim temp13
    Dim temp14
    Dim temp15
    Dim temp16
    Dim temp17

    Set FileObj = CreateObject("Scripting.FileSystemObject")
    If Not FileObj.FolderExists(UserDirectory)then
        Credits = 0
        Exit Sub
    End If
    If Not FileObj.FileExists(UserDirectory & TableName& ".txt")then
        Credits = 0
        Exit Sub
    End If
    Set ScoreFile = FileObj.GetFile(UserDirectory & TableName& ".txt")
    Set TextStr = ScoreFile.OpenAsTextStream(1, 0)
    If(TextStr.AtEndOfStream = True)then
        Exit Sub
    End If
    temp1 = TextStr.ReadLine
    temp2 = textstr.readline

    HighScore = cdbl(temp1)
    If HighScore <1 then
        temp8 = textstr.readline
        temp9 = textstr.readline
        temp10 = textstr.readline
        temp11 = textstr.readline
        temp12 = textstr.readline
        temp13 = textstr.readline
        temp14 = textstr.readline
        temp15 = textstr.readline
        temp16 = textstr.readline
        temp17 = textstr.readline
    End If
    TextStr.Close
    Credits = cdbl(temp2)
    If Credits > 0 Then DOF 123, DOFOn

    If HighScore <1 then
        HSScore(1) = int(temp8)
        HSScore(2) = int(temp9)
        HSScore(3) = int(temp10)
        HSScore(4) = int(temp11)
        HSScore(5) = int(temp12)

        HSName(1) = temp13
        HSName(2) = temp14
        HSName(3) = temp15
        HSName(4) = temp16
        HSName(5) = temp17
    End If
    Set ScoreFile = Nothing
    Set FileObj = Nothing
End Sub

Sub Savehs
    ' Based on Black's Highscore routines
    Dim FileObj
    Dim ScoreFile
    Dim xx
    Set FileObj = CreateObject("Scripting.FileSystemObject")
    If Not FileObj.FolderExists(UserDirectory)then
        Exit Sub
    End If
    Set ScoreFile = FileObj.CreateTextFile(UserDirectory & TableName& ".txt", True)
    ScoreFile.WriteLine 0
    ScoreFile.WriteLine Credits
    For xx = 1 to 5
        scorefile.writeline HSScore(xx)
    Next
    For xx = 1 to 5
        scorefile.writeline HSName(xx)
    Next
    ScoreFile.Close
    Set ScoreFile = Nothing
    Set FileObj = Nothing
End Sub

'****************************************
' Actualizaciones en tiempo real
'****************************************
' se usa sobre todo para hacer animaciones o sonidos que cambian en tiempo real
' como por ejemplo para sincronizar los flipers, puertas ó molinillos con primitivas

Sub GameTimer_Timer
    RollingUpdate 'actualiza el sonido de la bola rodando
End Sub

'***********************************************************************
' *********************************************************************
'            Aquí empieza el código particular a la mesa
' (hasta ahora todas las rutinas han sido muy generales para todas las mesas)
' (y hay muy pocas rutinas que necesitan cambiar de mesa a mesa)
' *********************************************************************
'***********************************************************************

' se inicia las dianas abatibles, primitivas, etc.
' aunque en el VPX no hay muchos objetos que necesitan ser iniciados

Sub VPObjects_Init 'inicia objetos al empezar
    BallsInit
    TurnOffPlayfieldLights()
End Sub

' variables de la mesa

Dim BallinYkicker, BallinRkicker, BallinCkicker
Dim DoubleBonusStep
Dim LightsState

Sub Game_Init() 'esta rutina se llama al principio de un nuevo juego

    'Empezar alguna música, si hubiera música en esta mesa

    'iniciar variables, en esta mesa hay muy pocas variables ya que usamos las luces, y el UserValue de las dianas
    BallinYkicker = False
    BallinRkicker = False
    BallinCkicker = False
    DoubleBonusStep = 0
    LightsState = 0
    'iniciar algún timer

    'Iniciar algunas luces
    TurnOffPlayfieldLights
    'arriba
    light023.State = 1
    light024.State = 1
    light025.State = 1
    RotateLights
End Sub

Sub StopEndOfBallMode()     'this sub is called after the last ball is drained

End Sub

Sub ResetNewBallVariables() 'inicia las variable y luces para una bola ó jugador nuevo
    If DoubleBonus = 2 Then
        DoubleBonus = 1
        DoubleBonusStep = 0
        light023.State = 1
        light024.State = 1
        light025.State = 1
        light026.State = 0
    End If
End Sub

Sub TurnOffPlayfieldLights()
    Dim a
    For each a in aLights
        a.State = 0
    Next
End Sub

' *********************************************************************
'       Eventos de la mesa - choque de la bola contra dianas
'
' En cada diana u objeto con el que la bola choque habrá que hacer:
' - sonar un sonido físico
' - hacer algún movimiento, si es necesario
' - añadir alguna puntuación
' - encender/apagar una luz
' - hacer algún chequeo para ver si el jugador ha completado algo
' *********************************************************************

' Slingshots
Dim LStep, RStep

Sub LeftSlingShot_Slingshot
    If Tilted Then Exit Sub
    PlaySoundAt SoundFXDOF("fx_slingshot",109,DOFPulse,DOFcontactors), Lemk
    DOF 111, DOFPulse
    LeftSling4.Visible = 1
    Lemk.RotX = 26
    LStep = 0
    LeftSlingShot.TimerEnabled = True
    ' add points
    AddScore 10
' some effect?
End Sub

Sub LeftSlingShot_Timer
    Select Case LStep
        Case 1:LeftSLing4.Visible = 0:LeftSLing3.Visible = 1:Lemk.RotX = 14
        Case 2:LeftSLing3.Visible = 0:LeftSLing2.Visible = 1:Lemk.RotX = 2
        Case 3:LeftSLing2.Visible = 0:Lemk.RotX = -20:LeftSlingShot.TimerEnabled = 0
    End Select
    LStep = LStep + 1
End Sub

Sub RightSlingShot_Slingshot
    If Tilted Then Exit Sub
    PlaySoundAt SoundFXDOF("fx_slingshot",110,DOFPulse,DOFcontactors), Remk
    DOF 112, DOFPulse
    RightSling4.Visible = 1
    Remk.RotX = 26
    RStep = 0
    RightSlingShot.TimerEnabled = True
    ' add points
    AddScore 10
' add effect?
End Sub

Sub RightSlingShot_Timer
    Select Case RStep
        Case 1:RightSLing4.Visible = 0:RightSLing3.Visible = 1:Remk.RotX = 14
        Case 2:RightSLing3.Visible = 0:RightSLing2.Visible = 1:Remk.RotX = 2
        Case 3:RightSLing2.Visible = 0:Remk.RotX = -20:RightSlingShot.TimerEnabled = 0
    End Select
    RStep = RStep + 1
End Sub

'*********
' Bumpers
'*********

Sub Bumper001_Hit
    If Tilted Then Exit Sub
    PlaySoundAt SoundFXDOF("fx_Bumper",103,DOFPulse,DOFContactors), bumper001
    DOF 106, DOFPulse
    ' añade algunos puntos
    AddScore 1 + 9 * LiBumper1a.State
    AddYBonus 1
    RotateLights
End Sub

Sub Bumper002_Hit
    If Tilted Then Exit Sub
    PlaySoundAt SoundFXDOF("fx_Bumper",104,DOFPulse,DOFContactors), bumper002
    DOF 107, DOFPulse
    ' añade algunos puntos
    AddScore 1 + 9 * LiBumper1a.State
    AddYBonus 1
    AddRBonus 1
End Sub

Sub Bumper003_Hit
    If Tilted Then Exit Sub
    PlaySoundAt SoundFXDOF("fx_Bumper",105,DOFPulse,DOFContactors), bumper003
    DOF 108, DOFPulse
    ' añade algunos puntos
    AddScore 1 + 9 * LiBumper3a.State
    AddRBonus 1
    RotateLights
End Sub

'*****************
'     Pasillos
'*****************

'top
Sub Trigger003_Hit
    DOF 120, DOFPulse
    PlaySoundAt "fx_sensor", Trigger003
    If Tilted Then Exit Sub
    AddScore 10
    If DoubleBonusStep = 1 Then
        light023.State = 0
        DoubleBonusStep = 2
    End If
    LiBumper1a.State = 1
    LiBumper1b.State = 1
End Sub

Sub Trigger004_Hit
    DOF 121, DOFPulse
    PlaySoundAt "fx_sensor", Trigger004
    If Tilted Then Exit Sub
    AddScore 100
    If DoubleBonusStep = 2 Then
        light024.State = 0
        DoubleBonusStep = 3
        light026.State = 1
        DoubleBonus = 2
    End If
    LiBumper2a.State = 1
    LiBumper2b.State = 1
End Sub

Sub Trigger005_Hit
    DOF 122, DOFPulse
    PlaySoundAt "fx_sensor", Trigger005
    If Tilted Then Exit Sub
    AddScore 10
    If DoubleBonusStep = 0 Then
        light025.State = 0
        DoubleBonusStep = 1
    End If
    LiBumper3a.State = 1
    LiBumper3b.State = 1
End Sub

' lado izquierdo
Sub Trigger001_Hit
    DOF 117, DOFPulse
    PlaySoundAt "fx_sensor", Trigger001
    If Tilted Then Exit Sub
    AddScore 5
    AddYBonus 5
End Sub

'lado derecho
Sub Trigger002_Hit
    DOF 118, DOFPulse
    PlaySoundAt "fx_sensor", Trigger002
    If Tilted Then Exit Sub
    AddScore 5
    AddRBonus 5
End Sub

'************************
'       Botones
'************************

Sub Trigger006_Hit
    DOF 116, DOFPulse
    PlaySoundAt "fx_sensor", Trigger006
    If Tilted Then Exit Sub
    AddScore 1 + 9 * light027.State
    AddYBonus 1
    RotateLights
End Sub

Sub Trigger007_Hit
    DOF 116, DOFPulse
    PlaySoundAt "fx_sensor", Trigger007
    If Tilted Then Exit Sub
    AddScore 1 + 9 * light028.State
    AddRBonus 1
    RotateLights
End Sub

'************************
'       Agujeros
'************************

Sub HoleTrigger_Hit 'agujero central
    DOF 115, DOFPulse
    If NOT Tilted Then
        If light026.State Then AwardSpecial
        YBonusEnabled = True
        RBonusEnabled = True
        BallinCkicker = True
        LiBumper1a.State = 0
        LiBumper1b.State = 0
        LiBumper2a.State = 0
        LiBumper2b.State = 0
        LiBumper3a.State = 0
        LiBumper3b.State = 0
        StartBonusCount
    End If
End Sub

Sub Kicker002_Hit 'yellow
    PlaySoundAt "fx_kicker_enter", Kicker002
    BallinYkicker = True
    If NOT Tilted Then
        YBonusEnabled = True
        StartBonusCount
    Else
        KickOutKickers
    End If
End Sub

Sub Kicker009_Hit 'red
    PlaySoundAt "fx_kicker_enter", Kicker009
    BallinRkicker = True
    If NOT Tilted Then
        RBonusEnabled = True
        StartBonusCount
    Else
        KickOutKickers
    End If
End Sub

Sub KickOutKickers
    If BallinYkicker Then
        BallinYkicker = False
        YBonusEnabled = False
        vpmtimer.addtimer 1000, "DOF 119, DOFPulse:DOF 124, DOFPulse:PlaySoundAt SoundFX(""fx_kicker"",DOFContactors),kicker002: kicker002.kick 176,12 '"
    End IF
    If BallinRkicker Then
        BallinRkicker = False
        RBonusEnabled = False
        vpmtimer.addtimer 1000, "DOF 119, DOFPulse:DOF 124, DOFPulse:PlaySoundAt SoundFX(""fx_kicker"",DOFContactors),kicker009: kicker009.kick 184,12 '"
    End IF
End Sub

'*****************
' Rubbers - Gomas
'*****************

Dim Rub003, Rub004, Rub023, Rub005, Rub007, Rub008, Rub009, Rub011, Rub012, Rub010

Sub RubberBand003_Hit
    If Tilted then Exit Sub
    AddScore 1
    Rub003 = 1:RubberBand003_Timer
    RotateLights
End Sub
Sub RubberBand003_Timer
    Select Case Rub003
        Case 1:r029.Visible = 0:r030.Visible = 1:RubberBand003.TimerEnabled = 1
        Case 2:r030.Visible = 0:r031.Visible = 1
        Case 3:r031.Visible = 0:r029.Visible = 1:RubberBand003.TimerEnabled = 0
    End Select
    Rub003 = Rub003 + 1
End Sub

Sub RubberBand004_Hit
    If Tilted then Exit Sub
    AddScore 1
    Rub004 = 1:RubberBand004_Timer
    RotateLights
End Sub
Sub RubberBand004_Timer
    Select Case Rub004
        Case 1:r032.Visible = 0:r033.Visible = 1:RubberBand004.TimerEnabled = 1
        Case 2:r033.Visible = 0:r034.Visible = 1
        Case 3:r034.Visible = 0:r032.Visible = 1:RubberBand004.TimerEnabled = 0
    End Select
    Rub004 = Rub004 + 1
End Sub

Sub RubberBand023_Hit
    If Tilted then Exit Sub
    AddScore 1
    Rub023 = 1:RubberBand023_Timer
    RotateLights
End Sub
Sub RubberBand023_Timer
    Select Case Rub023
        Case 1:r026.Visible = 0:r027.Visible = 1:RubberBand023.TimerEnabled = 1
        Case 2:r027.Visible = 0:r028.Visible = 1
        Case 3:r028.Visible = 0:r026.Visible = 1:RubberBand023.TimerEnabled = 0
    End Select
    Rub023 = Rub023 + 1
End Sub

Sub RubberBand005_Hit
    If Tilted then Exit Sub
    AddScore 1
    Rub005 = 1:RubberBand005_Timer
    RotateLights
End Sub
Sub RubberBand005_Timer
    Select Case Rub005
        Case 1:r035.Visible = 0:r036.Visible = 1:RubberBand005.TimerEnabled = 1
        Case 2:r036.Visible = 0:r037.Visible = 1
        Case 3:r037.Visible = 0:r035.Visible = 1:RubberBand005.TimerEnabled = 0
    End Select
    Rub005 = Rub005 + 1
End Sub

Sub RubberBand007_Hit
    If Tilted then Exit Sub
    AddScore 1 + 9 * Gi022.State
    Rub007 = 1:RubberBand007_Timer
End Sub
Sub RubberBand007_Timer
    Select Case Rub007
        Case 1:r007.Visible = 0:r008.Visible = 1:RubberBand007.TimerEnabled = 1
        Case 2:r008.Visible = 0:r009.Visible = 1
        Case 3:r009.Visible = 0:r007.Visible = 1:RubberBand007.TimerEnabled = 0
    End Select
    Rub007 = Rub007 + 1
End Sub

Sub RubberBand008_Hit
    If Tilted then Exit Sub
    AddScore 1 + 9 * Gi020.State
    Rub008 = 1:RubberBand008_Timer
End Sub
Sub RubberBand008_Timer
    Select Case Rub008
        Case 1:r022.Visible = 0:r023.Visible = 1:RubberBand008.TimerEnabled = 1
        Case 2:r023.Visible = 0:r024.Visible = 1
        Case 3:r024.Visible = 0:r022.Visible = 1:RubberBand008.TimerEnabled = 0
    End Select
    Rub008 = Rub008 + 1
End Sub

Sub RubberBand009_Hit
    If Tilted then Exit Sub
    AddScore 1
    Rub009 = 1:RubberBand009_Timer
End Sub
Sub RubberBand009_Timer
    Select Case Rub009
        Case 1:r010.Visible = 0:r011.Visible = 1:RubberBand009.TimerEnabled = 1
        Case 2:r011.Visible = 0:r012.Visible = 1
        Case 3:r012.Visible = 0:r010.Visible = 1:RubberBand009.TimerEnabled = 0
    End Select
    Rub009 = Rub009 + 1
End Sub

Sub RubberBand011_Hit
    If Tilted then Exit Sub
    AddScore 1
    Rub011 = 1:RubberBand011_Timer
End Sub
Sub RubberBand011_Timer
    Select Case Rub011
        Case 1:r013.Visible = 0:r014.Visible = 1:RubberBand011.TimerEnabled = 1
        Case 2:r014.Visible = 0:r015.Visible = 1
        Case 3:r015.Visible = 0:r013.Visible = 1:RubberBand011.TimerEnabled = 0
    End Select
    Rub011 = Rub011 + 1
End Sub

Sub RubberBand012_Hit
    If Tilted then Exit Sub
    AddScore 1
    Rub012 = 1:RubberBand012_Timer
End Sub
Sub RubberBand012_Timer
    Select Case Rub012
        Case 1:r016.Visible = 0:r017.Visible = 1:RubberBand012.TimerEnabled = 1
        Case 2:r017.Visible = 0:r018.Visible = 1
        Case 3:r018.Visible = 0:r016.Visible = 1:RubberBand012.TimerEnabled = 0
    End Select
    Rub012 = Rub012 + 1
End Sub

Sub RubberBand010_Hit
    If Tilted then Exit Sub
    AddScore 1
    Rub010 = 1:RubberBand010_Timer
End Sub
Sub RubberBand010_Timer
    Select Case Rub010
        Case 1:r019.Visible = 0:r020.Visible = 1:RubberBand010.TimerEnabled = 1
        Case 2:r020.Visible = 0:r021.Visible = 1
        Case 3:r021.Visible = 0:r019.Visible = 1:RubberBand010.TimerEnabled = 0
    End Select
    Rub010 = Rub010 + 1
End Sub

'***********************
' Exchange lit lights
'***********************

Sub RotateLights
    Select case LightsState
        Case 0:light027.State = 1:light028.State = 0:light011.State = 1:light022.State = 0:gi021.State = 1:gi022.State = 1:gi019.State = 0:gi020.State = 0
        Case 1:light027.State = 0:light028.State = 1:light011.State = 0:light022.State = 1:gi021.State = 0:gi022.State = 0:gi019.State = 1:gi020.State = 1
    End Select
    LightsState = ABS(LightsState - 1)
End Sub

'************************
'    Bolas del apron
'************************
' Las bolas del apron
Sub BallsRelease
    kicker003.enabled = 0
    kicker004.enabled = 0
    kicker005.enabled = 0
    kicker006.enabled = 0
    kicker007.enabled = 0
    kicker003.kick 90, 3
    kicker004.kick 90, 3
    kicker005.kick 90, 3
    kicker006.kick 90, 3
    kicker007.kick 90, 3
End Sub

Sub BallsInit
    kicker003.createball
    kicker004.createball
    kicker005.createball
    kicker006.createball
    kicker007.createball
End Sub

Sub kicker008_Hit
    PlaySoundAt "fx_hole_enter", kicker008
    Me.destroyball
End sub

Sub kicker007_hit:kicker006.Enabled = 1:End Sub
Sub kicker006_hit:kicker005.Enabled = 1:End Sub
Sub kicker005_hit:kicker004.Enabled = 1:End Sub
Sub kicker004_hit:kicker003.Enabled = 1:End Sub

' ============================================================================================
' GNMOD - Multiple High Score Display and Collection
' changed ramps by flashers, jpsalas
' ============================================================================================

Dim EnteringInitials ' Normally zero, set to non-zero to enter initials
EnteringInitials = False
Dim ScoreChecker
ScoreChecker = 0
Dim CheckAllScores
CheckAllScores = 0
Dim sortscores(4)
Dim sortplayers(4)

Dim PlungerPulled
PlungerPulled = 0

Dim SelectedChar   ' character under the "cursor" when entering initials

Dim HSTimerCount   ' Pass counter For HS timer, scores are cycled by the timer
HSTimerCount = 5   ' Timer is initially enabled, it'll wrap from 5 to 1 when it's displayed

Dim InitialString  ' the string holding the player's initials as they're entered

Dim AlphaString    ' A-Z, 0-9, space (_) and backspace (<)
Dim AlphaStringPos ' pointer to AlphaString, move Forward and backward with flipper keys
AlphaString = "ABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789_<"

Dim HSNewHigh      ' The new score to be recorded

Dim HSScore(5)     ' High Scores read in from config file
Dim HSName(5)      ' High Score Initials read in from config file

' default high scores, remove this when the scores are available from the config file
HSScore(1) = 750
HSScore(2) = 700
HSScore(3) = 600
HSScore(4) = 550
HSScore(5) = 500

HSName(1) = "AAA"
HSName(2) = "ZZZ"
HSName(3) = "XXX"
HSName(4) = "ABC"
HSName(5) = "BBB"

Sub HighScoreTimer_Timer
    If EnteringInitials then
        If HSTimerCount = 1 then
            SetHSLine 3, InitialString & MID(AlphaString, AlphaStringPos, 1)
            HSTimerCount = 2
        Else
            SetHSLine 3, InitialString
            HSTimerCount = 1
        End If
    ElseIf bGameInPlay then
        SetHSLine 1, "HIGH SCORE1"
        SetHSLine 2, HSScore(1)
        SetHSLine 3, HSName(1)
        HSTimerCount = 5 ' set so the highest score will show after the game is over
        HighScoreTimer.enabled = false
    ElseIf CheckAllScores then
        NewHighScore sortscores(ScoreChecker), sortplayers(ScoreChecker)
    Else
        ' cycle through high scores
        HighScoreTimer.interval = 2000
        HSTimerCount = HSTimerCount + 1
        If HsTimerCount> 5 then
            HSTimerCount = 1
        End If
        SetHSLine 1, "HIGH SCORE" + FormatNumber(HSTimerCount, 0)
        SetHSLine 2, HSScore(HSTimerCount)
        SetHSLine 3, HSName(HSTimerCount)
    End If
End Sub

Function GetHSChar(String, Index)
    Dim ThisChar
    Dim FileName
    ThisChar = Mid(String, Index, 1)
    FileName = "PostIt"
    If ThisChar = " " or ThisChar = "" then
        FileName = FileName & "BL"
    ElseIf ThisChar = "<" then
        FileName = FileName & "LT"
    ElseIf ThisChar = "_" then
        FileName = FileName & "SP"
    Else
        FileName = FileName & ThisChar
    End If
    GetHSChar = FileName
End Function

Sub SetHsLine(LineNo, String)
    Dim Letter
    Dim ThisDigit
    Dim ThisChar
    Dim StrLen
    Dim LetterLine
    Dim Index
    Dim StartHSArray
    Dim EndHSArray
    Dim LetterName
    Dim xFor
    StartHSArray = array(0, 1, 12, 22)
    EndHSArray = array(0, 11, 21, 31)
    StrLen = len(string)
    Index = 1

    For xFor = StartHSArray(LineNo)to EndHSArray(LineNo)
        Eval("HS" &xFor).imageA = GetHSChar(String, Index)
        Index = Index + 1
    Next
End Sub

Sub NewHighScore(NewScore, PlayNum)
    If NewScore> HSScore(5)then
        HighScoreTimer.interval = 500
        HSTimerCount = 1
        AlphaStringPos = 1      ' start with first character "A"
        EnteringInitials = true ' intercept the control keys while entering initials
        InitialString = ""      ' initials entered so far, initialize to empty
        SetHSLine 1, "PLAYER " + FormatNumber(PlayNum, 0)
        SetHSLine 2, "ENTER NAME"
        SetHSLine 3, MID(AlphaString, AlphaStringPos, 1)
        HSNewHigh = NewScore
    'AwardSpecial
    End If
    ScoreChecker = ScoreChecker-1
    If ScoreChecker = 0 then
        CheckAllScores = 0
    End If
End Sub

Sub CollectInitials(keycode)
    Dim i
    If keycode = LeftFlipperKey Then
        ' back up to previous character
        AlphaStringPos = AlphaStringPos - 1
        If AlphaStringPos <1 then
            AlphaStringPos = len(AlphaString) ' handle wrap from beginning to End
            If InitialString = "" then
                ' Skip the backspace If there are no characters to backspace over
                AlphaStringPos = AlphaStringPos - 1
            End If
        End If
        SetHSLine 3, InitialString & MID(AlphaString, AlphaStringPos, 1)
        PlaySound "fx_Previous"
    ElseIf keycode = RightFlipperKey Then
        ' advance to Next character
        AlphaStringPos = AlphaStringPos + 1
        If AlphaStringPos> len(AlphaString)or(AlphaStringPos = len(AlphaString)and InitialString = "")then
            ' Skip the backspace If there are no characters to backspace over
            AlphaStringPos = 1
        End If
        SetHSLine 3, InitialString & MID(AlphaString, AlphaStringPos, 1)
        PlaySound "fx_Next"
    ElseIf keycode = StartGameKey or keycode = PlungerKey Then
        SelectedChar = MID(AlphaString, AlphaStringPos, 1)
        If SelectedChar = "_" then
            InitialString = InitialString & " "
            PlaySound("fx_Esc")
        ElseIf SelectedChar = "<" then
            InitialString = MID(InitialString, 1, len(InitialString)- 1)
            If len(InitialString) = 0 then
                ' If there are no more characters to back over, don't leave the < displayed
                AlphaStringPos = 1
            End If
            PlaySound("fx_Esc")
        Else
            InitialString = InitialString & SelectedChar
            PlaySound("fx_Enter")
        End If
        If len(InitialString) <3 then
            SetHSLine 3, InitialString & SelectedChar
        End If
    End If
    If len(InitialString) = 3 then
        ' save the score
        For i = 5 to 1 step -1
            If i = 1 or(HSNewHigh> HSScore(i)and HSNewHigh <= HSScore(i - 1))then
                ' Replace the score at this location
                If i <5 then
                    HSScore(i + 1) = HSScore(i)
                    HSName(i + 1) = HSName(i)
                End If
                EnteringInitials = False
                HSScore(i) = HSNewHigh
                HSName(i) = InitialString
                HSTimerCount = 5
                HighScoreTimer_Timer
                HighScoreTimer.interval = 2000
                PlaySound("fx_Bong")
                Exit Sub
            ElseIf i <5 then
                ' move the score in this slot down by 1, it's been exceeded by the new score
                HSScore(i + 1) = HSScore(i)
                HSName(i + 1) = HSName(i)
            End If
        Next
    End If
End Sub

Sub Trigger008_Hit
    DOF 114, DOFPulse
End Sub
' End GNMOD

'*********************************
' Table Options F12 User Options
'*********************************
' Table1.Option arguments are:
' - option name, minimum value, maximum value, step between valid values, default value, unit (0=None, 1=Percent), an optional array of literal strings

Dim LUTImage, BallsPerGame, AutoBallLoading

Sub Table1_OptionEvent(ByVal eventId)
    Dim x, y

    'LUT
    LutImage = Table1.Option("Select LUT", 0, 21, 1, 0, 0, Array("Normal 0", "Normal 1", "Normal 2", "Normal 3", "Normal 4", "Normal 5", "Normal 6", "Normal 7", "Normal 8", "Normal 9", "Normal 10", _
        "Warm 0", "Warm 1", "Warm 2", "Warm 3", "Warm 4", "Warm 5", "Warm 6", "Warm 7", "Warm 8", "Warm 9", "Warm 10") )
    UpdateLUT

    ' Cabinet rails
    x = Table1.Option("Cabinet Rails", 0, 1, 1, 1, 0, Array("Hide", "Show") )
    For each y in aRails:y.visible = x:next

    ' Balls per Game
    x = Table1.Option("Balls per Game", 0, 1, 1, 1, 0, Array("3 Balls", "5 Balls") )
    If x = 1 Then BallsPerGame = 5 Else BallsPerGame = 3

    ' Auto Ball Loading
    x = Table1.Option("Auto Ball Loading", 0, 1, 1, 1, 0, Array("Yes", "No") )
    If x = 1 Then AutoBallLoading = False Else AutoBallLoading = True
End Sub

Sub UpdateLUT
    Select Case LutImage
        Case 0:table1.ColorGradeImage = "LUT0"
        Case 1:table1.ColorGradeImage = "LUT1"
        Case 2:table1.ColorGradeImage = "LUT2"
        Case 3:table1.ColorGradeImage = "LUT3"
        Case 4:table1.ColorGradeImage = "LUT4"
        Case 5:table1.ColorGradeImage = "LUT5"
        Case 6:table1.ColorGradeImage = "LUT6"
        Case 7:table1.ColorGradeImage = "LUT7"
        Case 8:table1.ColorGradeImage = "LUT8"
        Case 9:table1.ColorGradeImage = "LUT9"
        Case 10:table1.ColorGradeImage = "LUT10"
        Case 11:table1.ColorGradeImage = "LUT Warm 0"
        Case 12:table1.ColorGradeImage = "LUT Warm 1"
        Case 13:table1.ColorGradeImage = "LUT Warm 2"
        Case 14:table1.ColorGradeImage = "LUT Warm 3"
        Case 15:table1.ColorGradeImage = "LUT Warm 4"
        Case 16:table1.ColorGradeImage = "LUT Warm 5"
        Case 17:table1.ColorGradeImage = "LUT Warm 6"
        Case 18:table1.ColorGradeImage = "LUT Warm 7"
        Case 19:table1.ColorGradeImage = "LUT Warm 8"
        Case 20:table1.ColorGradeImage = "LUT Warm 9"
        Case 21:table1.ColorGradeImage = "LUT Warm 10"
    End Select
End Sub
