'                                                                                                    
'                                                                                                    
'                     .ri                                                                            
'                  M@@@B@BY7r                                                                        
'                  uB@B@B@B@B5                  i@@:           .rjuSXOMBM@B@B                        
'                  r@B@B@B@B@B:               kB@B@U       iNB@B@B@B@B@B@B@B,                        
'                  ,B@B@B@B@@@B             :@B@B@B     7@@@B@B@B@@@B@B@@@B@B@Oq;.                   
'                   @B@@@B@@@B@Bi          r@@@B@B   :@B@B@B@B@B@B@@@B@@@B@B@B@B@B7J1                
'                   B@B@B@B@B@B@Bk        i@B@B@B  2@@B@@@B@ui.      iB@B@B@B@B@B@B@B                
'                   @B@B@B@B@B@B@B@      N@B@B@8 ,@B@B@@@j             vO@@B@B@@@B@7                 
'                   @@B@B@@@B@B@B@B@    S@B@B@M 1@@@B@BJ                  L@B@B@B@@                  
'                   @B@B@B@B@B@B@B@B@. L@B@@@M P@@@B@B,                   S@@B@@@BG                  
'                   B@B@B@@@B .@B@B@B@5@B@@@B L@B@@@BE                  r@B@@@B@BZ                   
'                   @B@B@B@B@   MB@@@@@B@B@B: @@@B@B@P                :@@B@@@B@@2                    
'                   B@B@B@B@@.   0B@@@B@B@B@ .B@B@B@B@              .@B@B@B@B@B:                     
'                  :@@@B@B@B@     jB@@@B@B@Y  @B@B@B@BP          .u@@B@B@B@B@B                       
'                  j@@B@B@@27      .B@B@B@B    MB@B@B@B2:,.,:Jq@@@B@B@B@B@@Z,                        
'                  r@B@@@B          u@@@@Br     r@@B@@@B@B@B@B@B@B@B@B@BM;                           
'                   P8U.B.           7U.           .ivEOB@B@B@B@B@Z7:                                
'            777U71E  u7GSNqE8Bi                                                                     
'          @B@@@B@B@B@B@B@@@B@OMY       :Uvi.        :2NG@B            .:7LFPNPkJr.                  
'          B@B@B@B@@@B@PUjj7.  8@1rj1u5O@B@B@B@5   N@B@B@B@,     Y@B@B@@@B@@@B@B@B@B@2               
'          @B@@@@@@@         E@@B@B@B@B@B@B@BX,   B@B@B@B@B .uZ@B@@@B@B@B@B@B@B@@@B@B@M              
'          B@B@B@B@u        B@@B@B@@@B@B@Pr      k@B@B@B@BU @B@B@B@B@B@B@G57i,::OB@@@BZ              
'         .@B@B@@@B7  .:.  @B@B@B@@             7@@@B@B@B@B  .:iB@B@B@B         M@B@BS               
'         ;B@B@@@B@B@B@B@vrB@B@B@@@            r@B@B@B@B@@@i    MB@B@@@      :BB@B@B,                
'         7@B@B@B@B@B@@@B 1@B@@@B@B@B@B@BOj   7@B@@@@@B@B@B@    .@B@B@BM,.rMB@B@B@,                  
'         XB@B@@@B@BOSjr. i@@B@B@B@B@B@@@B@  S@B@B@B@B@@@B@@M    X@B@@@B@B@B@B@k                     
'         E@B@B@B@        ,@B@@@B@B@@@@E7   B@B@B@B@B@B@B@B@BN   .B@B@@@B@@@B@B0                     
'         @B@B@@@B7        B@B@B@B:        B@B@B@B@B  MB@@@B@B7   @B@B@@@B@B@B@B@M:                  
'         B@B@B@@@M        FB@B@B@        M@B@B@B@B@JvE@B@B@@@@:  B@B@B@B@B@B@@@B@BE                 
'         @B@B@B@@@         BB@B@B50GqqSuP@B@B@B@B@B@@@@@@@B@@@Br @@@B@B@G@@@B@B@B@B@L               
'         B@@@B@B@0          MB@B@@@B@@@B@B@B@B@B@B:;7i :@B@B@B@B r@@@B@B .@B@B@@@B@B@B              
'         rrBB@@@Bk           @B8M@B@B@@@B@B@B@B@B       i@B@B@B@O.B@B@B@5 ,@B@B@B@B@@@Bv            
'            @B@B@M                 ,rvjB@B@B@B@B,        7@B@B@B@q@B@B@B@   OB@B@B@B@B@@M           
'            7MS@B@i                     B@B@OBi           J@B@B@@@B@B@B@@1   iB@B@B@B@B@B@.         
'                .77                       L                J@@@B@BO@@B@B@B     u@@@B@B@B@@@:        
'                                                            u@BB1: 8B@B  :       qB@B@B@B@B@        
'                                                                    ..             u@@@B@B@B@r      
'                                                                                     vB@BOU@B@S     
'                                                                                       vG ,iBi      
'                                                                                        .@Z         
'                                                                                         :,         
'No Fear - Dangerous Sports - IPDB No. 2852
'Â© Williams 1995
'VPX recreation by ninuzzu
'Thanks to the VPDev Team for the amazing VPX!

Option Explicit
Randomize

'******************************************************************************************
'* CUSTOMIZABLE OPTIONS 			
'******************************************************************************************

'***********	Flippers Type 	('0=yellow/black, 1=yellow/red, 2=glowbats)
Const FlippersType = 0

'***********	Enable Custom Cabinet Side Artwork	('0=disabled, 1=enabled)
Const CustomCabSides = 0

'***********	Enable Stronger Jump Ramp Magnets ('0=disabled, 1=enabled)
Const StrongerMagnets = 0


'************************************************************************
'						 INIT VPM
'************************************************************************

Const BallSize = 50
Const BallMass = 1.25

' Load controller.vbs to handle VPinMAME.Controller and B2S.Server
On Error Resume Next
ExecuteGlobal GetTextFile("controller.vbs")
If Err Then MsgBox "You need the controller.vbs in order to run this table, available in the vp10 package"
On Error Goto 0

' Internal DMD in Desktop Mode, using a textbox (must be called before LoadVPM)
Dim DesktopMode:DesktopMode = Table1.ShowDT
Dim UseVPMDMD:UseVPMDMD = DesktopMode

' Standard Options
Const UseVPMModSol = 1, UseSolenoids = 2, UseLamps = 0, UseSync = 0, HandleMech = 0, SSolenoidOn = "fx_solon", SSolenoidOff = "", SCoin = "fx_Coin"

' Rom Name
Const cGameName = "nf_23x"

LoadVPM "03000000", "WPC.VBS", 3.50

'************************************************************************
'						 INIT TABLE
'************************************************************************

Dim bsTrough, bsLSaucer, bsRPopper, dtDropT, plungerIM, kickbackIM, MagnetSpeed

Sub Table1_Init
	vpmInit Me 
	With Controller      
		.GameName = cGameName
		If Err Then MsgBox "Can't start Game " & cGameName & vbNewLine & Err.Description:Exit Sub
		.SplashInfoLine = "No Fear - Dangerous Sports (Williams 1995)"
		.HandleKeyboard = 0
		.ShowTitle = 0
		.ShowDMDOnly = 1
		.ShowFrame = 0
		.HandleMechanics = 0
		.Hidden = DesktopMode	'Hide VPM DMD in Desktop Mode
		On Error Resume Next
		.Run GetPlayerHWnd
		If Err Then MsgBox Err.Description
		On Error Goto 0
		.Switch(22) = 1			'close coin door
		.Switch(24) = 1			'always closed
	End With

    ' Main Timer init
    PinMAMETimer.Interval = PinMAMEInterval
    PinMAMETimer.Enabled = 1

    ' Nudging
    vpmNudge.TiltSwitch = 14
    vpmNudge.Sensitivity = 5
    vpmNudge.TiltObj = Array(sw27, sw28)

    ' Trough
    Set bsTrough = New cvpmTrough
    With bsTrough
		.size = 4
		.initSwitches Array(32, 33, 34, 35)
		.Initexit BallRelease, 60, 5
		.InitEntrySounds "fx_drain", "", ""
		.InitExitSounds SoundFX(SSolenoidOn,DOFContactors), SoundFX("fx_ballrel",DOFContactors)
		.Balls = 3
		.CreateEvents "bsTrough", Drain
    End With

    ' Right Popper
    Set bsRPopper = New cvpmTrough
	With bsRPopper
		.size = 2
		.InitSwitches Array(41,42)
		.Initexit sw42, 180, 8
		.InitEntrySounds "fx_kicker_catch", "", ""
		.InitExitSounds SoundFX(SSolenoidOn,DOFContactors),SoundFX("fx_kicker_out",DOFContactors)
		.Balls = 0
		.CreateEvents "bsRPopper", sw41
	End With

    ' DropTarget
    Set dtDropT = new cvpmDropTarget
	With dtDropT
		.InitDrop sw51, 51
		.Initsnd SoundFX("fx_DropTargetDown",DOFDropTargets), SoundFX("fx_DropTargetUp",DOFDropTargets)
	End With

	' Left Eject
	Set bsLSaucer = New cvpmSaucer
	With bsLSaucer
		.InitKicker sw61, 61, 180, 10, 1
        .InitExitVariance 1, 1
		.InitSounds "fx_saucerHit", SoundFX(SSolenoidOn,DOFContactors), SoundFX("fx_saucer_exit",DOFContactors)
		.CreateEvents "bsLSaucer", sw61
	End With

	' Auto Plunger
	Set plungerIM = New cvpmImpulseP
	With plungerIM
		.InitImpulseP sw15, 65, 0.75
		.Switch 15
		.Random 0.05
		.InitExitSnd SoundFX("AutoPlunger",DOFContactors), SoundFX("AutoPlunger",DOFContactors)
		.CreateEvents "plungerIM"
	End With

	' Kickback
	Set kickbackIM = New cvpmImpulseP
	With kickbackIM
		.InitImpulseP sw25, 65, 0.5
		.Random 0.05
		.InitExitSnd SoundFX("AutoPlunger",DOFContactors), SoundFX("AutoPlunger",DOFContactors)
	End With

	InitLights

	'Init Options
	CabRails.visible = DesktopMode

	Select Case CustomCabSides
		Case 0
			Cabside.image = "Cabside": Cabside.material = "CabSides"
		Case 1
			Cabside.image = "Cabinet1": Cabside.material = "Diffuse no alpha"
	End Select

	Select Case FlippersType
		Case 0
			LeftFlipperP.visible=1:RightFlipperP.visible=1:RightFlipperP1.visible=1
			LeftFlipperP.image="flippers":RightFlipperP.image="flippers":RightFlipperP1.image="flippers"
			LeftFlipperSh.visible = 1 : RightFlipperSh.visible = 1
			glowbatleft.visible = 0 : glowbatright.visible = 0 : glowbatright1.visible = 0
		Case 1
			LeftFlipperP.visible=1:RightFlipperP.visible=1:RightFlipperP1.visible=1
			LeftFlipperP.image="flippers2":RightFlipperP.image="flippers2":RightFlipperP1.image="flippers2"
			LeftFlipperSh.visible = 1 : RightFlipperSh.visible = 1
			glowbatleft.visible = 0 : glowbatright.visible = 0 : glowbatright1.visible = 0
		Case 2
			LeftFlipperP.visible=0:RightFlipperP.visible=0:RightFlipperP1.visible=0
			LeftFlipperSh.visible = 0 : RightFlipperSh.visible = 0
			glowbatleft.visible = 1 : glowbatright.visible = 1 : glowbatright1.visible = 1
	End Select

	Select Case StrongerMagnets
		Case 0
			MagnetSpeed = 20
		Case 1
			MagnetSpeed = 30
	End Select

End Sub

'************************************************************************
'							KEYS
'************************************************************************

Sub Table1_KeyDown(ByVal Keycode)
	If keycode = keyFront Then Controller.Switch(23) = 1		'buy-in
	If Keycode = PlungerKey Then Controller.Switch(11) = 1
	If keycode = LeftTiltKey Then Nudge 90, 5:PlaySoundAt SoundFX("fx_nudge",0),sw25
	If keycode = RightTiltKey Then Nudge 270, 5:PlaySoundAt SoundFX("fx_nudge",0),Drain
	If keycode = CenterTiltKey Then Nudge 0, 6:PlaySoundAt SoundFX("fx_nudge",0),sw17
    If vpmKeyDown(keycode) Then Exit Sub
End Sub

Sub Table1_KeyUp(ByVal Keycode)
	If keycode = keyFront Then Controller.Switch(23) = 0		'buy-in
	If Keycode = PlungerKey Then Controller.Switch(11) = 0
	If vpmKeyUp(keycode) Then Exit Sub
End Sub

Sub Table1_Paused:Controller.Pause = True:End Sub
Sub Table1_unPaused:Controller.Pause = False:End Sub
Sub Table1_exit():Controller.Pause = False:Controller.Stop:End Sub

'*****************************************
'      		General Illumination
'*****************************************
Set GiCallBack2 = GetRef("UpdateGi")

Sub UpdateGi(nr,step)
	Dim ii
	Select Case nr

	Case 0		'Top Playfield
		If step=0 Then
			For each ii in GI_Top:ii.state=0:Next
		Else
			For each ii in GI_Top:ii.state=1:Next
		End If
		For each ii in GI_Top:ii.IntensityScale = 0.125 * step:Next

		If CustomCabSides = 0 Then
			MaterialColor "CabSides", RGB (255 * 0.125 * step, 255 * 0.125 * step, 255 * 0.125 * step)
		End If

		If FlippersType= 2 Then
			If step=0 Then
				GlowBatLightLeft.state = 0 : GlowBatLightRight.state = 0 : GlowBatLightRight1.state = 0
			Else
				GlowBatLightLeft.state = 1 : GlowBatLightRight.state = 1 : GlowBatLightRight1.state = 1
			End If
			glowbatleft.BlendDisableLighting = 0.125 * step : glowbatright.BlendDisableLighting = 0.125 * step : glowbatright1.BlendDisableLighting = 0.125 * step
			GlowBatLightLeft.IntensityScale = 0.125 * step : GlowBatLightRight.IntensityScale = 0.125 * step : GlowBatLightRight1.IntensityScale = 0.125 * step
		End If

		If Step>=7 Then Table1.ColorGradeImage = "ColorGradeEx_8":Else Table1.ColorGradeImage = "ColorGradeEx_" & (step+1):End If

		If step>4 Then DOF 103, DOFOn : Else DOF 103, DOFOff : End If

	Case 1		'Right Playfield
		If step=0 Then
			For each ii in GI_Right:ii.state=0:Next
		Else
			For each ii in GI_Right:ii.state=1:Next
		End If
		For each ii in GI_Right:ii.IntensityScale = 0.125 * step:Next

	Case 2 		'Left Playfield
		If step=0 Then
			For each ii in GI_Left:ii.state=0:Next
		Else
			For each ii in GI_Left:ii.state=1:Next
		End If
		For each ii in GI_Left:ii.IntensityScale = 0.125 * step:Next

	End Select
	If nr=0 AND step=8 AND cAttract=0 Then cAttract=1:cWait=Timer
End Sub

'*****************************************
' 			Lights Mapping
'*****************************************

Dim LampState(200), FadingLevel(200)
Dim FlashSpeedUp(200), FlashSpeedDown(200), FlashMin(200), FlashMax(200), FlashLevel(200)
Dim l83on, l83off
dim MagnetEffect:MagnetEffect = false

Sub InitLights
    Dim x
    For x = 0 to 200
        LampState(x) = 0       	 ' current light state
        FadingLevel(x) = 0       ' current light fading level
        FlashSpeedUp(x) = 0.5    ' faster speed when turning on the flasher
        FlashSpeedDown(x) = 0.35 ' slower speed when turning off the flasher
        FlashMax(x) = 1          ' the maximum value when on, usually 1
        FlashMin(x) = 0          ' the minimum value when off, usually 0
        FlashLevel(x) = 0        ' the intensity of the flashers, usually from 0 to 1
    Next
	UpdateGI 0,0:UpdateGI 1,0:UpdateGI 2,0
	LampTimer.Interval = -1
	LampTimer.Enabled = 1
End Sub

Sub LampTimer_Timer()
    Dim chgLamp, num, chg, ii
    chgLamp = Controller.ChangedLamps
    If Not IsEmpty(chgLamp) Then
        For ii = 0 To UBound(chgLamp)
            LampState(chgLamp(ii, 0) ) = chgLamp(ii, 1)       'keep the real state in an array
        Next
    End If
	UpdateMagnets
    UpdateLamps
End Sub

Sub UpdateMagnets
	Select Case LampState(83)
		Case 0
		l83on=0
		l83off= l83off + 1
		Case 1
		l83off=0
		l83on= l83on + 1
	End Select
	If (l83on=0 AND l83off>100) Then
		If MagnetEffect Then MagnetEffect = False : l83off = 100
	ElseIf (l83off=0 AND l83on>100) Then
		If MagnetEffect Then MagnetEffect = False: l83on = 100
	Else
		If NOT MagnetEffect Then MagnetEffect = True
	End If
End Sub

Sub UpdateLamps
'Inserts
FadeLamp 11, l11
FadeLamp 12, l12
FadeLamp 13, l13
FadeLamp 14, l14
FadeLamp 15, l15
FadeLamp 16, l16
FadeLamp 17, l17
FadeLamp 18, l18
FadeLamp 21, l21
FadeLamp 22, l22
FadeLamp 23, l23
FadeLamp 24, l24
FadeLamp 25, l25
FadeLamp 26, l26
FadeLamp 27, l27
FadeLamp 28, l28
FadeLamp 31, l31
FadeLamp 32, l32
FadeLamp 33, l33
FadeLamp 34, l34
FadeLamp 35, l35
FadeLamp 36, l36
FadeLamp 37, l37

FadeLamp 38, Fl38
FadeLamp 38, Fl38a
FadePrim 38, bulb1, 2

FadeLamp 41, l41
FadeLamp 42, l42
FadeLamp 43, l43
FadeLamp 44, l44
FadeLamp 45, l45
FadeLamp 46, l46
FadeLamp 47, l47
FadeLamp 48, l48
FadeLamp 51, l51
FadeLamp 52, l52
FadeLamp 53, l53
FadeLamp 54, l54
FadeLamp 55, l55
FadeLamp 56, l56
FadeLamp 57, l57
FadeLamp 61, l61
FadeLamp 62, l62
FadeLamp 63, l63
FadeLamp 64, l64
FadeLamp 65, l65
FadeLamp 66, l66
FadeLamp 67, l67
FadeLamp 68, l68
FadeLamp 71, l71
FadeLamp 72, l72
FadeLamp 73, l73
FadeLamp 74, l74
FadeLamp 75, l75
FadeLamp 76, l76
FadeLamp 77, l77
FadeLamp 78, l78
FadeLamp 81, l81

'Skull eyes
FadeLamp 82, F82
FadeLamp 85, F85
SkullEyesLamp 82

FadeLamp 83, FL83
FadeLamp 83, FL83a
FadePrim 83, bulb2, 10

FadeLamp 84, FL84
FadeLamp 84, FL84a
FadePrim 84, bulb3, 100

'FadeLamp 86, l86		'Ball Launch Button (Cabinet)
'FadeLamp 87, l87		'Buy-In Button (Cabinet)
'FadeLamp 88, l88		'Start Button (Cabinet)

'Flashers
FadeModLamp 117, FL17a, 2
FadeModLamp 117, FL17b, 2
FadeModLamp 118, FL18a, 2
FadeModLamp 118, FL18b, 2
FadeModLamp 118, Dome1, 2

FadeModLamp 119, FL19a, 2
FadeModLamp 119, FL19b, 2
FadeModLamp 120, FL20, 2
FadeModLamp 121, Skull, 2
FadeModLamp 121, SkullJaw, 2
FadeModLamp 121, FL21a, 2
FadeModLamp 121, FL21b, 2

FadeModLamp 123, FL23, 2
FadeModLamp 124, FL24a, 2
FadeModLamp 124, FL24b, 2
FadeModLamp 124, Dome2, 2
FadeModLamp 125, FL25a, 2
FadeModLamp 125, FL25b, 2
FadeModLamp 125, FL25c, 2
FadeModLamp 125, FL25d, 2

FadeModLamp 128, FL28, 2

End Sub

' Not Modulated lights and flashers
Sub SkullEyesLamp(nr)
	Select Case LampState(nr)
		Case 0
			Eyes.image = "eyes"
		Case 1
			Eyes.image = "eyes_on"
	End Select
End sub

Sub FadeLamp(nr, object)
	If TypeName(object) = "Light" Then
		Object.State = LampState(nr)
	End If
	If TypeName(object) = "Flasher" Then
		Object.IntensityScale = LampState(nr)
	End If
End Sub 

Sub FadePrim(nr, object, factor)
	Object.BlendDisableLighting = factor * LampState(nr)
End Sub 

' Modulated lights and flashers
Sub SetModLamp(nr, value)
    If value > 0 Then
		LampState(nr) = 1
	Else
		LampState(nr) = 0
	End If
	FadingLevel(nr) = value
End Sub

Sub FadeModLamp(nr, object, factor)
	If TypeName(object) = "Light" Then
		Object.State = LampState(nr)
		Object.IntensityScale = FadingLevel(nr) * factor/255
	End If
	If TypeName(object) = "Flasher" Then
		Object.visible = LampState(nr)
		Object.IntensityScale = FadingLevel(nr) * factor/255
	End If
	If TypeName(object) = "Primitive" Then
		Object.BlendDisableLighting = FadingLevel(nr) * factor/255
	End If
End Sub

'*****************************************
'      		Solenoids Mapping
'*****************************************
SolCallback(1)= "bsRPopper.Solout" 				'1-Right Popper
SolCallback(2)= "SolAutoPlungerIM"				'2-AutoPlunger
'SolCallback(3)= ""								'3-Right Magnet
SolCallback(4)= "SolKickbackIM"					'4-KickBack
'SolCallback(5)= ""								'5-Center Magnet
'SolCallback(6)= ""								'6-Left Magnet
SolCallback(7)= "vpmSolSound SoundFX(""fx_knocker"",DOFKnocker),"		'7-Knocker
SolCallback(8)= "dtDropT.SolDropDown" 			'8-Drop Target Down
'SolCallback(9)= ""								'9-N.U.
'SolCallback(10)= ""						'10-Right Sling
'SolCallback(11)= ""						'11-Left Sling
SolCallback(12)= "dtDropT.SolDropUp"			'12-Drop Target Up
'SolCallback(13)= ""							'13-N.U.
SolCallback(14)= "SolTrough"					'14-Trough
SolCallback(15)= "bsLSaucer.SolOut"				'15-Left Eject
SolCallback(16)= "SolMouth"						'16-Skull Mouth

SolModCallback(17)= "SetModLamp 117,"			'17-Flasher: Flipper Return (x2)
SolModCallback(18)= "SetModLamp 118,"			'18-Flasher: Spinner
SolModCallback(19)= "SetModLamp 119,"			'19-Flasher: No Fear (x2)
SolModCallback(20)= "SetModLamp 120,"			'20-Flasher: Right Ramp
SolModCallback(21)= "SetModLamp 121,"			'21-Flasher: Skull (x2)
SolModCallback(23)= "SetModLamp 123,"			'23-Flasher: Left Ramp
SolModCallback(24)= "SetModLamp 124,"			'24-Flasher: Top Left
SolModCallback(25)= "SetModLamp 125,"			'25-Flasher: AutoFire (x2)
SolModCallback(28)= "SetModLamp 128,"			'28-Flasher: Right Popper

'******************************************
'				FLIPPERS
'******************************************

SolCallback(sLRFlipper) = "SolRFlipper"
SolCallback(sLLFlipper) = "SolLFlipper"

Sub SolLFlipper(Enabled)
	If Enabled Then		 
		PlaySoundAt SoundFX("fx_FlipperUp",DOFFlippers), LeftFlipper
		LeftFlipper.RotateToEnd
	Else
        PlaySoundAt SoundFX("fx_FlipperDown",DOFFlippers), LeftFlipper
        LeftFlipper.RotateToStart
     End If
 End Sub

Sub SolRFlipper(Enabled)
    If Enabled Then
        PlaySoundAt SoundFX("fx_FlipperUp",DOFFlippers), RightFlipper
        RightFlipper.RotateToEnd:RightUpFlipper.RotateToEnd
    Else
        PlaySoundAt SoundFX("fx_FlipperDown",DOFFlippers), RightFlipper
        RightFlipper.RotateToStart:RightUpFlipper.RotateToStart
     End If
 End Sub

'************************************************************************
'						 BALL TROUGH
'************************************************************************
Sub SolTrough(Enabled)
	If Enabled Then
		bsTrough.ExitSol_On
		If BsTrough.Balls Then vpmTimer.PulseSw 31
	End If
End Sub

'************************************************************************
'						 AUTOPLUNGER 
'************************************************************************
Sub SolAutoPlungerIM(Enabled)
	If Enabled Then
		PlungerIM.AutoFire
	End If
End Sub

'************************************************************************
'						 KICKBACK
'************************************************************************
Sub SolKickbackIM(Enabled)
	If Enabled Then
		KickBackIM.AutoFire
	End If
End Sub

'************************************************************************
'						SKULL MOUTH
'************************************************************************

Sub SolMouth(enabled)
	If enabled Then
		SkullJaw.RotX=-15 : PlaySoundAt SoundFX("solenoidOn",DOFContactors), SkullJaw
	Else
		SkullJaw.RotX= 0 : PlaySoundAt SoundFX("solenoidOff", DOFContactors), SkullJaw
	End If
End Sub

'************************************************************************
'      					SWITCHES
'************************************************************************

'Spinner
Sub sw16_spin:vpmtimer.pulsesw 16: PlaySoundAt "fx_spinner", sw16:End Sub

'rollover
Sub sw17_hit:Controller.switch(17) = 1: PlaySoundAt "fx_sensor",ActiveBall:End Sub
Sub sw17_unhit:Controller.switch(17) = 0: End Sub
Sub sw18_hit:Controller.switch(18) = 1: PlaySoundAt "fx_sensor",ActiveBall:End Sub
Sub sw18_unhit:Controller.switch(18) = 0: End Sub

Sub sw25_hit:Controller.switch(25) = 1: PlaySoundAt "fx_sensor",ActiveBall: KickBackIM.AddBall ActiveBall : End Sub
Sub sw25_unhit:Controller.switch(25) = 0: KickBackIM.RemoveBall ActiveBall : End Sub
Sub sw26_hit:Controller.switch(26) = 1: PlaySoundAt "fx_sensor",ActiveBall:End Sub
Sub sw26_unhit:Controller.switch(26) = 0: End Sub

'Slingshots
Dim LStep, RStep

Sub sw27_slingshot
	vpmTimer.PulseSw 27
	PlaySoundAt SoundFX("LeftSlingShot",DOFContactors), ActiveBall
	LSling.Visible = 0: LSling2.Visible = 1: sling1.TransZ = -20: LStep = 0
	Me.TimerInterval = 50: Me.TimerEnabled = 1
End Sub

Sub sw28_slingshot
	vpmTimer.PulseSw 28
	PlaySoundAt SoundFX("RightSlingshot",DOFContactors), ActiveBall
	RSling.Visible = 0: RSling2.Visible = 1: sling2.TransZ = -20: RStep = 0
	Me.TimerInterval = 50: Me.TimerEnabled = 1
End Sub

Sub sw27_Timer
	Select Case LStep
		Case 3:LSLing2.Visible = 0:LSLing1.Visible = 1:sling1.TransZ = -10
		Case 4:LSLing1.Visible = 0:LSLing.Visible = 1:sling1.TransZ = 0:Me.TimerEnabled = 0
	End Select
	LStep = LStep + 1
End Sub

Sub sw28_Timer
	Select Case RStep
		Case 3:RSLing2.Visible = 0:RSLing1.Visible = 1:sling2.TransZ = -10
		Case 4:RSLing1.Visible = 0:RSLing.Visible = 1:sling2.TransZ = 0:Me.TimerEnabled = 0
	End Select
	RStep = RStep + 1
End Sub

'Holes optos
Sub sw37_hit: vpmtimer.pulsesw 37 : Me.TimerInterval=300: Me.TimerEnabled=1: RandomSoundHole:End Sub
Sub sw37_timer: Me.TimerEnabled=0 : PlaySoundAt "fx_subway",sw37:End Sub
Sub sw38_hit: vpmtimer.pulsesw 38 : Me.TimerInterval=300: Me.TimerEnabled=1: RandomSoundHole:End Sub
Sub sw38_timer: Me.TimerEnabled=0 : PlaySoundAt "fx_subway",sw38:End Sub
Sub SubwayStop_Hit():StopSound "fx_subway":End Sub

'Magnet Optos
Dim MagnetSnd:MagnetSnd=0
Sub sw46_hit
	vpmtimer.pulsesw 46
	If MagnetEffect Then
		If MagnetSnd=0 Then MagnetSnd=1 : PlaySoundAt SoundFX("fx_magnet",DOFShaker), ActiveBall
		If ActiveBall.VelX>0 Then ActiveBall.VelX = MagnetSpeed
	Else
		If ActiveBall.VelX>0 Then ActiveBall.VelX=8
	End If
End Sub

Sub sw47_hit
	vpmtimer.pulsesw 47
	If MagnetEffect Then
		If MagnetSnd=0 Then MagnetSnd=1 : PlaySoundAt SoundFX("fx_magnet",DOFShaker), ActiveBall
		If ActiveBall.VelX>0 Then ActiveBall.VelX = MagnetSpeed
	Else
		If ActiveBall.VelX>0 Then ActiveBall.VelX=8
	End If
End Sub

Sub sw48_hit
	vpmtimer.pulsesw 48
	If MagnetEffect Then
		If MagnetSnd=0 Then MagnetSnd=1 : PlaySoundAt SoundFX("fx_magnet",DOFShaker), ActiveBall
		If ActiveBall.VelX>0 Then ActiveBall.VelX = MagnetSpeed
	Else
		If ActiveBall.VelX>0 Then ActiveBall.VelX=8
	End If
End Sub

'Drop Target
Sub sw51_dropped:dtDropT.Hit 1:End Sub

'Wire Ramp
Sub sw54_hit:Controller.switch(54) = 1: PlaySoundAt "fx_sensor",ActiveBall:End Sub
Sub sw54_unhit:Controller.switch(54) = 0: End Sub

'Inner Loop
Sub sw55_hit:vpmtimer.PulseSw 55: PlaySoundAt "fx_sensor",ActiveBall:End Sub

'StandUp Targets
Sub sw56_hit:vpmtimer.PulseSw 56: PlaySoundAt SoundFX("fx_target",DOFTargets), ActiveBall:End Sub
Sub sw57_hit:vpmtimer.PulseSw 57: PlaySoundAt SoundFX("fx_target",DOFTargets), ActiveBall:End Sub

'Right Loop
Sub sw58_hit
vpmtimer.PulseSw 58: PlaySoundAt "fx_sensor",ActiveBall
If ActiveBall.VelY>20 Then ActiveBall.VelY = 18
End Sub

'Left Loop
Sub sw62_hit
vpmtimer.PulseSw 62: PlaySoundAt "fx_sensor",ActiveBall
If ActiveBall.VelY>20 Then ActiveBall.VelY = 18
End Sub

'Left Plastic Ramp
Sub sw63_hit:vpmtimer.PulseSw 63 :End Sub
Sub sw64_hit:vpmtimer.PulseSw 64 :End Sub

'Right Plastic Ramp
Sub sw66_hit:vpmtimer.PulseSw 66 :End Sub
Sub sw67_hit:vpmtimer.PulseSw 67 :End Sub

' *********************************************************************
'					Supporting Ball & Sound Functions
' *********************************************************************

Function Vol(ball) ' Calculates the Volume of the sound based on the ball speed
    Vol = Csng(BallVel(ball) ^2 / 2000)
End Function

Function Pan(ball) ' Calculates the pan for a ball based on the X position on the table. "table1" is the name of the table
    Dim tmp
    tmp = ball.x * 2 / table1.width-1
    If tmp > 0 Then
        Pan = Csng(tmp ^10)
    Else
        Pan = Csng(-((- tmp) ^10) )
    End If
End Function

function AudioFade(ball)
	Dim tmp
    tmp = ball.y * 2 / Table1.height-1
    If tmp > 0 Then
		AudioFade = Csng(tmp ^10)
    Else
        AudioFade = Csng(-((- tmp) ^10) )
    End If
End Function

Function Pitch(ball) ' Calculates the pitch of the sound based on the ball speed
    Pitch = BallVel(ball) * 20
End Function

Function BallVel(ball) 'Calculates the ball speed
    BallVel = INT(SQR((ball.VelX ^2) + (ball.VelY ^2) ) )
End Function

Function RndNum(min,max)
 RndNum = Int(Rnd()*(max-min+1))+min     ' Sets a random number between min and max
End Function

'Set position as table object (Use object or light but NOT wall) and Volume to 1
Sub PlaySoundAt(sound, tableobj)
	PlaySound sound, 1, 1, Pan(tableobj), 0,0,0, 1, AudioFade(tableobj)
End Sub

'Set all as per ball position & speed, but Volume Multiplier may be used eg; PlaySoundAtBallVol "sound",3
Sub PlaySoundAtBallVol(sound, VolMult)
	PlaySound sound, 0, Vol(ActiveBall) * VolMult, Pan(ActiveBall), 0, Pitch(ActiveBall), 0, 1, AudioFade(ActiveBall)
End Sub

'Set position as table object and Volume manually.
Sub PlaySoundAtVol(sound, tableobj, Volume)
	PlaySound sound, 1, Volume, Pan(tableobj), 0, 0, 0, 1, AudioFade(tableobj)
End Sub

Sub PlayLoopSoundAtVol(sound, tableobj, Volume)
	PlaySound sound, -1, Volume, Pan(tableobj), 0, 0, 1, 0, AudioFade(tableobj)
End Sub

' *********************************************************************
' 						Other Sound FX
' *********************************************************************
Dim WRSnd:WRSnd=0
sub trigger1_hit: If Activeball.velx> 8 Then Activeball.velx= 8: End If: End Sub
sub trigger2_hit: If ActiveBall.VelY>20 Then ActiveBall.VelY = 18 : End If : End Sub
sub trigger3_hit: If ActiveBall.VelY>20 Then ActiveBall.VelY = 18 : End If : End Sub
Sub trigger4_hit: MagnetSnd=0: Activeball.VelY = MagnetSpeed-5 : PlaysoundAt "fx_prenter2", ActiveBall:End Sub
Sub trigger5_hit: PlaysoundAt "fx_mrturn", ActiveBall:End Sub

Sub JREnter_Hit()
	If ActiveBall.VelY < 0 Then					'ball is going up
		PlaySoundAtVol "fx_mrenter", JREnter, 0.5
	Else										'ball is going down								
		StopSound "fx_mrenter":PlaySoundAtVol "fx_mrexit",JREnter, 1
	End If
End Sub

Sub JRExit_Hit
PlaySoundAtVol "fx_mrturn",JREnter, 0.5
Me.TimerInterval=200:Me.TimerEnabled=1
End Sub

Sub JRExit_timer:Me.TimerEnabled=0:PlaysoundAt "fx_balldrop_ramp",JRExit:End Sub

Sub LREnter_Hit():If ActiveBall.VelY < 0 Then PlaySoundAt "fx_prenter1", ActiveBall:End If:End Sub				'ball is going up
Sub LREnter_UnHit():If ActiveBall.VelY > 0 Then StopSound "fx_prenter1":End If:End Sub							'ball is going down

Sub LWREnter_hit: PlaysoundAt "fx_wireramp_enter", ActiveBall: MagnetSnd=0: End Sub
Sub LWRBump1_hit: PlaysoundAt "fx_wrbump", ActiveBall:End Sub
Sub LWRBump2_hit: PlaysoundAt "fx_wrbump", ActiveBall:End Sub
Sub LWRBump3_hit: PlaysoundAt "fx_wrbump", ActiveBall:End Sub
Sub LWRExit_hit:StopSound"fx_wireramp_enter":PlaysoundAt "fx_wireramp_exit", ActiveBall:Me.TimerInterval=200:Me.TimerEnabled=1:End Sub
Sub LWRExit_timer:Me.TimerEnabled=0:PlaysoundAt "fx_BallDrop",LWRExit:End Sub

Sub RREnter_Hit():If ActiveBall.VelY < 0 Then PlaySoundAt "fx_prenter1",ActiveBall:End If:End Sub				'ball is going up
Sub RREnter_UnHit():If ActiveBall.VelY > 0 Then StopSound "fx_prenter1":End If:End Sub							'ball is going down

Sub RWREnter_hit: PlaysoundAt "fx_wireramp_enter", ActiveBall: WRSnd=1:End Sub

Sub RWRBump1_hit
	PlaysoundAt "fx_wrbump", ActiveBall
	If WRSnd=0 Then PlaysoundAt "fx_wireramp_enter", ActiveBall: WRSnd=1
End Sub

Sub RWRBump2_hit: PlaysoundAt "fx_wrbump", ActiveBall:End Sub

Sub RWRBump3_hit
	PlaysoundAt "fx_wrbump", ActiveBall
	If WRSnd=0 Then PlaysoundAt "fx_wireramp_enter", ActiveBall: WRSnd=1
End Sub

Sub RWRExit_hit:StopSound"fx_wireramp_enter":PlaysoundAt "fx_wireramp_exit", ActiveBall:Me.TimerInterval=200:Me.TimerEnabled=1:WRSnd=0:End Sub
Sub RWRExit_timer:Me.TimerEnabled=0:PlaysoundAt "fx_BallDrop",RWRExit:End Sub

Sub OnBallBallCollision(ball1, ball2, velocity)
	PlaySound("fx_collide"), 0, Csng(velocity) ^2 / 500, Pan(ball1), 0, Pitch(ball1), 0, 0, AudioFade(ball1)
End Sub

Sub LeftFlipper_Collide(parm)
    RandomSoundFlipper
End Sub

Sub Rightflipper_Collide(parm)
    RandomSoundFlipper
End Sub

Sub Posts_Hit(idx)
	RandomSoundRubber()
End Sub

Sub Rubbers_Hit(idx)
	RandomSoundRubber()
End Sub

Sub Gates_Hit(idx)
	PlaySoundAtBallVol "fx_gate4",1
End Sub

Sub RandomSoundRubber()
	Select Case RndNum(1,3)
		Case 1 : PlaySoundAtBallVol "fx_rubber_hit_1",10
		Case 2 : PlaySoundAtBallVol "fx_rubber_hit_2",10
		Case 3 : PlaySoundAtBallVol "fx_rubber_hit_3",10
	End Select
End Sub

Sub RandomSoundFlipper()
	Select Case RndNum(1,3)
		Case 1 : PlaySoundAtBallVol "fx_flip_hit_1", 20
		Case 2 : PlaySoundAtBallVol "fx_flip_hit_2", 20
		Case 3 : PlaySoundAtBallVol "fx_flip_hit_3", 20
	End Select
End Sub

Sub RandomSoundHole()
	Select Case RndNum(1,3)
		Case 1 : PlaySoundAt "fx_hole1", ActiveBall
		Case 2 : PlaySoundAt "fx_hole2", ActiveBall
		Case 3 : PlaySoundAt "fx_hole3", ActiveBall
	End Select
End Sub

'******************************************************
'       	RealTime Updates
'******************************************************
Dim cAttract, cWait, InitBall:cAttract=0:InitBall=0

Sub GameTimer_Timer()
	If cAttract= 1 Then
		'Wait 3 seconds, then create a ball
		If Timer>cWait+3 AND InitBall=0 Then InitBall=1:kicker1.createball: kicker1.kick 180,20:cAttract=2
		if cAttract=2 Then Exit Sub
	End If

	If FlippersType=2 Then
		'move glowbats
		GlowBatLightLeft.y = 1848 - 120 + LeftFlipper.CurrentAngle
		glowbatleft.objrotz = LeftFlipper.CurrentAngle
		GlowBatLightRight.y =1848 - 120 - RightFlipper.CurrentAngle
		glowbatright.objrotz = RightFlipper.CurrentAngle
		GlowBatLightRight1.y =770 - 166 - RightUpFlipper.CurrentAngle
		glowbatright1.objrotz = RightUpFlipper.CurrentAngle
	Else
		LeftFlipperP.RotZ = LeftFlipper.currentangle
		RightFlipperP.RotZ = RightFlipper.currentangle
		RightFlipperP1.RotZ = RightUpFlipper.currentangle
		'move flipper shadows
		LeftFlipperSh.RotZ = LeftFlipper.currentangle
		RightFlipperSh.RotZ = RightFlipper.currentangle
	End If

	RollingSoundUpdate
	BallShadowUpdate
End Sub

Const tnob = 4						' total number of balls : 4 (trough)
Const fakeballs = 0					' number of balls created on table start (rolling sound and ballshadow will be skipped)
ReDim rolling(tnob-fakeballs-1)
ReDim BallShadow(tnob-fakeballs-1)
InitRolling:InitBallShadow

Sub InitRolling
	Dim i
	For i=0 to tnob-fakeballs-1
		rolling(i) = False
	Next
End Sub

Sub InitBallShadow
	Dim i
	For i=0 to tnob-fakeballs-1
		ExecuteGlobal "Set BallShadow(" & i & ") = BallShadow" & (i+1) & ":"
	Next
End Sub

' ************************ROLLING SOUND************************
Sub RollingSoundUpdate()
    Dim BOT, b
    BOT = GetBalls
	' stop the sound of deleted balls
	If UBound(BOT)<(tnob - 1) Then
		For b = (UBound(BOT) + 1) to (tnob-1)
			rolling(b-fakeballs) = False
			StopSound("fx_ballrolling" & b-fakeballs)
		Next
	End If

	' exit the Sub if no balls on the table
    If UBound(BOT) = fakeballs-1 Then Exit Sub

    ' play the rolling sound for each ball
    For b = fakeballs to UBound(BOT)
        If BallVel(BOT(b) ) > 1 AND BOT(b).z < 30 AND BOT(b).z > 0 Then
			rolling(b-fakeballs) = True
			PlaySound("fx_ballrolling" & b-fakeballs), -1, Vol(BOT(b) )*0.5, Pan(BOT(b) ), 0, Pitch(BOT(b) ), 1, 0, AudioFade(BOT(b) )
		Else
            If rolling(b-fakeballs) = True Then
                StopSound("fx_ballrolling" & b-fakeballs)
                rolling(b-fakeballs) = False
            End If
        End If
    Next

End Sub

' **********************BALL SHADOW**********************
Sub BallShadowUpdate()
    Dim BOT, b
    BOT = GetBalls
	' hide shadow of deleted balls
	If UBound(BOT)<(tnob-1) Then
		For b = (UBound(BOT) + 1) to (tnob-1)
		BallShadow(b-fakeballs).visible = 0
		Next
	End If

	' exit the Sub if no balls on the table
    If UBound(BOT) = fakeballs-1 Then Exit Sub

	' render the shadow for each ball
    For b = fakeballs to UBound(BOT)
		If BOT(b).X < Table1.Width/2 Then
			BallShadow(b-fakeballs).X = ((BOT(b).X) - (Ballsize/6) + ((BOT(b).X - (Table1.Width/2))/10)) + 10
		Else
			BallShadow(b-fakeballs).X = ((BOT(b).X) + (Ballsize/6) + ((BOT(b).X - (Table1.Width/2))/10)) - 10
		End If
	    BallShadow(b-fakeballs).Y = BOT(b).Y + 20
		If BOT(b).Z > 20 Then
			BallShadow(b-fakeballs).visible = 1
		Else
			BallShadow(b-fakeballs).visible = 0
		End If
	Next
End Sub
