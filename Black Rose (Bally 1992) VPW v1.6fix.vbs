'*************   VPW Presents   *************
'Black Rose (Bally 1992)
'https://www.ipdb.org/machine.cgi?id=313
'********************************************
' ______   _____          _        ______  ___  ____    _______      ___     ______   ________
'|_   _ \ |_   _|        / \     .' ___  ||_  ||_  _|  |_   __ \   .'   `. .' ____ \ |_   __  |
'  | |_) |  | |         / _ \   / .'   \_|  | |_/ /      | |__) | /  .-.  \| (___ \_|  | |_ \_|
'  |  __'.  | |   _    / ___ \  | |         |  __'.      |  __ /  | |   | | _.____`.   |  _| _
' _| |__) |_| |__/ | _/ /   \ \_\ `.___.'\ _| |  \ \_   _| |  \ \_\  `-'  /| \____) | _| |__/ |
'|_______/|________||____| |____|`.____ .'|____||____| |____| |___|`.___.'  \______.'|________|
'
'*********************
' VPW Black Rose Crew
'*********************
'Sixtoe - Project Lead
'endi - Amazing Playfield Redraw
'ebislit - New playfield stitch / scan
'Sheltemke - New backglass imageand other images
'Skitso - New GI Lighting, Flasher & Insert Tweaking
'iaakki - Updated / fixed GI script to work with new GI lighting
'oqqsan - 3D Inserts
'Apophis - Numerous fixes and code issues
'Fluffhead - Ramp and ball rolling updated
'Onevox - Pirate Ship VR Room
'tomate - LUT selector added, black/red or withe/blue flippers color options added, subtle changes in PF wood parts, cabinet buttons not visible in cabinet mode,  POV changed
'leojreimroc - VR Backglass

'Stuff to check - light values, lamp/flasher test. blenddisable
'
'Original Table Thanks;
'Coindropper for his vpx version
'gtxjoe for a working script and playfield resource
'JayFoxRox for plastic scans
'Xagesz for additional photos
'HauntFreaks for his magic touch and lighting

Option Explicit
Randomize

On Error Resume Next
ExecuteGlobal GetTextFile("controller.vbs")
If Err Then MsgBox "You need the controller.vbs in order to run this table, available in the vp10 package"
On Error Goto 0

'****************************************************************
' Table Options
'****************************************************************

'///////////////////////---- Volume ----/////////////////////////
'VolumeDial is the global volume for mechanical sounds
Const VolumeDial = 0.8        'Recommended values should be no higher than 1

Const BallRollAmpFactor = 0 ' 0 = no amplification, 1 = 2.5db amplification, 2 = 5db amplification, 3 = 7.5db amplification, 4 = 9db amplification (aka: Tie Fighter)

'-----RampRoll Sound Amplification -----/////////////////////
Const RampRollAmpFactor = 0 ' 0 = no amplification, 1 = 2.5db amplification, 2 = 5db amplification, 3 = 7.5db amplification, 4 = 9db amplification (aka: Tie Fighter)

'/////////////////////---- Flipper Colours ----/////////////////////
Const FlipColour = 0        '0 - Black/Red, 1 - White/Red, 2 - White/Blue

'/////////////////////---- Flipper Colours ----/////////////////////
Const BallBright = 0        '0 - Normal, 1 - Bright

'///////////////////////---- VR Room ----////////////////////////
Const VRRoom = 0          '0 - VR Room Off, 1 - Minimal Room, 2 - Yarr!, 3 - Ultra Minimal

'///////////////////////---- VR Flashing Backglass ----////////////////////////
Const VRFlashBackglass = 0      '0 - Static Backglass, 1- Flashing Backglass

'///////////////////////---- Siderails ----////////////////////////
Const RailColour = 0        '0 - Metal, 1 - Black Metal

'/////////////////////---- Cabinet Mode ----/////////////////////
Const CabinetMode = 0         '0 - Rails, 1 - Hidden Rails

'/////////////////////---- Difficulty / Outlane Post Positions ----/////////////////////
Const Difficulty = 0        '0 - Easy, 1 - Medium, 2 - Difficult

'/////////////////////---- Physics Mods ----/////////////////////
Const RubberizerEnabled = 1     '0 = normal flip rubber, 1 = more lively rubber for flips, 2 = different rubberizer
Const FlipperCoilRampupMode = 0   '0 = fast, 1 = medium, 2 = slow (tap passes should work)
Const TargetBouncerEnabled = 1    '0 = normal standup targets, 1 = bouncy targets, 2 = orig TargetBouncer
Const TargetBouncerFactor = 0.7   'Level of bounces. Recommmended value of 0.7 when TargetBouncerEnabled=1, and 1.1 when TargetBouncerEnabled=2

'/////////////////////---- Cannon Walls Mode ----/////////////////////
Const Cannon_Walls = 0 'On the real machine, the ball sometimes runs along the edges of the cannon toy in
'the middle of the playfield resulting in unpredictable behaviour. Set this to 1 to
'attempt to simulate that effect.

'/////////////////////---- Ball Shadows ----/////////////////////
'----- Shadow Options -----
Const DynamicBallShadowsOn = 1    '0 = no dynamic ball shadow ("triangles" near slings and such), 1 = enable dynamic ball shadow
Const AmbientBallShadowOn = 1   '0 = Static shadow under ball ("flasher" image, like JP's)
                  '1 = Moving ball shadow ("primitive" object, like ninuzzo's)
                  '2 = flasher image shadow, but it moves like ninuzzo's

'//////////////---- LUT (Colour Look Up Table) ----//////////////
'0 = Fleep Natural Dark 1
'1 = Fleep Natural Dark 2
'2 = Fleep Warm Dark
'3 = Fleep Warm Bright
'4 = Fleep Warm Vivid Soft
'5 = Fleep Warm Vivid Hard
'6 = Skitso Natural and Balanced
'7 = Skitso Natural High Contrast
'8 = 3rdaxis Referenced THX Standard
'9 = CalleV Punchy Brightness and Contrast
'10 = HauntFreaks Desaturated
'11 = Tomate Washed Out
'12 = VPW Original 1 to 1
'13 = Bassgeige
'14 = Blacklight
'15 = B&W Comic Book

Dim LUTset, DisableLUTSelector, LutToggleSound
LutToggleSound = True
LoadLUT
'LUTset = 16      ' Override saved LUT for debug
SetLUT
DisableLUTSelector = 0  ' Disables the ability to change LUT option with magna saves in game when set to 1

' Thalamus : This sub is used twice - this means ... this one IS NOT USED
' Moved SaveLUT to the other sub
' Sub Table1_exit:SaveLUT:Controller.Stop:End Sub

'/////////////////////---- Flasher Brightness ----/////////////////////
Const flasher_brightness = 16 '< This is a divisor so a higher number = Duller Flashers.

Const BallSize = 50
Const BallMass = 1
Const tnob = 4            'Total number of balls
Const lob = 0           'Locked balls

Const UseVPMModSol = 1
Dim DesktopMode:DesktopMode = Table1.ShowDT
Dim UseVPMDMD
If VRRoom <> 0 Then UseVPMDMD = True Else UseVPMDMD = DesktopMode End If
Dim tablewidth: tablewidth = Table1.width
Dim tableheight: tableheight = Table1.height

Const cGameName = "br_l4" 'Black Rose ROM L4

LoadVPM "01120100", "wpc.VBS", 3.26

'****************************************************************
' STANDARD DEFINITIONS
'****************************************************************
'Main definitions
Const UseSolenoids = 2    'Enable fastflips for williams tables (1 disables)
Const UseLamps = 0      'Use core.vbs script light support instead of the custom lamptimer scripts
Const UseGI = 0       'Use core.vbs GI routine for older machines or machines with no GI
Const UseSync = 0     'Use vsync - Use if table runs too fast
Const HandleMech = 0    'Use core.vbs mechanicals (cvpmMech) pre-programmed into VPX.

'Sounds definitions - Defaulted to empty as they are set elsewhere in the script.
Const SSolenoidOn = ""    'Set solonoid sound when they activate
Const SSolenoidOff = ""   'Set solonoid sound when they deactivate
Const SFlipperOn = ""   'Set flipper sound when they activate
Const SFlipperOff = ""    'Set flipper sound when they deactivate
Const SCoin = ""      'Set coin entry sound

'*******************************************
' Timers
'*******************************************

' The game timer interval is 10 ms
Sub GameTimer_Timer()
  Cor.Update            'update ball tracking
  RollingUpdate         'update rolling sounds
End Sub

' The frame timer interval is -1, so executes at the display frame rate
Sub FrameTimer_Timer()
  If DynamicBallShadowsOn=1 Then DynamicBSUpdate  'update ball shadows
  FlipperLSh.RotZ = LeftFlipper.currentangle
  FlipperRSh.RotZ = RightFlipper.currentangle
  FlipperRTop.RotZ = RightFlipper1.currentangle
' VR_Primary_plunger.Y = 21 + (5* Plunger.Position) -20
End Sub

'************************************************
' Table Init
'************************************************
Dim bsTrough

Sub Table1_Init
  vpminit Me
  With Controller
    .GameName = cGameName
    If Err Then MsgBox "Can't start Game " & cGameName & vbNewLine & Err.Description:Exit Sub
    .SplashInfoLine = "Black Rose (Bally 1992)"&vbNewLine&"V-Pin Workshop"
    .HandleKeyboard = 0
    .ShowTitle = 0
    .ShowDMDOnly = 1
    .ShowFrame = 0
    .HandleMechanics = 0
    .Hidden = 0
    On Error Resume Next
    Controller.Run GetPlayerHWnd
    On Error Goto 0
  End With

   ' Trough
    Set bsTrough = New cvpmTrough
    With bsTrough
        .size = 3
        .initSwitches Array(16, 17, 18)
        .Initexit BallRelease, 55, 8
        .Balls = 3
        .EntrySw = 15
    End With

  ' Main Timer init
  PinMAMETimer.Interval = PinMAMEInterval
  PinMAMETimer.Enabled = 1

  ' Nudging
  vpmNudge.TiltSwitch = 14
  vpmNudge.Sensitivity = 1
    vpmNudge.TiltObj = Array(Bumper1, Bumper2, bumpersw48, LeftSlingShot, RightSlingShot)

  Controller.Switch(22) = 1 'Close coin door - High Power Mode On
  Controller.Switch(63) = 0 'Lockup 1
  Controller.Switch(64) = 0 'Lockup 2

  ' Turn off the bumper lights
  FlBumperFadeTarget(1) = 0
  FlBumperFadeTarget(2) = 0

End Sub

'************************************************
' Solenoids
'************************************************
SolCallback(1) =  "RampSwordKicker"
SolCallback(2) =  "bsTroughSolIn"
SolCallback(3) =  "CannonMotor"
SolCallback(4) =  "bsTroughSolOut"
SolCallback(5) =  ""
SolCallback(6) =  ""
SolCallback(7) =  "SolKnocker"
SolCallback(8) =  "FireCannon"
SolCallback(9) =  "PiratesCoveKick"
SolCallback(10) = "RampUp"
SolCallback(11) = "RampDown"
SolCallback(12) =   ""
SolCallback(13) =   ""
SolCallback(14) =   ""
SolCallback(15) =   ""
SolCallback(16) =   ""
SolModCallback(17) = "Sol17"        'Left Bottom Flasher    'Top "Black" (Backglass)
SolModCallback(18) = "Sol18"        'Left Top Flasher     'Lady Pirate Belly
SolModCallback(19) = "Sol19"        'Right Bottom Flasher   'Man Pirate Right
SolModCallback(20) = "Sol20"        'Right Top Flasher      'Right top
SolModCallback(21) = "Sol21"        'Right Ramp Flasher     'Bottom "Rose"
SolModCallback(22) = "Sol22"        'Left Ramp Flasher      'Bottom "Black"
SolModCallback(23) = "Sol23"        'Locker Open Flasher    'Skull
SolModCallback(24) = "Sol24"        'Left Sword Flasher     'Bottom of Skull
SolModCallback(25) = "Sol25"        'Top Popper Flasher
SolModCallback(26) = "Sol26"        'Cannon Flasher x2
SolModCallback(27) = "Sol27"        'Fire Button Flasher    'Canon Flame
SolModCallback(28) = "Sol28"        'Right Sword Flasher    'Middle Pirate

SolCallback(sLRFlipper) = "SolRFlipper"     'Right Flipper
SolCallback(sLLFlipper) = "SolLFlipper"     'Left Flipper
SolCallback(sURFlipper) = "SolURFlipper"    'Upper Right Flipper



'************************************************
' Keys / Controls
'************************************************

Sub Table1_Paused:Controller.Pause=1:End Sub
Sub Table1_unPaused: Controller.Pause=0:End Sub
Sub Table1_exit
  SaveLUT
  Controller.Pause = False
  Controller.Stop
End Sub

Sub Table1_KeyDown(ByVal keycode)
  If keycode = PlungerKey or keycode = LockBarKey Then Controller.Switch(34)=1:Plunger.PullBack: SoundPlungerPull
  If keycode = LeftFlipperKey Then
    FlipperActivate LeftFlipper, LFPress
  End If
  If keycode = RightFlipperKey Then
    FlipperActivate RightFlipper, RFPress
  End If
  If keycode = LeftTiltKey Then Nudge 90, 5:SoundNudgeLeft
  If keycode = RightTiltKey Then Nudge 270, 5:SoundNudgeRight
  If keycode = CenterTiltKey Then Nudge 0, 3:SoundNudgeCenter
  If keycode = StartGameKey Then SoundStartButton
  If keycode = keyInsertCoin1 or keycode = keyInsertCoin2 or keycode = keyInsertCoin3 or keycode = keyInsertCoin4 Then
    Select Case Int(rnd*3)
      Case 0: PlaySoundAtLevelStatic ("Coin_In_1"), CoinSoundLevel, Drain
      Case 1: PlaySoundAtLevelStatic ("Coin_In_2"), CoinSoundLevel, Drain
      Case 2: PlaySoundAtLevelStatic ("Coin_In_3"), CoinSoundLevel, Drain
    End Select
  End If
  If vpmKeyDown(keycode) Then Exit Sub

'LUT controls

  If Keycode = LeftMagnaSave Then             'Left Fire Button
    if DisableLUTSelector = 0 then
            LUTSet = LUTSet  + 1
      if LutSet > 16 then LUTSet = 0
      SetLUT
      ShowLUT
    end if
  End If

  If Keycode = RightMagnaSave Then              'Left Fire Button
    if DisableLUTSelector = 0 then
            LUTSet = LUTSet  - 1
      if LutSet < 0 then LUTSet = 16
      SetLUT
      ShowLUT
    end if
  End If

  If keycode = PlungerKey Then
    If VRRoom > 0 Then
      TimerVRPlunger.Enabled = True
      TimerVRPlunger2.Enabled = False
    End If
  End If
End Sub

Sub Table1_KeyUp(ByVal keycode)
  If keycode = PlungerKey or keycode = LockBarKey Then Controller.Switch(34)=0: Plunger.Fire: SoundPlungerReleaseBall
  If keycode = LeftFlipperKey Then
    FlipperDeActivate LeftFlipper, LFPress
  End If
  If keycode = RightFlipperKey Then
    FlipperDeActivate RightFlipper, RFPress
  End If
  If VRRoom > 0 Then
    TimerVRPlunger.Enabled = False
    TimerVRPlunger2.Enabled = True
  End If
  If vpmKeyUp(keycode) Then Exit Sub
End Sub

'********************  Sling Shot Animations  *******************
' Rstep and Lstep  are the variables that increment the animation
'****************************************************************
Dim RStep, Lstep

Sub RightSlingShot_Slingshot
  vpmTimer.PulseSw(38)
  RandomSoundSlingshotRight Sling1
  RSling.Visible = 0
  RSling1.Visible = 1
  sling1.TransZ = -20
  RStep = 0
  RightSlingShot.TimerEnabled = 1
  RightSlingShot.TimerInterval = 10
End Sub

Sub RightSlingShot_Timer
  Select Case RStep
    Case 3:RSLing1.Visible = 0:RSLing2.Visible = 1:sling1.TransZ = -10
    Case 4:RSLing2.Visible = 0:RSLing.Visible = 1:sling1.TransZ = 0:RightSlingShot.TimerEnabled = 0
  End Select
  RStep = RStep + 1
End Sub

Sub LeftSlingShot_Slingshot
  vpmTimer.PulseSw(28)
  RandomSoundSlingshotLeft Sling2
  LSling.Visible = 0
  LSling1.Visible = 1
  sling2.TransZ = -20
  LStep = 0
  LeftSlingShot.TimerEnabled = 1
  LeftSlingShot.TimerInterval  = 10
End Sub

Sub LeftSlingShot_Timer
  Select Case LStep
    Case 3:LSLing1.Visible = 0:LSLing2.Visible = 1:sling2.TransZ = -10
    Case 4:LSLing2.Visible = 0:LSLing.Visible = 1:sling2.TransZ = 0:LeftSlingShot.TimerEnabled = 0
  End Select
  LStep = LStep + 1
End Sub

'****************************************************************
'   FLIPPER CONTROL
'****************************************************************

Const ReflipAngle = 20

Sub SolRFlipper(Enabled)
  If Enabled Then
    RF.Fire
    If rightflipper.currentangle > rightflipper.endangle - ReflipAngle Then
      RandomSoundReflipUpRight RightFlipper
    Else
      SoundFlipperUpAttackRight RightFlipper
      RandomSoundFlipperUpRight RightFlipper
    End If
  Else
    RightFlipper.RotateToStart
    If RightFlipper.currentangle > RightFlipper.startAngle + 5 Then
      RandomSoundFlipperDownRight RightFlipper
    End If
    FlipperRightHitParm = FlipperUpSoundLevel
  End If
End Sub

Sub SolLFlipper(Enabled)
  If Enabled Then
    LF.Fire
    If leftflipper.currentangle < leftflipper.endangle + ReflipAngle Then
      RandomSoundReflipUpLeft LeftFlipper
    Else
      SoundFlipperUpAttackLeft LeftFlipper
      RandomSoundFlipperUpLeft LeftFlipper
    End If
  Else
    LeftFlipper.RotateToStart
    If LeftFlipper.currentangle < LeftFlipper.startAngle - 5 Then
      RandomSoundFlipperDownLeft LeftFlipper
    End If
    FlipperLeftHitParm = FlipperUpSoundLevel
  End If
End Sub

Sub SolURFlipper(Enabled)
  If Enabled Then
    RightFlipper1.RotateToEnd
    If rightflipper1.currentangle > rightflipper1.endangle - ReflipAngle Then
      RandomSoundReflipUpRight RightFlipper1
    Else
      SoundFlipperUpAttackRight RightFlipper1
      RandomSoundFlipperUpRight RightFlipper1
    End If
  Else
    RightFlipper1.RotateToStart
    If RightFlipper1.currentangle > RightFlipper1.startAngle + 5 Then
      RandomSoundFlipperDownRight RightFlipper1
    End If
    FlipperRightHitParm = FlipperUpSoundLevel
  End If
End Sub

'***********************************************************************
' Begin NFozzy Physics Scripting:  Flipper Tricks and Rubber Dampening '
'***********************************************************************

'****************************************************************
' Flipper Collision Subs
'****************************************************************

Sub LeftFlipper_Collide(parm)
  CheckLiveCatch Activeball, LeftFlipper, LFCount, parm
  LeftFlipperCollide parm
  if RubberizerEnabled = 1 then Rubberizer(parm)
  if RubberizerEnabled = 2 then Rubberizer2(parm)
End Sub

Sub RightFlipper_Collide(parm)
  CheckLiveCatch Activeball, RightFlipper, RFCount, parm
  RightFlipperCollide parm
  if RubberizerEnabled = 1 then Rubberizer(parm)
  if RubberizerEnabled = 2 then Rubberizer2(parm)
End Sub

Sub RightFlipper1_Collide(parm)
  CheckLiveCatch Activeball, RightFlipper1, RFCount1, parm
  RightFlipperCollide parm
  if RubberizerEnabled = 1 then Rubberizer(parm)
  if RubberizerEnabled = 2 then Rubberizer2(parm)
End Sub


'****************************************************************
' iaakki's Rubberizer
'****************************************************************

sub Rubberizer(parm)
  if parm < 10 And parm > 2 And Abs(activeball.angmomz) < 10 then
    'debug.print "parm: " & parm & " momz: " & activeball.angmomz &" vely: "& activeball.vely
    activeball.angmomz = activeball.angmomz * 1.2
    activeball.vely = activeball.vely * 1.2
    'debug.print ">> newmomz: " & activeball.angmomz&" newvely: "& activeball.vely
  Elseif parm <= 2 and parm > 0.2 and activeball.vely < 0 Then
    'debug.print "* parm: " & parm & " momz: " & activeball.angmomz &" vely: "& activeball.vely
    activeball.angmomz = activeball.angmomz * -1.1
    activeball.vely = activeball.vely * 1.4
    'debug.print "**** >> newmomz: " & activeball.angmomz&" newvely: "& activeball.vely
  end if
end sub

sub Rubberizer2(parm)
  if parm < 10 And parm > 2 And Abs(activeball.angmomz) < 10 then
    'debug.print "parm: " & parm & " momz: " & activeball.angmomz &" vely: "& activeball.vely
    activeball.angmomz = -activeball.angmomz * 2
    activeball.vely = activeball.vely * 1.2
    'debug.print ">> newmomz: " & activeball.angmomz&" newvely: "& activeball.vely
  Elseif parm <= 2 and parm > 0.2 and activeball.vely < 0 Then
    'debug.print "* parm: " & parm & " momz: " & activeball.angmomz &" vely: "& activeball.vely
    activeball.angmomz = -activeball.angmomz * 0.5
    activeball.vely = activeball.vely * (1.2 + rnd(1)/3 )
    'debug.print "**** >> newmomz: " & activeball.angmomz&" newvely: "& activeball.vely
  end if
end sub

'****************************************************************
' FLIPPER CORRECTION INITIALIZATION
'****************************************************************

Const LiveCatch = 16

dim LF : Set LF = New FlipperPolarity
dim RF : Set RF = New FlipperPolarity

InitPolarity

Sub InitPolarity()
  dim x, a : a = Array(LF, RF)
  for each x in a
    x.AddPoint "Ycoef", 0, RightFlipper.Y-65, 1 'disabled
    x.AddPoint "Ycoef", 1, RightFlipper.Y-11, 1
    x.enabled = True
    x.TimeDelay = 60
  Next

  AddPt "Polarity", 0, 0, 0
  AddPt "Polarity", 1, 0.05, -5.5
  AddPt "Polarity", 2, 0.4, -5.5
  AddPt "Polarity", 3, 0.6, -5.0
  AddPt "Polarity", 4, 0.65, -4.5
  AddPt "Polarity", 5, 0.7, -4.0
  AddPt "Polarity", 6, 0.75, -3.5
  AddPt "Polarity", 7, 0.8, -3.0
  AddPt "Polarity", 8, 0.85, -2.5
  AddPt "Polarity", 9, 0.9,-2.0
  AddPt "Polarity", 10, 0.95, -1.5
  AddPt "Polarity", 11, 1, -1.0
  AddPt "Polarity", 12, 1.05, -0.5
  AddPt "Polarity", 13, 1.1, 0
  AddPt "Polarity", 14, 1.3, 0

  addpt "Velocity", 0, 0,   1
  addpt "Velocity", 1, 0.16, 1.06
  addpt "Velocity", 2, 0.41,  1.05
  addpt "Velocity", 3, 0.53,  1'0.982
  addpt "Velocity", 4, 0.702, 0.968
  addpt "Velocity", 5, 0.95,  0.968
  addpt "Velocity", 6, 1.03,  0.945

  LF.Object = LeftFlipper
  LF.EndPoint = EndPointLp
  RF.Object = RightFlipper
  RF.EndPoint = EndPointRp
End Sub

Sub TriggerLF_Hit() : LF.Addball activeball : End Sub
Sub TriggerLF_UnHit() : LF.PolarityCorrect activeball : End Sub
Sub TriggerRF_Hit() : RF.Addball activeball : End Sub
Sub TriggerRF_UnHit() : RF.PolarityCorrect activeball : End Sub

'****************************************************************
' FLIPPER CORRECTION FUNCTIONS
'****************************************************************

Sub AddPt(aStr, idx, aX, aY)  'debugger wrapper for adjusting flipper script in-game
  dim a : a = Array(LF, RF)
  dim x : for each x in a
    x.addpoint aStr, idx, aX, aY
  Next
End Sub

Class FlipperPolarity
  Public DebugOn, Enabled
  Private FlipAt  'Timer variable (IE 'flip at 723,530ms...)
  Public TimeDelay  'delay before trigger turns off and polarity is disabled TODO set time!
  private Flipper, FlipperStart,FlipperEnd, FlipperEndY, LR, PartialFlipCoef
  Private Balls(20), balldata(20)

  dim PolarityIn, PolarityOut
  dim VelocityIn, VelocityOut
  dim YcoefIn, YcoefOut
  Public Sub Class_Initialize
    redim PolarityIn(0) : redim PolarityOut(0) : redim VelocityIn(0) : redim VelocityOut(0) : redim YcoefIn(0) : redim YcoefOut(0)
    Enabled = True : TimeDelay = 50 : LR = 1:  dim x : for x = 0 to uBound(balls) : balls(x) = Empty : set Balldata(x) = new SpoofBall : next
  End Sub

  Public Property let Object(aInput) : Set Flipper = aInput : StartPoint = Flipper.x : End Property
  Public Property Let StartPoint(aInput) : if IsObject(aInput) then FlipperStart = aInput.x else FlipperStart = aInput : end if : End Property
  Public Property Get StartPoint : StartPoint = FlipperStart : End Property
  Public Property Let EndPoint(aInput) : FlipperEnd = aInput.x: FlipperEndY = aInput.y: End Property
  Public Property Get EndPoint : EndPoint = FlipperEnd : End Property
  Public Property Get EndPointY: EndPointY = FlipperEndY : End Property

  Public Sub AddPoint(aChooseArray, aIDX, aX, aY) 'Index #, X position, (in) y Position (out)
    Select Case aChooseArray
      case "Polarity" : ShuffleArrays PolarityIn, PolarityOut, 1 : PolarityIn(aIDX) = aX : PolarityOut(aIDX) = aY : ShuffleArrays PolarityIn, PolarityOut, 0
      Case "Velocity" : ShuffleArrays VelocityIn, VelocityOut, 1 :VelocityIn(aIDX) = aX : VelocityOut(aIDX) = aY : ShuffleArrays VelocityIn, VelocityOut, 0
      Case "Ycoef" : ShuffleArrays YcoefIn, YcoefOut, 1 :YcoefIn(aIDX) = aX : YcoefOut(aIDX) = aY : ShuffleArrays YcoefIn, YcoefOut, 0
    End Select
    if gametime > 100 then Report aChooseArray
  End Sub

  Public Sub Report(aChooseArray)   'debug, reports all coords in tbPL.text
    if not DebugOn then exit sub
    dim a1, a2 : Select Case aChooseArray
      case "Polarity" : a1 = PolarityIn : a2 = PolarityOut
      Case "Velocity" : a1 = VelocityIn : a2 = VelocityOut
      Case "Ycoef" : a1 = YcoefIn : a2 = YcoefOut
        case else :tbpl.text = "wrong string" : exit sub
    End Select
    dim str, x : for x = 0 to uBound(a1) : str = str & aChooseArray & " x: " & round(a1(x),4) & ", " & round(a2(x),4) & vbnewline : next
    tbpl.text = str
  End Sub

  Public Sub AddBall(aBall) : dim x : for x = 0 to uBound(balls) : if IsEmpty(balls(x)) then set balls(x) = aBall : exit sub :end if : Next  : End Sub

  Private Sub RemoveBall(aBall)
    dim x : for x = 0 to uBound(balls)
      if TypeName(balls(x) ) = "IBall" then
        if aBall.ID = Balls(x).ID Then
          balls(x) = Empty
          Balldata(x).Reset
        End If
      End If
    Next
  End Sub

  Public Sub Fire()
    Flipper.RotateToEnd
    processballs
  End Sub

  Public Property Get Pos 'returns % position a ball. For debug stuff.
    dim x : for x = 0 to uBound(balls)
      if not IsEmpty(balls(x) ) then
        pos = pSlope(Balls(x).x, FlipperStart, 0, FlipperEnd, 1)
      End If
    Next
  End Property

  Public Sub ProcessBalls() 'save data of balls in flipper range
    FlipAt = GameTime
    dim x : for x = 0 to uBound(balls)
      if not IsEmpty(balls(x) ) then
        balldata(x).Data = balls(x)
      End If
    Next
    PartialFlipCoef = ((Flipper.StartAngle - Flipper.CurrentAngle) / (Flipper.StartAngle - Flipper.EndAngle))
    PartialFlipCoef = abs(PartialFlipCoef-1)
  End Sub
  Private Function FlipperOn() : if gameTime < FlipAt+TimeDelay then FlipperOn = True : End If : End Function 'Timer shutoff for polaritycorrect

  Public Sub PolarityCorrect(aBall)
    if FlipperOn() then
      dim tmp, BallPos, x, IDX, Ycoef : Ycoef = 1

      'y safety Exit
      if aBall.VelY > -8 then 'ball going down
        RemoveBall aBall
        exit Sub
      end if

      'Find balldata. BallPos = % on Flipper
      for x = 0 to uBound(Balls)
        if aBall.id = BallData(x).id AND not isempty(BallData(x).id) then
          idx = x
          BallPos = PSlope(BallData(x).x, FlipperStart, 0, FlipperEnd, 1)
          if ballpos > 0.65 then  Ycoef = LinearEnvelope(BallData(x).Y, YcoefIn, YcoefOut)        'find safety coefficient 'ycoef' data
        end if
      Next

      If BallPos = 0 Then 'no ball data meaning the ball is entering and exiting pretty close to the same position, use current values.
        BallPos = PSlope(aBall.x, FlipperStart, 0, FlipperEnd, 1)
        if ballpos > 0.65 then  Ycoef = LinearEnvelope(aBall.Y, YcoefIn, YcoefOut)            'find safety coefficient 'ycoef' data
      End If

      'Velocity correction
      if not IsEmpty(VelocityIn(0) ) then
        Dim VelCoef
        :       VelCoef = LinearEnvelope(BallPos, VelocityIn, VelocityOut)

        if partialflipcoef < 1 then VelCoef = PSlope(partialflipcoef, 0, 1, 1, VelCoef)

        if Enabled then aBall.Velx = aBall.Velx*VelCoef
        if Enabled then aBall.Vely = aBall.Vely*VelCoef
      End If

      'Polarity Correction (optional now)
      if not IsEmpty(PolarityIn(0) ) then
        If StartPoint > EndPoint then LR = -1 'Reverse polarity if left flipper
        dim AddX : AddX = LinearEnvelope(BallPos, PolarityIn, PolarityOut) * LR

        if Enabled then aBall.VelX = aBall.VelX + 1 * (AddX*ycoef*PartialFlipcoef)
      End If
    End If
    RemoveBall aBall
  End Sub
End Class

'****************************************************************
' FLIPPER POLARITY AND RUBBER DAMPENER SUPPORTING FUNCTIONS
'****************************************************************

' Used for flipper correction and rubber dampeners
Sub ShuffleArray(ByRef aArray, byVal offset) 'shuffle 1d array
  dim x, aCount : aCount = 0
  redim a(uBound(aArray) )
  for x = 0 to uBound(aArray) 'Shuffle objects in a temp array
    if not IsEmpty(aArray(x) ) Then
      if IsObject(aArray(x)) then
        Set a(aCount) = aArray(x)
      Else
        a(aCount) = aArray(x)
      End If
      aCount = aCount + 1
    End If
  Next
  if offset < 0 then offset = 0
  redim aArray(aCount-1+offset) 'Resize original array
  for x = 0 to aCount-1   'set objects back into original array
    if IsObject(a(x)) then
      Set aArray(x) = a(x)
    Else
      aArray(x) = a(x)
    End If
  Next
End Sub

' Used for flipper correction and rubber dampeners
Sub ShuffleArrays(aArray1, aArray2, offset)
  ShuffleArray aArray1, offset
  ShuffleArray aArray2, offset
End Sub

' Used for flipper correction, rubber dampeners, and drop targets
Function BallSpeed(ball) 'Calculates the ball speed
  BallSpeed = SQR(ball.VelX^2 + ball.VelY^2 + ball.VelZ^2)
End Function

' Used for flipper correction and rubber dampeners
Function PSlope(Input, X1, Y1, X2, Y2)  'Set up line via two points, no clamping. Input X, output Y
  dim x, y, b, m : x = input : m = (Y2 - Y1) / (X2 - X1) : b = Y2 - m*X2
  Y = M*x+b
  PSlope = Y
End Function

' Used for flipper correction
Class spoofball
  Public X, Y, Z, VelX, VelY, VelZ, ID, Mass, Radius
  Public Property Let Data(aBall)
    With aBall
      x = .x : y = .y : z = .z : velx = .velx : vely = .vely : velz = .velz
      id = .ID : mass = .mass : radius = .radius
    end with
  End Property
  Public Sub Reset()
    x = Empty : y = Empty : z = Empty  : velx = Empty : vely = Empty : velz = Empty
    id = Empty : mass = Empty : radius = Empty
  End Sub
End Class

' Used for flipper correction and rubber dampeners
Function LinearEnvelope(xInput, xKeyFrame, yLvl)
  dim y 'Y output
  dim L 'Line
  dim ii : for ii = 1 to uBound(xKeyFrame)  'find active line
    if xInput <= xKeyFrame(ii) then L = ii : exit for : end if
  Next
  if xInput > xKeyFrame(uBound(xKeyFrame) ) then L = uBound(xKeyFrame)  'catch line overrun
  Y = pSlope(xInput, xKeyFrame(L-1), yLvl(L-1), xKeyFrame(L), yLvl(L) )

  if xInput <= xKeyFrame(lBound(xKeyFrame) ) then Y = yLvl(lBound(xKeyFrame) )  'Clamp lower
  if xInput >= xKeyFrame(uBound(xKeyFrame) ) then Y = yLvl(uBound(xKeyFrame) )  'Clamp upper

  LinearEnvelope = Y
End Function

'****************************************************************
' FLIPPER TRICKS
'****************************************************************

RightFlipper.timerinterval=1
Rightflipper.timerenabled=True

sub RightFlipper_timer()
  FlipperTricks LeftFlipper, LFPress, LFCount, LFEndAngle, LFState
  FlipperTricks RightFlipper, RFPress, RFCount, RFEndAngle, RFState
  FlipperTricks RightFlipper1, RFPress1, RFCount1, RFEndAngle1, RFState1

  FlipperNudge RightFlipper, RFEndAngle, RFEOSNudge, LeftFlipper, LFEndAngle
  FlipperNudge LeftFlipper, LFEndAngle, LFEOSNudge,  RightFlipper, RFEndAngle
end sub

Dim LFEOSNudge, RFEOSNudge

Sub FlipperNudge(Flipper1, Endangle1, EOSNudge1, Flipper2, EndAngle2)
  Dim BOT, b

  If Flipper1.currentangle = Endangle1 and EOSNudge1 <> 1 Then
    EOSNudge1 = 1
    'debug.print Flipper1.currentangle &" = "& Endangle1 &"--"& Flipper2.currentangle &" = "& EndAngle2
    If Flipper2.currentangle = EndAngle2 Then
      BOT = GetBalls
      For b = 0 to Ubound(BOT)
        If FlipperTrigger(BOT(b).x, BOT(b).y, Flipper1) Then
          'Debug.Print "ball in flip1. exit"
          exit Sub
        end If
      Next
      For b = 0 to Ubound(BOT)
        If FlipperTrigger(BOT(b).x, BOT(b).y, Flipper2) Then
          BOT(b).velx = BOT(b).velx / 1.3
          BOT(b).vely = BOT(b).vely - 0.5
        end If
      Next
    End If
  Else
    If Flipper1.currentangle <> EndAngle1 then
      EOSNudge1 = 0
    end if
  End If
End Sub

'****************************************************************
' Maths
'****************************************************************
Const PI = 3.1415927

Function dSin(degrees)
  dsin = sin(degrees * Pi/180)
End Function

Function dCos(degrees)
  dcos = cos(degrees * Pi/180)
End Function

Function Atn2(dy, dx)
  If dx > 0 Then
    Atn2 = Atn(dy / dx)
  ElseIf dx < 0 Then
    If dy = 0 Then
      Atn2 = pi
    Else
      Atn2 = Sgn(dy) * (pi - Atn(Abs(dy / dx)))
    end if
  ElseIf dx = 0 Then
    if dy = 0 Then
      Atn2 = 0
    else
      Atn2 = Sgn(dy) * pi / 2
    end if
  End If
End Function

'****************************************************************
' Check ball distance from Flipper for Rem
'****************************************************************

Function Distance(ax,ay,bx,by)
  Distance = SQR((ax - bx)^2 + (ay - by)^2)
End Function

Function DistancePL(px,py,ax,ay,bx,by) ' Distance between a point and a line where point is px,py
  DistancePL = ABS((by - ay)*px - (bx - ax) * py + bx*ay - by*ax)/Distance(ax,ay,bx,by)
End Function

Function Radians(Degrees)
  Radians = Degrees * PI /180
End Function

Function AnglePP(ax,ay,bx,by)
  AnglePP = Atn2((by - ay),(bx - ax))*180/PI
End Function

Function DistanceFromFlipper(ballx, bally, Flipper)
  DistanceFromFlipper = DistancePL(ballx, bally, Flipper.x, Flipper.y, Cos(Radians(Flipper.currentangle+90))+Flipper.x, Sin(Radians(Flipper.currentangle+90))+Flipper.y)
End Function

Function FlipperTrigger(ballx, bally, Flipper)
  Dim DiffAngle
  DiffAngle  = ABS(Flipper.currentangle - AnglePP(Flipper.x, Flipper.y, ballx, bally) - 90)
  If DiffAngle > 180 Then DiffAngle = DiffAngle - 360

  If DistanceFromFlipper(ballx,bally,Flipper) < 48 and DiffAngle <= 90 and Distance(ballx,bally,Flipper.x,Flipper.y) < Flipper.Length Then
    FlipperTrigger = True
  Else
    FlipperTrigger = False
  End If
End Function

'****************************************************************
' End - Check ball distance from Flipper for Rem
'****************************************************************

dim LFPress, RFPress, LFPress1, RFPress1, LFCount, LFCount1, RFCount, RFCount1
dim LFState, LFState1, RFState, RFState1
dim EOST, EOSA, Frampup, FElasticity, FReturn
dim RFEndAngle, RFEndAngle1, LFEndAngle, LFEndAngle1

EOST = leftflipper.eostorque
EOSA = leftflipper.eostorqueangle
Frampup = LeftFlipper.rampup
FElasticity = LeftFlipper.elasticity
FReturn = LeftFlipper.return
Const EOSTnew = 0.8
Const EOSAnew = 1
Const EOSRampup = 0
Dim SOSRampup
Select Case FlipperCoilRampupMode
  Case 0:
    SOSRampup = 2.5
  Case 1:
    SOSRampup = 6
  Case 2:
    SOSRampup = 8.5
End Select
Const LiveElasticity = 0.45
Const SOSEM = 0.815
Const EOSReturn = 0.025

LFEndAngle = Leftflipper.endangle
RFEndAngle = RightFlipper.endangle
RFEndAngle1 = RightFlipper1.endangle

Sub FlipperActivate(Flipper, FlipperPress)
  FlipperPress = 1
  Flipper.Elasticity = FElasticity

  Flipper.eostorque = EOST
  Flipper.eostorqueangle = EOSA
End Sub

Sub FlipperDeactivate(Flipper, FlipperPress)
  FlipperPress = 0
  Flipper.eostorqueangle = EOSA
  Flipper.eostorque = EOST*EOSReturn/FReturn

  If Abs(Flipper.currentangle) <= Abs(Flipper.endangle) + 0.1 Then
    Dim BOT, b
    BOT = GetBalls

    For b = 0 to UBound(BOT)
      If Distance(BOT(b).x, BOT(b).y, Flipper.x, Flipper.y) < 55 Then 'check for cradle
        If BOT(b).vely >= -0.4 Then BOT(b).vely = -0.4
      End If
    Next
  End If
End Sub

Sub FlipperTricks (Flipper, FlipperPress, FCount, FEndAngle, FState)
  Dim Dir
  Dir = Flipper.startangle/Abs(Flipper.startangle)  '-1 for Right Flipper

  If Abs(Flipper.currentangle) > Abs(Flipper.startangle) - 0.05 Then
    If FState <> 1 Then
      Flipper.rampup = SOSRampup
      Flipper.endangle = FEndAngle - 3*Dir
      Flipper.Elasticity = FElasticity * SOSEM
      FCount = 0
      FState = 1
    End If
  ElseIf Abs(Flipper.currentangle) <= Abs(Flipper.endangle) and FlipperPress = 1 then
    if FCount = 0 Then FCount = GameTime

    If FState <> 2 Then
      Flipper.eostorqueangle = EOSAnew
      Flipper.eostorque = EOSTnew
      Flipper.rampup = EOSRampup
      Flipper.endangle = FEndAngle
      FState = 2
    End If
  Elseif Abs(Flipper.currentangle) > Abs(Flipper.endangle) + 0.01 and FlipperPress = 1 Then
    If FState <> 3 Then
      Flipper.eostorque = EOST
      Flipper.eostorqueangle = EOSA
      Flipper.rampup = Frampup
      Flipper.Elasticity = FElasticity
      FState = 3
    End If

  End If
End Sub

Const LiveDistanceMin = 30  'minimum distance in vp units from flipper base live catch dampening will occur
Const LiveDistanceMax = 114  'maximum distance in vp units from flipper base live catch dampening will occur (tip protection)

Sub CheckLiveCatch(ball, Flipper, FCount, parm) 'Experimental new live catch
  Dim Dir
  Dir = Flipper.startangle/Abs(Flipper.startangle)    '-1 for Right Flipper
  Dim LiveCatchBounce                             'If live catch is not perfect, it won't freeze ball totally
  Dim CatchTime : CatchTime = GameTime - FCount

  if CatchTime <= LiveCatch and parm > 6 and ABS(Flipper.x - ball.x) > LiveDistanceMin and ABS(Flipper.x - ball.x) < LiveDistanceMax Then
    if CatchTime <= LiveCatch*0.5 Then            'Perfect catch only when catch time happens in the beginning of the window
      LiveCatchBounce = 0
    else
      LiveCatchBounce = Abs((LiveCatch/2) - CatchTime)  'Partial catch when catch happens a bit late
    end If
    '   debug.print "Live catch! Bounce: " & LiveCatchBounce

    If LiveCatchBounce = 0 and ball.velx * Dir > 0 Then ball.velx = 0
    ball.vely = LiveCatchBounce * (32 / LiveCatch) ' Multiplier for inaccuracy bounce
    ball.angmomx= 0
    ball.angmomy= 0
    ball.angmomz= 0
  End If
End Sub

'****************************************************************
' PHYSICS DAMPENERS
'****************************************************************
'These are data mined bounce curves,
'dialed in with the in-game elasticity as much as possible to prevent angle / spin issues.
'Requires tracking ballspeed to calculate COR

Sub RDampen_Timer()
  Cor.Update
End Sub

Sub dPosts_Hit(idx)
  RubbersD.dampen Activeball
  TargetBouncer Activeball, 1
End Sub

Sub dSleeves_Hit(idx)
  SleevesD.Dampen Activeball
  TargetBouncer Activeball, 0.7
End Sub

dim RubbersD : Set RubbersD = new Dampener  'frubber
RubbersD.name = "Rubbers"
RubbersD.debugOn = False  'shows info in textbox "TBPout"
RubbersD.Print = False  'debug, reports in debugger (in vel, out cor)
'cor bounce curve (linear)
'for best results, try to match in-game velocity as closely as possible to the desired curve
'RubbersD.addpoint 0, 0, 0.935  'point# (keep sequential), ballspeed, CoR (elasticity)
RubbersD.addpoint 0, 0, 0.96  'point# (keep sequential), ballspeed, CoR (elasticity)
RubbersD.addpoint 1, 3.77, 0.96
RubbersD.addpoint 2, 5.76, 0.967  'dont take this as gospel. if you can data mine rubber elasticitiy, please help!
RubbersD.addpoint 3, 15.84, 0.874
RubbersD.addpoint 4, 56, 0.64 'there's clamping so interpolate up to 56 at least

dim SleevesD : Set SleevesD = new Dampener  'this is just rubber but cut down to 85%...
SleevesD.name = "Sleeves"
SleevesD.debugOn = False  'shows info in textbox "TBPout"
SleevesD.Print = False  'debug, reports in debugger (in vel, out cor)
SleevesD.CopyCoef RubbersD, 0.85

Class Dampener
  Public Print, debugOn 'tbpOut.text
  public name, Threshold  'Minimum threshold. Useful for Flippers, which don't have a hit threshold.
  Public ModIn, ModOut
  Private Sub Class_Initialize : redim ModIn(0) : redim Modout(0): End Sub

  Public Sub AddPoint(aIdx, aX, aY)
    ShuffleArrays ModIn, ModOut, 1 : ModIn(aIDX) = aX : ModOut(aIDX) = aY : ShuffleArrays ModIn, ModOut, 0
    if gametime > 100 then Report
  End Sub

  public sub Dampen(aBall)
    if threshold then if BallSpeed(aBall) < threshold then exit sub end if end if
    dim RealCOR, DesiredCOR, str, coef
    DesiredCor = LinearEnvelope(cor.ballvel(aBall.id), ModIn, ModOut )
    if cor.ballvel(aBall.id) <> 0 then
      RealCOR = BallSpeed(aBall) / cor.ballvel(aBall.id)
    Else
      RealCOR = BallSpeed(aBall) / 0.0000001
    end if
    coef = desiredcor / realcor
    if debugOn then str = name & " in vel:" & round(cor.ballvel(aBall.id),2 ) & vbnewline & "desired cor: " & round(desiredcor,4) & vbnewline & _
    "actual cor: " & round(realCOR,4) & vbnewline & "ballspeed coef: " & round(coef, 3) & vbnewline
    if Print then debug.print Round(cor.ballvel(aBall.id),2) & ", " & round(desiredcor,3)

' Thalamus - patched :     aBall.velx = aBall.velx * coef : aBall.vely = aBall.vely * coef
    aBall.velx = aBall.velx * coef : aBall.vely = aBall.vely * coef : aBall.velz = aBall.velz * coef
    if debugOn then TBPout.text = str
  End Sub

  Public Sub CopyCoef(aObj, aCoef) 'alternative addpoints, copy with coef
    dim x : for x = 0 to uBound(aObj.ModIn)
      addpoint x, aObj.ModIn(x), aObj.ModOut(x)*aCoef
    Next
  End Sub


  Public Sub Report()   'debug, reports all coords in tbPL.text
    if not debugOn then exit sub
    dim a1, a2 : a1 = ModIn : a2 = ModOut
    dim str, x : for x = 0 to uBound(a1) : str = str & x & ": " & round(a1(x),4) & ", " & round(a2(x),4) & vbnewline : next
    TBPout.text = str
  End Sub

End Class

'****************************************************************
' TRACK ALL BALL VELOCITIES FOR RUBBER DAMPENER AND DROP TARGETS
'****************************************************************

dim cor : set cor = New CoRTracker

Class CoRTracker
  public ballvel, ballvelx, ballvely

  Private Sub Class_Initialize : redim ballvel(0) : redim ballvelx(0): redim ballvely(0) : End Sub

  Public Sub Update() 'tracks in-ball-velocity
    dim str, b, AllBalls, highestID : allBalls = getballs

    for each b in allballs
      if b.id >= HighestID then highestID = b.id
    Next

    if uBound(ballvel) < highestID then redim ballvel(highestID)  'set bounds
    if uBound(ballvelx) < highestID then redim ballvelx(highestID)  'set bounds
    if uBound(ballvely) < highestID then redim ballvely(highestID)  'set bounds

    for each b in allballs
      ballvel(b.id) = BallSpeed(b)
      ballvelx(b.id) = b.velx
      ballvely(b.id) = b.vely
    Next
  End Sub
End Class

'*** Determines if a Points (px,py) is inside a 4 point polygon A-D in Clockwise/CCW order
Function InRect(px,py,ax,ay,bx,by,cx,cy,dx,dy)
  Dim AB, BC, CD, DA
  AB = (bx*py) - (by*px) - (ax*py) + (ay*px) + (ax*by) - (ay*bx)
  BC = (cx*py) - (cy*px) - (bx*py) + (by*px) + (bx*cy) - (by*cx)
  CD = (dx*py) - (dy*px) - (cx*py) + (cy*px) + (cx*dy) - (cy*dx)
  DA = (ax*py) - (ay*px) - (dx*py) + (dy*px) + (dx*ay) - (dy*ax)

  If (AB <= 0 AND BC <=0 AND CD <= 0 AND DA <= 0) Or (AB >= 0 AND BC >=0 AND CD >= 0 AND DA >= 0) Then
    InRect = True
  Else
    InRect = False
  End If
End Function

'******************************************************
' VPW TargetBouncer for targets and posts by Iaakki, Wrd1972, Apophis
'******************************************************

Sub TargetBouncer(aBall,defvalue)
    dim zMultiplier, vel, vratio
    If TargetBouncerEnabled = 1 and aball.z < 30 then
        'debug.print "velx: " & aball.velx & " vely: " & aball.vely & " velz: " & aball.velz
        vel = BallSpeed(aBall)
        If aBall.velx = 0 then vratio = 1 Else vratio = aBall.vely/aBall.velx End If
        Select Case Int(Rnd * 6) + 1
            Case 1: zMultiplier = 0.2*defvalue
      Case 2: zMultiplier = 0.25*defvalue
            Case 3: zMultiplier = 0.3*defvalue
      Case 4: zMultiplier = 0.4*defvalue
            Case 5: zMultiplier = 0.45*defvalue
            Case 6: zMultiplier = 0.5*defvalue
        End Select
        aBall.velz = abs(vel * zMultiplier * TargetBouncerFactor)
        aBall.velx = sgn(aBall.velx) * sqr(abs((vel^2 - aBall.velz^2)/(1+vratio^2)))
        aBall.vely = aBall.velx * vratio
        'debug.print "---> velx: " & aball.velx & " vely: " & aball.vely & " velz: " & aball.velz
        'debug.print "conservation check: " & BallSpeed(aBall)/vel
    Elseif TargetBouncerEnabled = 2 and aball.z < 30 then
    'debug.print "velz: " & activeball.velz
    Select Case Int(Rnd * 4) + 1
      Case 1: zMultiplier = defvalue+1.1
      Case 2: zMultiplier = defvalue+1.05
      Case 3: zMultiplier = defvalue+0.7
      Case 4: zMultiplier = defvalue+0.3
    End Select
    aBall.velz = aBall.velz * zMultiplier * TargetBouncerFactor
    'debug.print "----> velz: " & activeball.velz
    'debug.print "conservation check: " & BallSpeed(aBall)/vel
  End If
End Sub

'****************************************************************
' END nFozzy Physics
'****************************************************************


'****************************************************************
' Control Subs
'****************************************************************
Sub ShooterLane_Hit
  Controller.Switch(25) = 1
End Sub

Sub ShooterLane_UnHit
  Controller.Switch(25) = 0
End Sub

Sub Drain_Hit
  bsTrough.AddBall Me
  RandomSoundDrain Drain
End Sub

Sub bsTroughSolIn(Enabled)
  If Enabled Then
    bsTrough.SolIn Enabled
  End If
End Sub

Sub bsTroughSolOut(Enabled)
  If Enabled Then
    RandomSoundBallRelease BallRelease
    bsTrough.SolOut Enabled
  End If
End Sub

Sub SolKnocker(Enabled)
  If Enabled Then PlaySound SoundFX("Knocker_1",DOFKnocker)
End Sub

Sub RampDown(Enabled)
  If Enabled Then
    debug.print "rampdown"
    Controller.Switch(54) = 1
    DaveyRampDown.Collidable = 1
    DaveyRampUp.Collidable = 0
    DaveyRampTimer.Enabled = 1 'DaveyRamp.HeightBottom = 0
    playsoundat "fx_diverter", sw61_sub
  End If
End sub

Sub RampUp(Enabled)
  If Enabled Then
    debug.print "rampup"
    Controller.Switch(54) = 0
    DaveyRampDown.Collidable = 0
    DaveyRampUp.Collidable = 1
    DaveyRampTimer.Enabled = 1 'DaveyRamp.HeightBottom = 80
    playsoundat "fx_diverter", sw61_sub
  End If
End Sub

Const RampInc = 5
DaveyRampTimer.Interval = 20
Sub DaveyRampTimer_Timer
  dim temp

  If Controller.Switch(54) = True Then 'Down
    Temp = DaveyRamp.HeightBottom - RampInc
    If Temp  <= 0 then
      DaveyRamp.HeightBottom = 0
      DaveyRampTimer.Enabled = 0
    Else
      DaveyRamp.HeightBottom = Temp
    End If
  Else  'Up
    Temp = DaveyRamp.HeightBottom  + RampInc
    If Temp >= 80 then
      DaveyRamp.HeightBottom = 80
      DaveyRampTimer.Enabled = 0
    Else
      DaveyRamp.HeightBottom = Temp
    End If
  End If
  debug.print daveyramp.heightbottom
End Sub

Sub RampSwordKicker (Enabled)
  If (Enabled and Controller.Switch(55) = True) Then
    KickerSw55.destroyball
    vpmCreateBall KickerSW55Upper
    SoundSaucerKick 1, Kickersw55
    'PlaySound "fx_metalrolling"
    PlaySoundAt "Vuk", RmpSnd006
    KickerSW55Upper.kick 180,5
    Controller.Switch(55)=0
  End If
End Sub

'Cannon
Sub FireCannon(Enabled)
  if Enabled and Controller.Switch(35) = True then
    'debug.print discangle
    PlaySoundat "gunshot", DiscCannonOnly
    DiscCannonOnly.visible=1
    vpmCreateBall CannonKicker
    cannonKicker.kick -1*discangle, 52
    Controller.Switch(35) = 0
  else
    DiscCannonOnly.visible=0
  end if
end sub

Sub CannonMotor(Enabled)
  If Enabled Then
    DiscTimer.Enabled = 1
    PlaySoundAtLevelStaticLoop "Motor_Cannon", 0.2, Disc
  Else
    DiscTimer.Enabled = 0
    Stopsound "Motor_Cannon"
  End If
End Sub

If cannon_walls = 1 Then
  C1.isdropped = 0
  C2.isdropped = 0
  C3.isdropped = 0
  C4.isdropped = 0
Else
  C1.isdropped = 1
  C2.isdropped = 1
  C3.isdropped = 1
  C4.isdropped = 1
End If

Dim DiscAngle, Dir
Dir = 1
Sub DiscTimer_Timer
  DiscAngle = DiscAngle + Dir
  Disc.Rotz = DiscAngle
  DiscCannonOnly.Rotz = DiscAngle
  'debug.print disc.rotz & ":" & Timer
  If DiscAngle > -2 AND DiscAngle < 2 AND cannon_walls = 1 Then
    C1.isdropped = 0
    C2.isdropped = 0
    C3.isdropped = 0
    C4.isdropped = 0
  Else
    C1.isdropped = 1
    C2.isdropped = 1
    C3.isdropped = 1
    C4.isdropped = 1
  End If

  If DiscAngle => 45 then Dir = -1
  If DiscAngle =< -45 then Dir = 1
End Sub

'************************************************
' Switches
'************************************************

'Rollovers (Add to collection for sound)
Sub Sw26_Hit: Controller.Switch(26)=1: End Sub
Sub Sw26_UnHit: Controller.Switch(26)=0: End Sub
Sub Sw27_Hit: Controller.Switch(27)=1: End Sub
Sub Sw27_UnHit: Controller.Switch(27)=0: End Sub
Sub Sw36_Hit: Controller.Switch(36)=1: End Sub
Sub Sw36_UnHit: Controller.Switch(36)=0: End Sub
Sub Sw37_Hit: Controller.Switch(37)=1: End Sub
Sub Sw37_UnHit: Controller.Switch(37)=0: End Sub
Sub Sw45_Hit: Controller.Switch(45)=1: End Sub
Sub Sw45_UnHit: Controller.Switch(45)=0: End Sub
Sub Sw56_Hit: Controller.Switch(56)=1: End Sub
Sub Sw56_UnHit: Controller.Switch(56)=0: End Sub
Sub Sw57_Hit: Controller.Switch(57)=1: End Sub
Sub Sw57_UnHit: Controller.Switch(57)=0: End Sub
Sub Sw58_Hit: Controller.Switch(58)=1: End Sub
Sub Sw58_UnHit: Controller.Switch(58)=0: End Sub
Sub Sw62_Hit: Controller.Switch(62)=1: End Sub
Sub Sw62_UnHit: Controller.Switch(62)=0: End Sub

'Bumpers
Sub Bumper1_Hit:  vpmTimer.PulseSw(46): RandomSoundBumperTop Bumper1 : End Sub
Sub Bumper2_Hit:  vpmTimer.PulseSw(47): RandomSoundBumperMiddle Bumper2 : End Sub
Sub BumperSw48_Hit: vpmTimer.PulseSw(48): RandomSoundBumperBottom BumperSw48: Dir=-1: BumperSw48.TimerEnabled=1: BumperSw48.TimerInterval=10: End Sub
Sub BumperSw48_Timer
  BumperCap48.TransY = BumperCap48.TransY + Dir*5
  BumperCap48.blenddisablelighting = 1
  If BumperCap48.TransY < -20 Then Dir=1
  If BumperCap48.TransY > 0 Then BumperCap48.TransY=0: BumperCap48.blenddisablelighting = 0: BumperSw48.TimerEnabled=0
End Sub

'Targets (Add to collection for sound)
Sub Sw31_Hit: vpmTimer.PulseSw(31): End Sub
Sub Sw32_Hit: vpmTimer.PulseSw(32): End Sub
Sub Sw33_Hit: vpmTimer.PulseSw(33): End Sub
Sub Sw41_Hit: vpmTimer.PulseSw(41): End Sub
Sub Sw42_Hit: vpmTimer.PulseSw(42): End Sub
Sub Sw43_Hit: vpmTimer.PulseSw(43): End Sub
Sub Sw51_Hit: vpmTimer.PulseSw(51): End Sub
Sub Sw52_Hit: vpmTimer.PulseSw(52): End Sub
Sub Sw53_Hit: vpmTimer.PulseSw(53): End Sub
Sub Sw65_Hit: vpmTimer.PulseSw(65): End Sub

'Gates (Add to collection for sound)
Sub GateSw44_Hit: vpmTimer.PulseSw(44): End Sub
Sub GateSw71_Hit: vpmTimer.PulseSw(71): End Sub
Sub GateSw72_Hit: vpmTimer.PulseSw(72): End Sub
Sub GateSw76_Hit: vpmTimer.PulseSw(76): End Sub

'Pirates Cove kickers
Sub KickerSw63_Hit: Controller.Switch(63) = 1: KickerSw64.Enabled=1: End Sub
Sub KickerSw64_Hit: Controller.Switch(64) = 1: KickerSw64.Enabled=0: End Sub

Sub PiratesCoveKick (Enabled)
  Enabled = abs(Enabled)
  If (Enabled) Then
    ukick = 1
    Upper_Kicker.enabled = 1
    If (Controller.Switch(64) = True) Then
      KickerSw64.Enabled = 0
      KickerSW64.kick 345, 40: SoundSaucerKick 1,Kickersw64
      Controller.Switch(64) = 0
    End If
    If (Controller.Switch (63) = True) Then
      KickerSw64.Enabled = 0
      KickerSW63.kick 345, 40: SoundSaucerKick 1,Kickersw64
      Controller.Switch(63) = 0
    End If
  End If
End Sub

Dim ukick

Sub Upper_Kicker_Timer()
  Select Case ukick
    Case 1:
      If Hammer.RotZ => 45 Then
        ukick = 2
      End If
      Hammer.RotZ = Hammer.RotZ + 1
    Case 2:
      If Hammer.RotZ <= 0 Then
        Hammer.RotZ = 0
        me.enabled = 0
      End If
      Hammer.RotZ = Hammer.RotZ - 1
  End Select
End Sub

Sub sw61_Sub_Hit()
  PlaySoundat "headquarterhit", sw61_sub
  PlaySound "subway"
  vpmTimer.PulseSwitch 61, 0, ""
End Sub

Sub sw66_Sub_Hit()
  vpmTimer.PulseSwitch 66, 0, ""
  Controller.Switch(35) = 1
  StopSound "subway"
  SoundSaucerLock
  me.destroyball
End Sub

'Ramp Sword
Sub KickerSW55_Hit
  Controller.Switch(55) = 1
  SoundSaucerLock
End Sub

' Ramp Sounds
Sub RmpSnd006_Hit
  'Debug.Print "RmpSnd006"
  WireRampOn False
End Sub

Sub RHelp2_Hit
  'Debug.Print "RHelp2"
  WireRampOff
  PlaySoundAt "WireRamp_Stop", RHelp2
End Sub

Sub RmpSnd001_Hit
  'Debug.Print "RmpSnd001"
  WireRampOn True
End Sub

Sub RmpSnd002_Hit
  'Debug.Print "RmpSnd002"
  WireRampOff
  WireRampOn False
End Sub

Sub RmpSnd003_Hit
  'Debug.Print "RmpSnd003"
  WireRampOff
  PlaySoundAt "WireRamp_Stop", RmpSnd003
End Sub

Sub RmpSnd004_Hit
  'Debug.Print "RmpSnd004"
  WireRampOn True
End Sub

Sub RmpSnd005_Hit
  'Debug.Print "RmpSnd005"
  WireRampOn True
End Sub

Sub RmpSnd007_Hit
  'Debug.Print "RmpSnd007"
  WireRampOff
End Sub


' #####################################
' ######   Flupper Bumpers  #####
' #####################################

' prepare some global vars to dim/brighten objects when using day-night slider
Dim DayNightAdjust , DNA30, DNA45, DNA90
If NightDay < 10 Then
  DNA30 = 0 : DNA45 = (NightDay-10)/20 : DNA90 = 0 : DayNightAdjust = 0.4
Else
  DNA30 = (NightDay-10)/30 : DNA45 = (NightDay-10)/45 : DNA90 = (NightDay-10)/90 : DayNightAdjust = NightDay/25
End If

Dim FlBumperFadeActual(6), FlBumperFadeTarget(6), FlBumperColor(6), FlBumperTop(6), FlBumperSmallLight(6), Flbumperbiglight(6)
Dim FlBumperDisk(6), FlBumperBase(6), FlBumperBulb(6), FlBumperscrews(6), FlBumperActive(6), FlBumperHighlight(6)
Dim cnt : For cnt = 1 to 6 : FlBumperActive(cnt) = False : Next

' colors available are red, white, blue, orange, yellow, green, purple and blacklight

FlInitBumper 1, "blue"
FlInitBumper 2, "blue"

' ### uncomment the statement below to change the color for all bumpers ###
' Dim ind : For ind = 1 to 5 : FlInitBumper ind, "green" : next

Sub FlInitBumper(nr, col)
  FlBumperActive(nr) = True
  ' store all objects in an array for use in FlFadeBumper subroutine
  FlBumperFadeActual(nr) = 1 : FlBumperFadeTarget(nr) = 1.1: FlBumperColor(nr) = col
  Set FlBumperTop(nr) = Eval("bumpertop" & nr) : FlBumperTop(nr).material = "bumpertopmat" & nr
  Set FlBumperSmallLight(nr) = Eval("bumpersmalllight" & nr) : Set Flbumperbiglight(nr) = Eval("bumperbiglight" & nr)
  Set FlBumperDisk(nr) = Eval("bumperdisk" & nr) : Set FlBumperBase(nr) = Eval("bumperbase" & nr)
  Set FlBumperBulb(nr) = Eval("bumperbulb" & nr) : FlBumperBulb(nr).material = "bumperbulbmat" & nr
  Set FlBumperscrews(nr) = Eval("bumperscrews" & nr): FlBumperscrews(nr).material = "bumperscrew" & col
  Set FlBumperHighlight(nr) = Eval("bumperhighlight" & nr)
  ' set the color for the two VPX lights
  select case col
    Case "blue"
      FlBumperBigLight(nr).color = RGB(32,80,255) : FlBumperBigLight(nr).colorfull = RGB(32,80,255)
      FlBumperSmallLight(nr).color = RGB(0,80,255) : FlBumperSmallLight(nr).colorfull = RGB(0,80,255)
      FlBumperSmallLight(nr).TransmissionScale = 0 : MaterialColor "bumpertopmat" & nr, RGB(8,120,255)
      FlBumperHighlight(nr).color = RGB(255,16,8)
      FlBumperSmallLight(nr).BulbModulateVsAdd = 1
    Case "white"
      FlBumperBigLight(nr).color = RGB(255,230,190) : FlBumperBigLight(nr).colorfull = RGB(255,230,190)
      FlBumperHighlight(nr).color = RGB(255,180,100) :
      FlBumperSmallLight(nr).TransmissionScale = 0
      FlBumperSmallLight(nr).BulbModulateVsAdd = 0.99
  end select
End Sub

Sub FlFadeBumper(nr, Z)
  FlBumperBase(nr).BlendDisableLighting = 0.5 * DayNightAdjust
' UpdateMaterial(string, float wrapLighting, float roughness, float glossyImageLerp, float thickness, float edge, float edgeAlpha, float opacity,
'               OLE_COLOR base, OLE_COLOR glossy, OLE_COLOR clearcoat, VARIANT_BOOL isMetal, VARIANT_BOOL opacityActive,
'               float elasticity, float elasticityFalloff, float friction, float scatterAngle) - updates all parameters of a material
  FlBumperDisk(nr).BlendDisableLighting = (0.5 - Z * 0.3 )* DayNightAdjust

  select case FlBumperColor(nr)

    Case "blue" :
      UpdateMaterial "bumperbulbmat" & nr, 0, 0.75 , 0, 1-Z, 1-Z, 1-Z, 0.9999, RGB(38-24*Z,130 - 98*Z,255), RGB(255,255,255), RGB(32,32,32), false, True, 0, 0, 0, 0
      FlBumperSmallLight(nr).intensity = 20  + 500 * Z / (0.5 + DNA30)
      FlBumperTop(nr).BlendDisableLighting = 3 * DayNightAdjust + 50 * Z
      FlBumperBulb(nr).BlendDisableLighting = 12 * DayNightAdjust + 5000 * (0.03 * Z +0.97 * Z^3)
      Flbumperbiglight(nr).intensity = 45 * Z / (1 + DNA45)
      FlBumperHighlight(nr).opacity = 10000 * (Z^3) / (0.5 + DNA90)

    Case "white"
      UpdateMaterial "bumperbulbmat" & nr, 0, 0.75 , 0, 1-Z, 1-Z, 1-Z, 0.9999, RGB(255,230 - 100 * Z, 200 - 150 * Z), RGB(255,255,255), RGB(32,32,32), false, true, 0, 0, 0, 0
      FlBumperSmallLight(nr).intensity = 20 + 180 * Z / (1 + DNA30)
      FlBumperTop(nr).BlendDisableLighting = 5 * DayNightAdjust + 30 * Z
      FlBumperBulb(nr).BlendDisableLighting = 18 * DayNightAdjust + 3000 * (0.03 * Z +0.97 * Z^10)
      Flbumperbiglight(nr).intensity = 14 * Z / (1 + DNA45)
      FlBumperHighlight(nr).opacity = 1000 * (Z^3) / (1 + DNA90)
      FlBumperSmallLight(nr).color = RGB(255,255 - 20*Z,255-65*Z) : FlBumperSmallLight(nr).colorfull = RGB(255,255 - 20*Z,255-65*Z)
      MaterialColor "bumpertopmat" & nr, RGB(255,235 - z*36,220 - Z*90)

  end select
End Sub

Sub BumperTimer_Timer
  dim nr
  For nr = 1 to 6
    If FlBumperFadeActual(nr) < FlBumperFadeTarget(nr) and FlBumperActive(nr)  Then
      FlBumperFadeActual(nr) = FlBumperFadeActual(nr) + (FlBumperFadeTarget(nr) - FlBumperFadeActual(nr)) * 0.8
      If FlBumperFadeActual(nr) > 0.99 Then FlBumperFadeActual(nr) = 1 : End If
      FlFadeBumper nr, FlBumperFadeActual(nr)
    End If
    If FlBumperFadeActual(nr) > FlBumperFadeTarget(nr) and FlBumperActive(nr)  Then
      FlBumperFadeActual(nr) = FlBumperFadeActual(nr) + (FlBumperFadeTarget(nr) - FlBumperFadeActual(nr)) * 0.4 / (FlBumperFadeActual(nr) + 0.1)
      If FlBumperFadeActual(nr) < 0.01 Then FlBumperFadeActual(nr) = 0 : End If
      FlFadeBumper nr, FlBumperFadeActual(nr)
    End If
  next
End Sub

'' ###################################
'' ###### copy script until here #####
'' ###################################


'******************************************************
'****  BALL ROLLING AND DROP SOUNDS
'******************************************************

ReDim rolling(tnob)
InitRolling

Dim DropCount
ReDim DropCount(tnob)

Dim ampFactor

Sub InitRolling
  Dim i
  For i = 0 to tnob
    rolling(i) = False
  Next
  Select Case BallRollAmpFactor
    Case 0
      ampFactor = "_amp0"
    Case 1
      ampFactor = "_amp2_5"
    Case 2
      ampFactor = "_amp5"
    Case 3
      ampFactor = "_amp7_5"
    Case 4
      ampFactor = "_amp9"
    Case Else
      ampFactor = "_amp0"
  End Select
End Sub

Sub RollingUpdate()
  Dim BOT, b
  BOT = GetBalls

  ' stop the sound of deleted balls
  For b = UBound(BOT) + 1 to tnob
    If AmbientBallShadowOn = 0 Then BallShadowA(b).visible = 0
    rolling(b) = False
    StopSound("BallRoll_" & b & ampFactor)
  Next

  ' "Static" Ball Shadows
  If AmbientBallShadowOn = 0 Then
    If BOT(b).Z > 30 Then
      BallShadowA(b).height=BOT(b).z - BallSize/4   'This is technically 1/4 of the ball "above" the ramp, but it keeps it from clipping
    Else
      BallShadowA(b).height=BOT(b).z - BallSize/2 + 5
    End If
    BallShadowA(b).Y = BOT(b).Y + Ballsize/5 + fovY
    BallShadowA(b).X = BOT(b).X
    BallShadowA(b).visible = 1
  End If

  ' exit the sub if no balls on the table
  If UBound(BOT) = -1 Then Exit Sub

  ' play the rolling sound for each ball

  For b = 0 to UBound(BOT)

    If BallVel(BOT(b)) > 1 AND BOT(b).z < 30 Then
      rolling(b) = True
      PlaySound ("BallRoll_" & b & ampFactor), -1, VolPlayfieldRoll(BOT(b)) * 1.1 * VolumeDial, AudioPan(BOT(b)), 0, PitchPlayfieldRoll(BOT(b)), 1, 0, AudioFade(BOT(b))

    Else
      If rolling(b) = True Then
        StopSound("BallRoll_" & b & ampFactor)
        rolling(b) = False
      End If
    End If

    ' Ball Drop Sounds
    If BOT(b).VelZ < -1 and BOT(b).z < 55 and BOT(b).z > 27 Then 'height adjust for ball drop sounds
      If DropCount(b) >= 5 Then
        DropCount(b) = 0
        If BOT(b).velz > -7 Then
          RandomSoundBallBouncePlayfieldSoft BOT(b)
        Else
          RandomSoundBallBouncePlayfieldHard BOT(b)
        End If
      End If
    End If
    If DropCount(b) < 5 Then
      DropCount(b) = DropCount(b) + 1
    End If
  Next
End Sub


'******************************************************
'****  END BALL ROLLING AND DROP SOUNDS
'******************************************************



'******************************************************
'**** RAMP ROLLING SFX
'******************************************************

'Ball tracking ramp SFX 1.0
'   Reqirements:
'          * Import A Sound File for each ball on the table for plastic ramps.  Call It RampLoop<Ball_Number> ex: RampLoop1, RampLoop2, ...
'          * Import a Sound File for each ball on the table for wire ramps. Call it WireLoop<Ball_Number> ex: WireLoop1, WireLoop2, ...
'          * Create a Timer called RampRoll, that is enabled, with a interval of 100
'          * Set RampBAlls and RampType variable to Total Number of Balls
' Usage:
'          * Setup hit events and call WireRampOn True or WireRampOn False (True = Plastic ramp, False = Wire Ramp)
'          * To stop tracking ball
'                 * call WireRampOff
'                 * Otherwise, the ball will auto remove if it's below 30 vp units
'

dim RampMinLoops : RampMinLoops = 4

dim rampAmpFactor

InitRampRolling

Sub InitRampRolling()
  Select Case RampRollAmpFactor
    Case 0
      rampAmpFactor = "_amp0"
    Case 1
      rampAmpFactor = "_amp2_5"
    Case 2
      rampAmpFactor = "_amp5"
    Case 3
      rampAmpFactor = "_amp7_5"
    Case 4
      rampAmpFactor = "_amp9"
    Case Else
      rampAmpFactor = "_amp0"
  End Select
End Sub

' RampBalls
'      Setup:        Set the array length of x in RampBalls(x,2) Total Number of Balls on table + 1:  if tnob = 5, then RammBalls(6,2)
'      Description:
dim RampBalls(6,2)
'x,0 = ball x,1 = ID, 2 = Protection against ending early (minimum amount of updates)
'0,0 is boolean on/off, 0,1 unused for now
RampBalls(0,0) = False

' RampType
'     Setup: Set this array to the number Total number of balls that can be tracked at one time + 1.  5 ball multiball then set value to 6
'     Description: Array type indexed on BallId and a values used to deterimine what type of ramp the ball is on: False = Wire Ramp, True = Plastic Ramp
dim RampType(6)

Sub WireRampOn(input)  : Waddball ActiveBall, input : RampRollUpdate: End Sub
Sub WireRampOff() : WRemoveBall ActiveBall.ID : End Sub


' WaddBall (Active Ball, Boolean)
'     Description: This subroutine is called from WireRampOn to Add Balls to the RampBalls Array
Sub Waddball(input, RampInput)  'Add ball
  ' This will loop through the RampBalls array checking each element of the array x, position 1
  ' To see if the the ball was already added to the array.
  ' If the ball is found then exit the subroutine
  dim x : for x = 1 to uBound(RampBalls)  'Check, don't add balls twice
    if RampBalls(x, 1) = input.id then
      if Not IsEmpty(RampBalls(x,1) ) then Exit Sub 'Frustating issue with BallId 0. Empty variable = 0
    End If
  Next

  ' This will itterate through the RampBalls Array.
  ' The first time it comes to a element in the array where the Ball Id (Slot 1) is empty.  It will add the current ball to the array
  ' The RampBalls assigns the ActiveBall to element x,0 and ball id of ActiveBall to 0,1
  ' The RampType(BallId) is set to RampInput
  ' RampBalls in 0,0 is set to True, this will enable the timer and the timer is also turned on
  For x = 1 to uBound(RampBalls)
    if IsEmpty(RampBalls(x, 1)) then
      Set RampBalls(x, 0) = input
      RampBalls(x, 1) = input.ID
      RampType(x) = RampInput
      RampBalls(x, 2) = 0
      'exit For
      RampBalls(0,0) = True
      RampRoll.Enabled = 1   'Turn on timer
      'RampRoll.Interval = RampRoll.Interval 'reset timer
      exit Sub
    End If
    if x = uBound(RampBalls) then   'debug
      Debug.print "WireRampOn error, ball queue is full: " & vbnewline & _
      RampBalls(0, 0) & vbnewline & _
      Typename(RampBalls(1, 0)) & " ID:" & RampBalls(1, 1) & "type:" & RampType(1) & vbnewline & _
      Typename(RampBalls(2, 0)) & " ID:" & RampBalls(2, 1) & "type:" & RampType(2) & vbnewline & _
      Typename(RampBalls(3, 0)) & " ID:" & RampBalls(3, 1) & "type:" & RampType(3) & vbnewline & _
      Typename(RampBalls(4, 0)) & " ID:" & RampBalls(4, 1) & "type:" & RampType(4) & vbnewline & _
      Typename(RampBalls(5, 0)) & " ID:" & RampBalls(5, 1) & "type:" & RampType(5) & vbnewline & _
      " "
    End If
  next
End Sub

' WRemoveBall (BallId)
'    Description: This subroutine is called from the RampRollUpdate subroutine
'                 and is used to remove and stop the ball rolling sounds
Sub WRemoveBall(ID)   'Remove ball
  'Debug.Print "In WRemoveBall() + Remove ball from loop array"
  dim ballcount : ballcount = 0
  dim x : for x = 1 to Ubound(RampBalls)
    if ID = RampBalls(x, 1) then 'remove ball
      Set RampBalls(x, 0) = Nothing
      RampBalls(x, 1) = Empty
      RampType(x) = Empty
      StopSound("RampLoop" & x)
      StopSound("wireloop" & x)
    end If
    'if RampBalls(x,1) = Not IsEmpty(Rampballs(x,1) then ballcount = ballcount + 1
    if not IsEmpty(Rampballs(x,1)) then ballcount = ballcount + 1
  next
  if BallCount = 0 then RampBalls(0,0) = False  'if no balls in queue, disable timer update
End Sub

Sub RampRoll_Timer():RampRollUpdate:End Sub

Sub RampRollUpdate()    'Timer update
  dim x : for x = 1 to uBound(RampBalls)
    if Not IsEmpty(RampBalls(x,1) ) then
      if BallVel(RampBalls(x,0) ) > 1 then ' if ball is moving, play rolling sound
        If RampType(x) then
          PlaySound("RampLoop" & x & rampAmpFactor), -1, VolPlayfieldRoll(RampBalls(x,0)) * 1.1 * VolumeDial, AudioPan(RampBalls(x,0)), 0, BallPitchV(RampBalls(x,0)), 1, 0, AudioFade(RampBalls(x,0))
          StopSound("wireloop" & x & rampAmpFactor)
        Else
          StopSound("RampLoop" & x & rampAmpFactor)
          PlaySound("wireloop" & x & rampAmpFactor), -1, VolPlayfieldRoll(RampBalls(x,0)) * 1.1 * VolumeDial, AudioPan(RampBalls(x,0)), 0, BallPitch(RampBalls(x,0)), 1, 0, AudioFade(RampBalls(x,0))
        End If
        RampBalls(x, 2) = RampBalls(x, 2) + 1
      Else
        StopSound("RampLoop" & x & rampAmpFactor)
        StopSound("wireloop" & x & rampAmpFactor)
      end if
      if RampBalls(x,0).Z < 30 and RampBalls(x, 2) > RampMinLoops then  'if ball is on the PF, remove  it
        StopSound("RampLoop" & x & rampAmpFactor)
        StopSound("wireloop" & x & rampAmpFactor)
        Wremoveball RampBalls(x,1)
      End If
    Else
      StopSound("RampLoop" & x & rampAmpFactor)
      StopSound("wireloop" & x & rampAmpFactor)
    end if
  next
  if not RampBalls(0,0) then RampRoll.enabled = 0

End Sub

' This can be used to debug the Ramp Roll time.  You need to enable the tbWR timer on the TextBox
Sub tbWR_Timer()  'debug textbox
  me.text = "on? " & RampBalls(0, 0) & " timer: " & RampRoll.Enabled & vbnewline & _
  "1 " & Typename(RampBalls(1, 0)) & " ID:" & RampBalls(1, 1) & " type:" & RampType(1) & " Loops:" & RampBalls(1, 2) & vbnewline & _
  "2 " & Typename(RampBalls(2, 0)) & " ID:" & RampBalls(2, 1) & " type:" & RampType(2) & " Loops:" & RampBalls(2, 2) & vbnewline & _
  "3 " & Typename(RampBalls(3, 0)) & " ID:" & RampBalls(3, 1) & " type:" & RampType(3) & " Loops:" & RampBalls(3, 2) & vbnewline & _
  "4 " & Typename(RampBalls(4, 0)) & " ID:" & RampBalls(4, 1) & " type:" & RampType(4) & " Loops:" & RampBalls(4, 2) & vbnewline & _
  "5 " & Typename(RampBalls(5, 0)) & " ID:" & RampBalls(5, 1) & " type:" & RampType(5) & " Loops:" & RampBalls(5, 2) & vbnewline & _
  "6 " & Typename(RampBalls(6, 0)) & " ID:" & RampBalls(6, 1) & " type:" & RampType(6) & " Loops:" & RampBalls(6, 2) & vbnewline & _
  " "
End Sub


Function BallPitchV(ball) ' Calculates the pitch of the sound based on the ball speed Variation
  BallPitchV = pSlope(BallVel(ball), 1, -4000, 60, 7000)
End Function

Function BallPitch(ball) ' Calculates the pitch of the sound based on the ball speed
    BallPitch = pSlope(BallVel(ball), 1, -1000, 60, 10000)
End Function


'******************************************************
'**** END RAMP ROLLING SFX
'******************************************************





'******************************************************
'****  FLEEP MECHANICAL SOUNDS
'******************************************************

' This part in the script is an entire block that is dedicated to the physics sound system.
' Various scripts and sounds that may be pretty generic and could suit other WPC systems, but the most are tailored specifically for the TOM table

' Many of the sounds in this package can be added by creating collections and adding the appropriate objects to those collections.
' Create the following new collections:
'   Metals (all metal objects, metal walls, metal posts, metal wire guides)
'   Apron (the apron walls and plunger wall)
'   Walls (all wood or plastic walls)
'   Rollovers (wire rollover triggers, star triggers, or button triggers)
'   Targets (standup or drop targets, these are hit sounds only ... you will want to add separate dropping sounds for drop targets)
'   Gates (plate gates)
'   GatesWire (wire gates)
'   Rubbers (all rubbers including posts, sleeves, pegs, and bands)
' When creating the collections, make sure "Fire events for this collection" is checked.
' You'll also need to make sure "Has Hit Event" is checked for each object placed in these collections (not necessary for gates and triggers).
' Once the collections and objects are added, the save, close, and restart VPX.
'
' Tutorial vides by Apophis
' Part 1:   https://youtu.be/PbE2kNiam3g
' Part 2:   https://youtu.be/B5cm1Y8wQsk
' Part 3:   https://youtu.be/eLhWyuYOyGg


'///////////////////////////////  SOUNDS PARAMETERS  //////////////////////////////
Dim GlobalSoundLevel, CoinSoundLevel, PlungerReleaseSoundLevel, PlungerPullSoundLevel, NudgeLeftSoundLevel
Dim NudgeRightSoundLevel, NudgeCenterSoundLevel, StartButtonSoundLevel, RollingSoundFactor

CoinSoundLevel = 1                            'volume level; range [0, 1]
NudgeLeftSoundLevel = 1                         'volume level; range [0, 1]
NudgeRightSoundLevel = 1                        'volume level; range [0, 1]
NudgeCenterSoundLevel = 1                       'volume level; range [0, 1]
StartButtonSoundLevel = 0.1                       'volume level; range [0, 1]
PlungerReleaseSoundLevel = 0.8 '1 wjr                     'volume level; range [0, 1]
PlungerPullSoundLevel = 1                       'volume level; range [0, 1]
RollingSoundFactor = 1.1/5

'///////////////////////-----Solenoids, Kickers and Flash Relays-----///////////////////////
Dim FlipperUpAttackMinimumSoundLevel, FlipperUpAttackMaximumSoundLevel, FlipperUpAttackLeftSoundLevel, FlipperUpAttackRightSoundLevel
Dim FlipperUpSoundLevel, FlipperDownSoundLevel, FlipperLeftHitParm, FlipperRightHitParm
Dim SlingshotSoundLevel, BumperSoundFactor, KnockerSoundLevel

FlipperUpAttackMinimumSoundLevel = 0.010                      'volume level; range [0, 1]
FlipperUpAttackMaximumSoundLevel = 0.635                'volume level; range [0, 1]
FlipperUpSoundLevel = 1.0                                   'volume level; range [0, 1]
FlipperDownSoundLevel = 0.45                                  'volume level; range [0, 1]
FlipperLeftHitParm = FlipperUpSoundLevel                'sound helper; not configurable
FlipperRightHitParm = FlipperUpSoundLevel               'sound helper; not configurable
SlingshotSoundLevel = 0.95                        'volume level; range [0, 1]
BumperSoundFactor = 4.25                        'volume multiplier; must not be zero
KnockerSoundLevel = 1                           'volume level; range [0, 1]

'///////////////////////-----Ball Drops, Bumps and Collisions-----///////////////////////
Dim RubberStrongSoundFactor, RubberWeakSoundFactor, RubberFlipperSoundFactor,BallWithBallCollisionSoundFactor
Dim BallBouncePlayfieldSoftFactor, BallBouncePlayfieldHardFactor, PlasticRampDropToPlayfieldSoundLevel, WireRampDropToPlayfieldSoundLevel, DelayedBallDropOnPlayfieldSoundLevel
Dim WallImpactSoundFactor, MetalImpactSoundFactor, SubwaySoundLevel, SubwayEntrySoundLevel, ScoopEntrySoundLevel
Dim SaucerLockSoundLevel, SaucerKickSoundLevel

BallWithBallCollisionSoundFactor = 3.2                  'volume multiplier; must not be zero
RubberStrongSoundFactor = 0.055/5                     'volume multiplier; must not be zero
RubberWeakSoundFactor = 0.075/5                     'volume multiplier; must not be zero
RubberFlipperSoundFactor = 0.075/5                    'volume multiplier; must not be zero
BallBouncePlayfieldSoftFactor = 0.025                 'volume multiplier; must not be zero
BallBouncePlayfieldHardFactor = 0.025                 'volume multiplier; must not be zero
DelayedBallDropOnPlayfieldSoundLevel = 0.8                  'volume level; range [0, 1]
WallImpactSoundFactor = 0.075                     'volume multiplier; must not be zero
MetalImpactSoundFactor = 0.075/3
SaucerLockSoundLevel = 0.8
SaucerKickSoundLevel = 0.8

'///////////////////////-----Gates, Spinners, Rollovers and Targets-----///////////////////////

Dim GateSoundLevel, TargetSoundFactor, SpinnerSoundLevel, RolloverSoundLevel, DTSoundLevel

GateSoundLevel = 0.5/5                          'volume level; range [0, 1]
TargetSoundFactor = 0.0025 * 10                     'volume multiplier; must not be zero
DTSoundLevel = 0.25                           'volume multiplier; must not be zero
RolloverSoundLevel = 0.25                                       'volume level; range [0, 1]
SpinnerSoundLevel = 0.5                                       'volume level; range [0, 1]

'///////////////////////-----Ball Release, Guides and Drain-----///////////////////////
Dim DrainSoundLevel, BallReleaseSoundLevel, BottomArchBallGuideSoundFactor, FlipperBallGuideSoundFactor

DrainSoundLevel = 0.8                           'volume level; range [0, 1]
BallReleaseSoundLevel = 1                       'volume level; range [0, 1]
BottomArchBallGuideSoundFactor = 0.2                  'volume multiplier; must not be zero
FlipperBallGuideSoundFactor = 0.015                   'volume multiplier; must not be zero

'///////////////////////-----Loops and Lanes-----///////////////////////
Dim ArchSoundFactor
ArchSoundFactor = 0.025/5                         'volume multiplier; must not be zero


'/////////////////////////////  SOUND PLAYBACK FUNCTIONS  ////////////////////////////
'/////////////////////////////  POSITIONAL SOUND PLAYBACK METHODS  ////////////////////////////
' Positional sound playback methods will play a sound, depending on the X,Y position of the table element or depending on ActiveBall object position
' These are similar subroutines that are less complicated to use (e.g. simply use standard parameters for the PlaySound call)
' For surround setup - positional sound playback functions will fade between front and rear surround channels and pan between left and right channels
' For stereo setup - positional sound playback functions will only pan between left and right channels
' For mono setup - positional sound playback functions will not pan between left and right channels and will not fade between front and rear channels

' PlaySound full syntax - PlaySound(string, int loopcount, float volume, float pan, float randompitch, int pitch, bool useexisting, bool restart, float front_rear_fade)
' Note - These functions will not work (currently) for walls/slingshots as these do not feature a simple, single X,Y position
Sub PlaySoundAtLevelStatic(playsoundparams, aVol, tableobj)
  PlaySound playsoundparams, 0, aVol * VolumeDial, AudioPan(tableobj), 0, 0, 0, 0, AudioFade(tableobj)
End Sub

Sub PlaySoundAtLevelExistingStatic(playsoundparams, aVol, tableobj)
  PlaySound playsoundparams, 0, aVol * VolumeDial, AudioPan(tableobj), 0, 0, 1, 0, AudioFade(tableobj)
End Sub

Sub PlaySoundAtLevelStaticLoop(playsoundparams, aVol, tableobj)
  PlaySound playsoundparams, -1, aVol * VolumeDial, AudioPan(tableobj), 0, 0, 0, 0, AudioFade(tableobj)
End Sub

Sub PlaySoundAtLevelStaticRandomPitch(playsoundparams, aVol, randomPitch, tableobj)
  PlaySound playsoundparams, 0, aVol * VolumeDial, AudioPan(tableobj), randomPitch, 0, 0, 0, AudioFade(tableobj)
End Sub

Sub PlaySoundAtLevelActiveBall(playsoundparams, aVol)
  PlaySound playsoundparams, 0, aVol * VolumeDial, AudioPan(ActiveBall), 0, 0, 0, 0, AudioFade(ActiveBall)
End Sub

Sub PlaySoundAtLevelExistingActiveBall(playsoundparams, aVol)
  PlaySound playsoundparams, 0, aVol * VolumeDial, AudioPan(ActiveBall), 0, 0, 1, 0, AudioFade(ActiveBall)
End Sub

Sub PlaySoundAtLeveTimerActiveBall(playsoundparams, aVol, ballvariable)
  PlaySound playsoundparams, 0, aVol * VolumeDial, AudioPan(ballvariable), 0, 0, 0, 0, AudioFade(ballvariable)
End Sub

Sub PlaySoundAtLevelTimerExistingActiveBall(playsoundparams, aVol, ballvariable)
  PlaySound playsoundparams, 0, aVol * VolumeDial, AudioPan(ballvariable), 0, 0, 1, 0, AudioFade(ballvariable)
End Sub

Sub PlaySoundAtLevelRoll(playsoundparams, aVol, pitch)
  PlaySound playsoundparams, -1, aVol * VolumeDial, AudioPan(tableobj), randomPitch, 0, 0, 0, AudioFade(tableobj)
End Sub

' Previous Positional Sound Subs

Sub PlaySoundAt(soundname, tableobj)
  PlaySound soundname, 1, 1 * VolumeDial, AudioPan(tableobj), 0,0,0, 1, AudioFade(tableobj)
End Sub

Sub PlaySoundAtVol(soundname, tableobj, aVol)
  PlaySound soundname, 1, aVol * VolumeDial, AudioPan(tableobj), 0,0,0, 1, AudioFade(tableobj)
End Sub

Sub PlaySoundAtBall(soundname)
  PlaySoundAt soundname, ActiveBall
End Sub

Sub PlaySoundAtBallVol (Soundname, aVol)
  Playsound soundname, 1,aVol * VolumeDial, AudioPan(ActiveBall), 0,0,0, 1, AudioFade(ActiveBall)
End Sub

Sub PlaySoundAtBallVolM (Soundname, aVol)
  Playsound soundname, 1,aVol * VolumeDial, AudioPan(ActiveBall), 0,0,0, 0, AudioFade(ActiveBall)
End Sub

Sub PlaySoundAtVolLoops(sound, tableobj, Vol, Loops)
  PlaySound sound, Loops, Vol * VolumeDial, AudioPan(tableobj), 0,0,0, 1, AudioFade(tableobj)
End Sub


'******************************************************
'  Fleep  Supporting Ball & Sound Functions
'******************************************************

Function AudioFade(tableobj) ' Fades between front and back of the table (for surround systems or 2x2 speakers, etc), depending on the Y position on the table. "table1" is the name of the table
  Dim tmp
  tmp = tableobj.y * 2 / tableheight-1

  if tmp > 7000 Then
    tmp = 7000
  elseif tmp < -7000 Then
    tmp = -7000
  end if

  If tmp > 0 Then
    AudioFade = Csng(tmp ^10)
  Else
    AudioFade = Csng(-((- tmp) ^10) )
  End If
End Function

Function AudioPan(tableobj) ' Calculates the pan for a tableobj based on the X position on the table. "table1" is the name of the table
  Dim tmp
  tmp = tableobj.x * 2 / tablewidth-1

  if tmp > 7000 Then
    tmp = 7000
  elseif tmp < -7000 Then
    tmp = -7000
  end if

  If tmp > 0 Then
    AudioPan = Csng(tmp ^10)
  Else
    AudioPan = Csng(-((- tmp) ^10) )
  End If
End Function

Function Vol(ball) ' Calculates the volume of the sound based on the ball speed
  Vol = Csng(BallVel(ball) ^2)
End Function

Function Volz(ball) ' Calculates the volume of the sound based on the ball speed
  Volz = Csng((ball.velz) ^2)
End Function

Function Pitch(ball) ' Calculates the pitch of the sound based on the ball speed
  Pitch = BallVel(ball) * 20
End Function

Function BallVel(ball) 'Calculates the ball speed
  BallVel = INT(SQR((ball.VelX ^2) + (ball.VelY ^2) ) )
End Function

Function VolPlayfieldRoll(ball) ' Calculates the roll volume of the sound based on the ball speed
  VolPlayfieldRoll = RollingSoundFactor * 0.0005 * Csng(BallVel(ball) ^3)
End Function

Function PitchPlayfieldRoll(ball) ' Calculates the roll pitch of the sound based on the ball speed
  PitchPlayfieldRoll = BallVel(ball) ^2 * 15
End Function

Function RndInt(min, max)
  RndInt = Int(Rnd() * (max-min + 1) + min)' Sets a random number integer between min and max
End Function

Function RndNum(min, max)
  RndNum = Rnd() * (max-min) + min' Sets a random number between min and max
End Function

'/////////////////////////////  GENERAL SOUND SUBROUTINES  ////////////////////////////
Sub SoundStartButton()
  PlaySound ("Start_Button"), 0, StartButtonSoundLevel, 0, 0.25
End Sub

Sub SoundNudgeLeft()
  PlaySound ("Nudge_" & Int(Rnd*2)+1), 0, NudgeLeftSoundLevel * VolumeDial, -0.1, 0.25
End Sub

Sub SoundNudgeRight()
  PlaySound ("Nudge_" & Int(Rnd*2)+1), 0, NudgeRightSoundLevel * VolumeDial, 0.1, 0.25
End Sub

Sub SoundNudgeCenter()
  PlaySound ("Nudge_" & Int(Rnd*2)+1), 0, NudgeCenterSoundLevel * VolumeDial, 0, 0.25
End Sub


Sub SoundPlungerPull()
  PlaySoundAtLevelStatic ("Plunger_Pull_1"), PlungerPullSoundLevel, Plunger
End Sub

Sub SoundPlungerReleaseBall()
  PlaySoundAtLevelStatic ("Plunger_Release_Ball"), PlungerReleaseSoundLevel, Plunger
End Sub

Sub SoundPlungerReleaseNoBall()
  PlaySoundAtLevelStatic ("Plunger_Release_No_Ball"), PlungerReleaseSoundLevel, Plunger
End Sub


'/////////////////////////////  KNOCKER SOLENOID  ////////////////////////////
Sub KnockerSolenoid()
  PlaySoundAtLevelStatic SoundFX("Knocker_1",DOFKnocker), KnockerSoundLevel, KnockerPosition
End Sub

'/////////////////////////////  DRAIN SOUNDS  ////////////////////////////
Sub RandomSoundDrain(drainswitch)
  PlaySoundAtLevelStatic ("Drain_" & Int(Rnd*11)+1), DrainSoundLevel, drainswitch
End Sub

'/////////////////////////////  TROUGH BALL RELEASE SOLENOID SOUNDS  ////////////////////////////

Sub RandomSoundBallRelease(drainswitch)
  PlaySoundAtLevelStatic SoundFX("BallRelease" & Int(Rnd*7)+1,DOFContactors), BallReleaseSoundLevel, drainswitch
End Sub

'/////////////////////////////  SLINGSHOT SOLENOID SOUNDS  ////////////////////////////
Sub RandomSoundSlingshotLeft(sling)
  PlaySoundAtLevelStatic SoundFX("Sling_L" & Int(Rnd*10)+1,DOFContactors), SlingshotSoundLevel, Sling
End Sub

Sub RandomSoundSlingshotRight(sling)
  PlaySoundAtLevelStatic SoundFX("Sling_R" & Int(Rnd*8)+1,DOFContactors), SlingshotSoundLevel, Sling
End Sub

'/////////////////////////////  BUMPER SOLENOID SOUNDS  ////////////////////////////
Sub RandomSoundBumperTop(Bump)
  PlaySoundAtLevelStatic SoundFX("Bumpers_Top_" & Int(Rnd*5)+1,DOFContactors), Vol(ActiveBall) * BumperSoundFactor, Bump
End Sub

Sub RandomSoundBumperMiddle(Bump)
  PlaySoundAtLevelStatic SoundFX("Bumpers_Middle_" & Int(Rnd*5)+1,DOFContactors), Vol(ActiveBall) * BumperSoundFactor, Bump
End Sub

Sub RandomSoundBumperBottom(Bump)
  PlaySoundAtLevelStatic SoundFX("Bumpers_Bottom_" & Int(Rnd*5)+1,DOFContactors), Vol(ActiveBall) * BumperSoundFactor, Bump
End Sub

'/////////////////////////////  SPINNER SOUNDS  ////////////////////////////
Sub SoundSpinner(spinnerswitch)
  PlaySoundAtLevelStatic ("Spinner"), SpinnerSoundLevel, spinnerswitch
End Sub


'/////////////////////////////  FLIPPER BATS SOUND SUBROUTINES  ////////////////////////////
'/////////////////////////////  FLIPPER BATS SOLENOID ATTACK SOUND  ////////////////////////////
Sub SoundFlipperUpAttackLeft(flipper)
  FlipperUpAttackLeftSoundLevel = RndNum(FlipperUpAttackMinimumSoundLevel, FlipperUpAttackMaximumSoundLevel)
  PlaySoundAtLevelStatic ("Flipper_Attack-L01"), FlipperUpAttackLeftSoundLevel, flipper
End Sub

Sub SoundFlipperUpAttackRight(flipper)
  FlipperUpAttackRightSoundLevel = RndNum(FlipperUpAttackMinimumSoundLevel, FlipperUpAttackMaximumSoundLevel)
  PlaySoundAtLevelStatic ("Flipper_Attack-R01"), FlipperUpAttackLeftSoundLevel, flipper
End Sub

'/////////////////////////////  FLIPPER BATS SOLENOID CORE SOUND  ////////////////////////////
Sub RandomSoundFlipperUpLeft(flipper)
  PlaySoundAtLevelStatic SoundFX("Flipper_L0" & Int(Rnd*9)+1,DOFFlippers), FlipperLeftHitParm, Flipper
End Sub

Sub RandomSoundFlipperUpRight(flipper)
  PlaySoundAtLevelStatic SoundFX("Flipper_R0" & Int(Rnd*9)+1,DOFFlippers), FlipperRightHitParm, Flipper
End Sub

Sub RandomSoundReflipUpLeft(flipper)
  PlaySoundAtLevelStatic SoundFX("Flipper_ReFlip_L0" & Int(Rnd*3)+1,DOFFlippers), (RndNum(0.8, 1))*FlipperUpSoundLevel, Flipper
End Sub

Sub RandomSoundReflipUpRight(flipper)
  PlaySoundAtLevelStatic SoundFX("Flipper_ReFlip_R0" & Int(Rnd*3)+1,DOFFlippers), (RndNum(0.8, 1))*FlipperUpSoundLevel, Flipper
End Sub

Sub RandomSoundFlipperDownLeft(flipper)
  PlaySoundAtLevelStatic SoundFX("Flipper_Left_Down_" & Int(Rnd*7)+1,DOFFlippers), FlipperDownSoundLevel, Flipper
End Sub

Sub RandomSoundFlipperDownRight(flipper)
  PlaySoundAtLevelStatic SoundFX("Flipper_Right_Down_" & Int(Rnd*8)+1,DOFFlippers), FlipperDownSoundLevel, Flipper
End Sub

'/////////////////////////////  FLIPPER BATS BALL COLLIDE SOUND  ////////////////////////////

Sub LeftFlipperCollide(parm)
  FlipperLeftHitParm = parm/10
  If FlipperLeftHitParm > 1 Then
    FlipperLeftHitParm = 1
  End If
  FlipperLeftHitParm = FlipperUpSoundLevel * FlipperLeftHitParm
  RandomSoundRubberFlipper(parm)
End Sub

Sub RightFlipperCollide(parm)
  FlipperRightHitParm = parm/10
  If FlipperRightHitParm > 1 Then
    FlipperRightHitParm = 1
  End If
  FlipperRightHitParm = FlipperUpSoundLevel * FlipperRightHitParm
  RandomSoundRubberFlipper(parm)
End Sub

Sub RandomSoundRubberFlipper(parm)
  PlaySoundAtLevelActiveBall ("Flipper_Rubber_" & Int(Rnd*7)+1), parm  * RubberFlipperSoundFactor
End Sub

'/////////////////////////////  ROLLOVER SOUNDS  ////////////////////////////
Sub RandomSoundRollover()
  PlaySoundAtLevelActiveBall ("Rollover_" & Int(Rnd*4)+1), RolloverSoundLevel
End Sub

Sub Rollovers_Hit(idx)
  RandomSoundRollover
End Sub

'/////////////////////////////  VARIOUS PLAYFIELD SOUND SUBROUTINES  ////////////////////////////
'/////////////////////////////  RUBBERS AND POSTS  ////////////////////////////
'/////////////////////////////  RUBBERS - EVENTS  ////////////////////////////
Sub Rubbers_Hit(idx)
  dim finalspeed
  finalspeed=SQR(activeball.velx * activeball.velx + activeball.vely * activeball.vely)
  If finalspeed > 5 then
    RandomSoundRubberStrong 1
  End if
  If finalspeed <= 5 then
    RandomSoundRubberWeak()
  End If
End Sub

'/////////////////////////////  RUBBERS AND POSTS - STRONG IMPACTS  ////////////////////////////
Sub RandomSoundRubberStrong(voladj)
  Select Case Int(Rnd*10)+1
    Case 1 : PlaySoundAtLevelActiveBall ("Rubber_Strong_1"), Vol(ActiveBall) * RubberStrongSoundFactor*voladj
    Case 2 : PlaySoundAtLevelActiveBall ("Rubber_Strong_2"), Vol(ActiveBall) * RubberStrongSoundFactor*voladj
    Case 3 : PlaySoundAtLevelActiveBall ("Rubber_Strong_3"), Vol(ActiveBall) * RubberStrongSoundFactor*voladj
    Case 4 : PlaySoundAtLevelActiveBall ("Rubber_Strong_4"), Vol(ActiveBall) * RubberStrongSoundFactor*voladj
    Case 5 : PlaySoundAtLevelActiveBall ("Rubber_Strong_5"), Vol(ActiveBall) * RubberStrongSoundFactor*voladj
    Case 6 : PlaySoundAtLevelActiveBall ("Rubber_Strong_6"), Vol(ActiveBall) * RubberStrongSoundFactor*voladj
    Case 7 : PlaySoundAtLevelActiveBall ("Rubber_Strong_7"), Vol(ActiveBall) * RubberStrongSoundFactor*voladj
    Case 8 : PlaySoundAtLevelActiveBall ("Rubber_Strong_8"), Vol(ActiveBall) * RubberStrongSoundFactor*voladj
    Case 9 : PlaySoundAtLevelActiveBall ("Rubber_Strong_9"), Vol(ActiveBall) * RubberStrongSoundFactor*voladj
    Case 10 : PlaySoundAtLevelActiveBall ("Rubber_1_Hard"), Vol(ActiveBall) * RubberStrongSoundFactor * 0.6*voladj
  End Select
End Sub

'/////////////////////////////  RUBBERS AND POSTS - WEAK IMPACTS  ////////////////////////////
Sub RandomSoundRubberWeak()
  PlaySoundAtLevelActiveBall ("Rubber_" & Int(Rnd*9)+1), Vol(ActiveBall) * RubberWeakSoundFactor
End Sub

'/////////////////////////////  WALL IMPACTS  ////////////////////////////
Sub Walls_Hit(idx)
  RandomSoundWall()
End Sub

Sub RandomSoundWall()
  dim finalspeed
  finalspeed=SQR(activeball.velx * activeball.velx + activeball.vely * activeball.vely)
  If finalspeed > 16 then
    Select Case Int(Rnd*5)+1
      Case 1 : PlaySoundAtLevelExistingActiveBall ("Wall_Hit_1"), Vol(ActiveBall) * WallImpactSoundFactor
      Case 2 : PlaySoundAtLevelExistingActiveBall ("Wall_Hit_2"), Vol(ActiveBall) * WallImpactSoundFactor
      Case 3 : PlaySoundAtLevelExistingActiveBall ("Wall_Hit_5"), Vol(ActiveBall) * WallImpactSoundFactor
      Case 4 : PlaySoundAtLevelExistingActiveBall ("Wall_Hit_7"), Vol(ActiveBall) * WallImpactSoundFactor
      Case 5 : PlaySoundAtLevelExistingActiveBall ("Wall_Hit_9"), Vol(ActiveBall) * WallImpactSoundFactor
    End Select
  End if
  If finalspeed >= 6 AND finalspeed <= 16 then
    Select Case Int(Rnd*4)+1
      Case 1 : PlaySoundAtLevelExistingActiveBall ("Wall_Hit_3"), Vol(ActiveBall) * WallImpactSoundFactor
      Case 2 : PlaySoundAtLevelExistingActiveBall ("Wall_Hit_4"), Vol(ActiveBall) * WallImpactSoundFactor
      Case 3 : PlaySoundAtLevelExistingActiveBall ("Wall_Hit_6"), Vol(ActiveBall) * WallImpactSoundFactor
      Case 4 : PlaySoundAtLevelExistingActiveBall ("Wall_Hit_8"), Vol(ActiveBall) * WallImpactSoundFactor
    End Select
  End If
  If finalspeed < 6 Then
    Select Case Int(Rnd*3)+1
      Case 1 : PlaySoundAtLevelExistingActiveBall ("Wall_Hit_4"), Vol(ActiveBall) * WallImpactSoundFactor
      Case 2 : PlaySoundAtLevelExistingActiveBall ("Wall_Hit_6"), Vol(ActiveBall) * WallImpactSoundFactor
      Case 3 : PlaySoundAtLevelExistingActiveBall ("Wall_Hit_8"), Vol(ActiveBall) * WallImpactSoundFactor
    End Select
  End if
End Sub

'/////////////////////////////  METAL TOUCH SOUNDS  ////////////////////////////
Sub RandomSoundMetal()
  PlaySoundAtLevelActiveBall ("Metal_Touch_" & Int(Rnd*13)+1), Vol(ActiveBall) * MetalImpactSoundFactor
End Sub

'/////////////////////////////  METAL - EVENTS  ////////////////////////////

Sub Metals_Hit (idx)
  RandomSoundMetal
End Sub

Sub ShooterDiverter_collide(idx)
  RandomSoundMetal
End Sub

'/////////////////////////////  BOTTOM ARCH BALL GUIDE  ////////////////////////////
'/////////////////////////////  BOTTOM ARCH BALL GUIDE - SOFT BOUNCES  ////////////////////////////
Sub RandomSoundBottomArchBallGuide()
  dim finalspeed
  finalspeed=SQR(activeball.velx * activeball.velx + activeball.vely * activeball.vely)
  If finalspeed > 16 then
    PlaySoundAtLevelActiveBall ("Apron_Bounce_"& Int(Rnd*2)+1), Vol(ActiveBall) * BottomArchBallGuideSoundFactor
  End if
  If finalspeed >= 6 AND finalspeed <= 16 then
    Select Case Int(Rnd*2)+1
      Case 1 : PlaySoundAtLevelActiveBall ("Apron_Bounce_1"), Vol(ActiveBall) * BottomArchBallGuideSoundFactor
      Case 2 : PlaySoundAtLevelActiveBall ("Apron_Bounce_Soft_1"), Vol(ActiveBall) * BottomArchBallGuideSoundFactor
    End Select
  End If
  If finalspeed < 6 Then
    Select Case Int(Rnd*2)+1
      Case 1 : PlaySoundAtLevelActiveBall ("Apron_Bounce_Soft_1"), Vol(ActiveBall) * BottomArchBallGuideSoundFactor
      Case 2 : PlaySoundAtLevelActiveBall ("Apron_Medium_3"), Vol(ActiveBall) * BottomArchBallGuideSoundFactor
    End Select
  End if
End Sub

'/////////////////////////////  BOTTOM ARCH BALL GUIDE - HARD HITS  ////////////////////////////
Sub RandomSoundBottomArchBallGuideHardHit()
  PlaySoundAtLevelActiveBall ("Apron_Hard_Hit_" & Int(Rnd*3)+1), BottomArchBallGuideSoundFactor * 0.25
End Sub

Sub Apron_Hit (idx)
  If Abs(cor.ballvelx(activeball.id) < 4) and cor.ballvely(activeball.id) > 7 then
    RandomSoundBottomArchBallGuideHardHit()
  Else
    RandomSoundBottomArchBallGuide
  End If
End Sub

'/////////////////////////////  FLIPPER BALL GUIDE  ////////////////////////////
Sub RandomSoundFlipperBallGuide()
  dim finalspeed
  finalspeed=SQR(activeball.velx * activeball.velx + activeball.vely * activeball.vely)
  If finalspeed > 16 then
    Select Case Int(Rnd*2)+1
      Case 1 : PlaySoundAtLevelActiveBall ("Apron_Hard_1"),  Vol(ActiveBall) * FlipperBallGuideSoundFactor
      Case 2 : PlaySoundAtLevelActiveBall ("Apron_Hard_2"),  Vol(ActiveBall) * 0.8 * FlipperBallGuideSoundFactor
    End Select
  End if
  If finalspeed >= 6 AND finalspeed <= 16 then
    PlaySoundAtLevelActiveBall ("Apron_Medium_" & Int(Rnd*3)+1),  Vol(ActiveBall) * FlipperBallGuideSoundFactor
  End If
  If finalspeed < 6 Then
    PlaySoundAtLevelActiveBall ("Apron_Soft_" & Int(Rnd*7)+1),  Vol(ActiveBall) * FlipperBallGuideSoundFactor
  End If
End Sub

'/////////////////////////////  TARGET HIT SOUNDS  ////////////////////////////
Sub RandomSoundTargetHitStrong()
  PlaySoundAtLevelActiveBall SoundFX("Target_Hit_" & Int(Rnd*4)+5,DOFTargets), Vol(ActiveBall) * 0.45 * TargetSoundFactor
End Sub

Sub RandomSoundTargetHitWeak()
  PlaySoundAtLevelActiveBall SoundFX("Target_Hit_" & Int(Rnd*4)+1,DOFTargets), Vol(ActiveBall) * TargetSoundFactor
End Sub

Sub PlayTargetSound()
  dim finalspeed
  finalspeed=SQR(activeball.velx * activeball.velx + activeball.vely * activeball.vely)
  If finalspeed > 10 then
    RandomSoundTargetHitStrong()
    RandomSoundBallBouncePlayfieldSoft Activeball
  Else
    RandomSoundTargetHitWeak()
  End If
End Sub

Sub Targets_Hit (idx)
  PlayTargetSound
End Sub

'/////////////////////////////  BALL BOUNCE SOUNDS  ////////////////////////////
Sub RandomSoundBallBouncePlayfieldSoft(aBall)
  Select Case Int(Rnd*9)+1
    Case 1 : PlaySoundAtLevelStatic ("Ball_Bounce_Playfield_Soft_1"), volz(aBall) * BallBouncePlayfieldSoftFactor, aBall
    Case 2 : PlaySoundAtLevelStatic ("Ball_Bounce_Playfield_Soft_2"), volz(aBall) * BallBouncePlayfieldSoftFactor * 0.5, aBall
    Case 3 : PlaySoundAtLevelStatic ("Ball_Bounce_Playfield_Soft_3"), volz(aBall) * BallBouncePlayfieldSoftFactor * 0.8, aBall
    Case 4 : PlaySoundAtLevelStatic ("Ball_Bounce_Playfield_Soft_4"), volz(aBall) * BallBouncePlayfieldSoftFactor * 0.5, aBall
    Case 5 : PlaySoundAtLevelStatic ("Ball_Bounce_Playfield_Soft_5"), volz(aBall) * BallBouncePlayfieldSoftFactor, aBall
    Case 6 : PlaySoundAtLevelStatic ("Ball_Bounce_Playfield_Hard_1"), volz(aBall) * BallBouncePlayfieldSoftFactor * 0.2, aBall
    Case 7 : PlaySoundAtLevelStatic ("Ball_Bounce_Playfield_Hard_2"), volz(aBall) * BallBouncePlayfieldSoftFactor * 0.2, aBall
    Case 8 : PlaySoundAtLevelStatic ("Ball_Bounce_Playfield_Hard_5"), volz(aBall) * BallBouncePlayfieldSoftFactor * 0.2, aBall
    Case 9 : PlaySoundAtLevelStatic ("Ball_Bounce_Playfield_Hard_7"), volz(aBall) * BallBouncePlayfieldSoftFactor * 0.3, aBall
  End Select
End Sub

Sub RandomSoundBallBouncePlayfieldHard(aBall)
  PlaySoundAtLevelStatic ("Ball_Bounce_Playfield_Hard_" & Int(Rnd*7)+1), volz(aBall) * BallBouncePlayfieldHardFactor, aBall
End Sub

'/////////////////////////////  DELAYED DROP - TO PLAYFIELD - SOUND  ////////////////////////////
Sub RandomSoundDelayedBallDropOnPlayfield(aBall)
  Select Case Int(Rnd*5)+1
    Case 1 : PlaySoundAtLevelStatic ("Ball_Drop_Playfield_1_Delayed"), DelayedBallDropOnPlayfieldSoundLevel, aBall
    Case 2 : PlaySoundAtLevelStatic ("Ball_Drop_Playfield_2_Delayed"), DelayedBallDropOnPlayfieldSoundLevel, aBall
    Case 3 : PlaySoundAtLevelStatic ("Ball_Drop_Playfield_3_Delayed"), DelayedBallDropOnPlayfieldSoundLevel, aBall
    Case 4 : PlaySoundAtLevelStatic ("Ball_Drop_Playfield_4_Delayed"), DelayedBallDropOnPlayfieldSoundLevel, aBall
    Case 5 : PlaySoundAtLevelStatic ("Ball_Drop_Playfield_5_Delayed"), DelayedBallDropOnPlayfieldSoundLevel, aBall
  End Select
End Sub

'/////////////////////////////  BALL GATES AND BRACKET GATES SOUNDS  ////////////////////////////

Sub SoundPlayfieldGate()
  PlaySoundAtLevelStatic ("Gate_FastTrigger_" & Int(Rnd*2)+1), GateSoundLevel, Activeball
End Sub

Sub SoundHeavyGate()
  PlaySoundAtLevelStatic ("Gate_2"), GateSoundLevel, Activeball
End Sub

Sub Gates_hit(idx)
  SoundHeavyGate
End Sub

Sub GatesWire_hit(idx)
  SoundPlayfieldGate
End Sub

'/////////////////////////////  LEFT LANE ENTRANCE - SOUNDS  ////////////////////////////

Sub RandomSoundLeftArch()
  PlaySoundAtLevelActiveBall ("Arch_L" & Int(Rnd*4)+1), Vol(ActiveBall) * ArchSoundFactor
End Sub

Sub RandomSoundRightArch()
  PlaySoundAtLevelActiveBall ("Arch_R" & Int(Rnd*4)+1), Vol(ActiveBall) * ArchSoundFactor
End Sub


Sub Arch1_hit()
  If Activeball.velx > 1 Then SoundPlayfieldGate
  StopSound "Arch_L1"
  StopSound "Arch_L2"
  StopSound "Arch_L3"
  StopSound "Arch_L4"
End Sub

Sub Arch1_unhit()
  If activeball.velx < -8 Then
    RandomSoundRightArch
  End If
End Sub

Sub Arch2_hit()
  If Activeball.velx < 1 Then SoundPlayfieldGate
  StopSound "Arch_R1"
  StopSound "Arch_R2"
  StopSound "Arch_R3"
  StopSound "Arch_R4"
End Sub

Sub Arch2_unhit()
  If activeball.velx > 10 Then
    RandomSoundLeftArch
  End If
End Sub

'/////////////////////////////  SAUCERS (KICKER HOLES)  ////////////////////////////

Sub SoundSaucerLock()
  PlaySoundAtLevelStatic ("Saucer_Enter_" & Int(Rnd*2)+1), SaucerLockSoundLevel, Activeball
End Sub

Sub SoundSaucerKick(scenario, saucer)
  Select Case scenario
    Case 0: PlaySoundAtLevelStatic SoundFX("Saucer_Empty", DOFContactors), SaucerKickSoundLevel, saucer
    Case 1: PlaySoundAtLevelStatic SoundFX("Saucer_Kick", DOFContactors), SaucerKickSoundLevel, saucer
  End Select
End Sub

'/////////////////////////////  BALL COLLISION SOUND  ////////////////////////////
Sub OnBallBallCollision(ball1, ball2, velocity)
  Dim snd
  Select Case Int(Rnd*7)+1
    Case 1 : snd = "Ball_Collide_1"
    Case 2 : snd = "Ball_Collide_2"
    Case 3 : snd = "Ball_Collide_3"
    Case 4 : snd = "Ball_Collide_4"
    Case 5 : snd = "Ball_Collide_5"
    Case 6 : snd = "Ball_Collide_6"
    Case 7 : snd = "Ball_Collide_7"
  End Select

  PlaySound (snd), 0, Csng(velocity) ^2 / 200 * BallWithBallCollisionSoundFactor * VolumeDial, AudioPan(ball1), 0, Pitch(ball1), 0, 0, AudioFade(ball1)
End Sub


'/////////////////////////////////////////////////////////////////
'         End Mechanical Sounds
'/////////////////////////////////////////////////////////////////

'******************************************************
'****  FLEEP MECHANICAL SOUNDS
'******************************************************


'******************************************************
'****  LAMPZ by nFozzy
'******************************************************
'
' Lampz is a utility designed to manage and fade the lights and light-related objects on a table that is being driven by a ROM.
' To set up Lampz, one must populate the Lampz.MassAssign array with VPX Light objects, where the index of the MassAssign array
' corrisponds to the ROM index of the associated light. More that one Light object can be associated with a single MassAssign index (not shown in this example)
' Optionally, callbacks can be assigned for each index using the Lampz.Callback array. This is very useful for allowing 3D Insert primitives
' to be controlled by the ROM. Note, the aLvl parameter (i.e. the fading level that ranges between 0 and 1) is appended to the callback call.
'
Dim NullFader : set NullFader = new NullFadingObject
Dim Lampz : Set Lampz = New LampFader
Dim ModLampz : Set ModLampz = New DynamicLamps
InitLampsNF              ' Setup lamp assignments
LampTimer.Interval = -1
LampTimer.Enabled = 1

Sub LampTimer_Timer()
  dim x, chglamp
  chgLamp = Controller.ChangedLamps
  If Not IsEmpty(chglamp) Then
    For x = 0 To UBound(chglamp)      'nmbr = chglamp(x, 0), state = chglamp(x, 1)
      Lampz.state(chglamp(x, 0)) = chglamp(x, 1)
    next
  End If
  Lampz.Update1 'update (fading logic only)
  ModLampz.Update1
End Sub

dim FrameTime, InitFrameTime : InitFrameTime = 0
LampTimer2.Interval = -1
LampTimer2.Enabled = True
Sub LampTimer2_Timer()
  FrameTime = gametime - InitFrameTime : InitFrameTime = gametime 'Count frametime. Unused atm?
  Lampz.Update 'updates on frametime (Object updates only)
  ModLampz.Update
End Sub

Function FlashLevelToIndex(Input, MaxSize)
  FlashLevelToIndex = cInt(MaxSize * Input)
End Function

'***Material Swap***
'Fade material for green, red, yellow colored Bulb prims
Sub FadeMaterialColoredBulb(pri, group, ByVal aLvl) 'cp's script
  ' if Lampz.UseFunction then aLvl = LampFilter(aLvl) 'Callbacks don't get this filter automatically
  if Lampz.UseFunction then aLvl = Lampz.FilterOut(aLvl)  'Callbacks don't get this filter automatically
  Select case FlashLevelToIndex(aLvl, 3)
    Case 0:pri.Material = group(0) 'Off
    Case 1:pri.Material = group(1) 'Fading...
    Case 2:pri.Material = group(2) 'Fading...
    Case 3:pri.Material = group(3) 'Full
  End Select
  'if tb.text <> pri.image then tb.text = pri.image : 'debug.print pri.image end If 'debug
  pri.blenddisablelighting = aLvl * 1 'Intensity Adjustment
End Sub


'Fade material for red, yellow colored bulb Filiment prims
Sub FadeMaterialColoredFiliment(pri, group, ByVal aLvl) 'cp's script
  ' if Lampz.UseFunction then aLvl = LampFilter(aLvl) 'Callbacks don't get this filter automatically
  if Lampz.UseFunction then aLvl = Lampz.FilterOut(aLvl)  'Callbacks don't get this filter automatically
  Select case FlashLevelToIndex(aLvl, 3)
    Case 0:pri.Material = group(0) 'Off
    Case 1:pri.Material = group(1) 'Fading...
    Case 2:pri.Material = group(2) 'Fading...
    Case 3:pri.Material = group(3) 'Full
  End Select
  'if tb.text <> pri.image then tb.text = pri.image : 'debug.print pri.image end If 'debug
  pri.blenddisablelighting = aLvl * 50  'Intensity Adjustment
End Sub


Sub DisableLighting(pri, DLintensity, ByVal aLvl) 'cp's script  DLintensity = disabled lighting intesity
  if Lampz.UseFunction then aLvl = Lampz.FilterOut(aLvl)  'Callbacks don't get this filter automatically
  pri.blenddisablelighting = aLvl * DLintensity
End Sub

Sub InitLampsNF()

  'Filtering (comment out to disable)
  Lampz.Filter = "LampFilter" 'Puts all lamp intensityscale output (no callbacks) through this function before updating
  ModLampz.Filter = "LampFilter"

  'Adjust fading speeds (1 / full MS fading time)
  dim x : for x = 0 to 140 : Lampz.FadeSpeedUp(x) = 1/2 : Lampz.FadeSpeedDown(x) = 1/10 : next
  for x = 0 to 5 : ModLampz.FadeSpeedUp(x) = 1/5 : ModLampz.FadeSpeedDown(x) = 1/40 : Next
  for x = 6 to 28 : ModLampz.FadeSpeedUp(x) = 1/10 : ModLampz.FadeSpeedDown(x) = 1/40 : Next

  'Lampz Assignments
  '  In a ROM based table, the lamp ID is used to set the state of the Lampz objects
  'MassAssign is an optional way to do assignments. It'll create arrays automatically / append objects to existing arrays

  Lampz.MassAssign(11)= l11
  Lampz.MassAssign(11)= h11
  Lampz.Callback(11) = "DisableLighting p11, 1,"
  Lampz.MassAssign(11)= l11a
  Lampz.MassAssign(11)= h11a
  Lampz.Callback(11) = "DisableLighting p11a, 1,"
  Lampz.MassAssign(12)= l12
  Lampz.MassAssign(12)= h12
  Lampz.Callback(12) = "DisableLighting p12, 20,"
  Lampz.MassAssign(13)= l13
  Lampz.MassAssign(13)= h13
  Lampz.Callback(13) = "DisableLighting p13, 20,"
  Lampz.MassAssign(14)= l14
  Lampz.MassAssign(14)= h14
  Lampz.Callback(14) = "DisableLighting p14, 20,"
  Lampz.MassAssign(15)= l15
  Lampz.MassAssign(15)= h15
  Lampz.Callback(15) = "DisableLighting p15, 20,"
  Lampz.MassAssign(16)= l16
  Lampz.MassAssign(16)= h16
  Lampz.Callback(16) = "DisableLighting p16, 20,"
  Lampz.MassAssign(17)= l17
  Lampz.MassAssign(17)= h17
  Lampz.Callback(17) = "DisableLighting p17, 20,"
  Lampz.MassAssign(18)= l18
  Lampz.MassAssign(18)= h18
  Lampz.Callback(18) = "DisableLighting p18, 2,"

'Sink Ship Lights
  Lampz.MassAssign(21)= l21
  Lampz.MassAssign(21)= h21
  Lampz.MassAssign(22)= l22
  Lampz.MassAssign(22)= h22
  Lampz.MassAssign(23)= l23
  Lampz.MassAssign(23)= h23
  Lampz.MassAssign(24)= l24
  Lampz.MassAssign(24)= h24
  Lampz.MassAssign(25)= l25
  Lampz.MassAssign(25)= h25
  Lampz.MassAssign(26)= l26
  Lampz.MassAssign(26)= h26
  Lampz.MassAssign(27)= l27
  Lampz.MassAssign(27)= h27
  Lampz.MassAssign(28)= l28
  Lampz.MassAssign(28)= h28

  Lampz.MassAssign(31)= l31
  Lampz.MassAssign(31)= h31
  Lampz.Callback(31) = "DisableLighting p31, 0.5,"
  Lampz.MassAssign(32)= l32
  Lampz.MassAssign(32)= h32
  Lampz.Callback(32) = "DisableLighting p32, 0.5,"
  Lampz.MassAssign(33)= l33
  Lampz.MassAssign(33)= h33
  Lampz.Callback(33) = "DisableLighting p33, 0.5,"
  Lampz.MassAssign(34)= l34
  Lampz.MassAssign(34)= h34
  Lampz.Callback(34) = "DisableLighting p34, 20,"
  Lampz.MassAssign(35)= l35
  Lampz.MassAssign(35)= h35
  Lampz.Callback(35) = "DisableLighting p35, 5,"
  Lampz.MassAssign(36)= l36
  Lampz.MassAssign(36)= h36
  Lampz.Callback(36) = "DisableLighting p36, 20,"
  Lampz.MassAssign(37)= l37
  Lampz.MassAssign(37)= h37
  Lampz.Callback(37) = "DisableLighting p37, 8,"
  Lampz.MassAssign(38)= l38
  Lampz.MassAssign(38)= h38
  Lampz.Callback(38) = "DisableLighting p38, 8,"

  Lampz.MassAssign(41)= l41
  Lampz.MassAssign(41)= h41
  Lampz.Callback(41) = "DisableLighting p41, 3,"
  Lampz.MassAssign(42)= l42
  Lampz.MassAssign(42)= h42
  Lampz.Callback(42) = "DisableLighting p42, 3,"
  Lampz.MassAssign(43)= l43
  Lampz.MassAssign(43)= h43
  Lampz.Callback(43) = "DisableLighting p43, 3,"
  Lampz.MassAssign(44)= l44
  Lampz.MassAssign(44)= h44
  Lampz.Callback(44) = "DisableLighting p44, 10,"
  Lampz.MassAssign(45)= l45
  Lampz.MassAssign(45)= h45
  Lampz.Callback(45) = "DisableLighting p45, 10,"
  Lampz.MassAssign(46)= l46
  Lampz.MassAssign(46)= h46
  Lampz.Callback(46) = "DisableLighting p46, 10,"
  Lampz.MassAssign(47)= l47
  Lampz.MassAssign(47)= h47
  Lampz.Callback(47) = "DisableLighting p47, 10,"
  Lampz.MassAssign(48)= l48
  Lampz.MassAssign(48)= h48
  Lampz.Callback(48) = "DisableLighting p48, 2,"

  Lampz.MassAssign(51)= l51
  Lampz.MassAssign(51)= h51
  Lampz.Callback(51) = "DisableLighting p51, 2,"
  Lampz.MassAssign(52)= l52
  Lampz.MassAssign(52)= h52
  Lampz.Callback(52) = "DisableLighting p52, 2,"
  Lampz.MassAssign(53)= l53
  Lampz.MassAssign(53)= h53
  Lampz.Callback(53) = "DisableLighting p53, 2,"
  Lampz.MassAssign(54)= l54
  Lampz.MassAssign(54)= h54
  Lampz.Callback(54) = "DisableLighting p54, 15,"
  Lampz.MassAssign(55)= l55
  Lampz.MassAssign(55)= h55
  Lampz.Callback(55) = "DisableLighting p55, 15,"
  Lampz.MassAssign(56)= l56
  Lampz.MassAssign(56)= h56
  Lampz.Callback(56) = "DisableLighting p56, 10,"
  Lampz.MassAssign(57)= l57
  Lampz.MassAssign(57)= h57
' Lampz.Callback(57) = "DisableLighting p57, 20," 'Treasure Left
  Lampz.MassAssign(58)= l58
  Lampz.MassAssign(58)= h58
  Lampz.Callback(58) = "DisableLighting p58, 10,"

  Lampz.MassAssign(61)= l61
  Lampz.MassAssign(61)= h61
  Lampz.Callback(61) = "DisableLighting p61, 20,"
  Lampz.MassAssign(62)= l62
  Lampz.MassAssign(62)= h62
  Lampz.Callback(62) = "DisableLighting p62, 20,"
  Lampz.MassAssign(63)= l63
  Lampz.MassAssign(63)= h63
  Lampz.Callback(63) = "DisableLighting p63, 20,"
  Lampz.MassAssign(64)= l64
  Lampz.MassAssign(64)= h64
  Lampz.Callback(64) = "DisableLighting p64, 2,"
  Lampz.MassAssign(65)= l65
  Lampz.MassAssign(65)= h65
  Lampz.Callback(65) = "DisableLighting p65, 20,"
  Lampz.MassAssign(66)= l66
  Lampz.MassAssign(66)= h66
' Lampz.Callback(66) = "DisableLighting p66, 20," 'Treasure Right
  Lampz.MassAssign(67)= l67
  Lampz.MassAssign(67)= h67
  Lampz.Callback(67) = "DisableLighting p67, 20,"
  Lampz.MassAssign(68)= l68
  Lampz.MassAssign(68)= h68
  Lampz.Callback(68) = "DisableLighting p68, 4,"

  Lampz.MassAssign(71)= l71
  Lampz.MassAssign(71)= h71
  Lampz.Callback(71) = "DisableLighting p71, 10,"
  Lampz.MassAssign(72)= l72
  Lampz.MassAssign(72)= h72
  Lampz.Callback(72) = "DisableLighting p72, 10,"
  Lampz.MassAssign(73)= l73
  Lampz.MassAssign(73)= h73
  Lampz.Callback(73) = "DisableLighting p73, 10,"
  Lampz.MassAssign(74)= l74
  Lampz.MassAssign(74)= h74
  Lampz.Callback(74) = "DisableLighting p74, 10,"
  Lampz.MassAssign(75)= l75
  Lampz.MassAssign(75)= h75
  Lampz.Callback(75) = "DisableLighting p75, 10,"
  Lampz.MassAssign(76)= l76
  Lampz.MassAssign(76)= h76
  Lampz.Callback(76) = "DisableLighting p76, 10,"
  Lampz.MassAssign(77)= l77
  Lampz.MassAssign(77)= h77
  Lampz.Callback(77) = "DisableLighting p77, 10,"
' Lampz.MassAssign(78)= l78             'Insert Left??
' Lampz.Callback(78) = "DisableLighting p78, 20,"

  Lampz.MassAssign(81)= l81
  Lampz.MassAssign(81)= h81
  Lampz.Callback(81) = "DisableLighting p81, 3,"
  Lampz.MassAssign(82)= l82
  Lampz.MassAssign(82)= h82
  Lampz.Callback(82) = "DisableLighting p82, 1,"
  Lampz.MassAssign(83)= l83
  Lampz.MassAssign(83)= h83
  Lampz.Callback(83) = "DisableLighting p83, 20,"
  Lampz.MassAssign(84)= l84
  Lampz.MassAssign(84)= h84
  Lampz.Callback(84) = "DisableLighting p84, 20,"
  Lampz.MassAssign(85)= l85
  Lampz.MassAssign(85)= h85
  Lampz.Callback(85) = "DisableLighting p85, 7,"
  Lampz.MassAssign(86)= l86             'Jack
  Lampz.MassAssign(86)= h86
  Lampz.Callback(86) = "DisableLighting p86, 2,"
  Lampz.MassAssign(86)= l86a              'Pot
  Lampz.MassAssign(86)= h86a
  Lampz.Callback(86) = "DisableLighting p86a, 2,"
' Lampz.MassAssign(87)= l87             'Insert Right??
' Lampz.Callback(87) = "DisableLighting p87, 20,"
' Lampz.MassAssign(88)= l88             'Credit Button
' Lampz.Callback(88) = "DisableLighting p88, 20,"



'****************************************************************
'           GI assignments
'****************************************************************

  ModLampz.Callback(0) = "GIUpdates"
  ModLampz.Callback(1) = "GIUpdates"
  ModLampz.Callback(2) = "GIUpdates"
  ModLampz.Callback(3) = "GIUpdates"
  ModLampz.Callback(4) = "GIupdates"

  ModLampz.MassAssign(0)= ColToArray(GIstring1)     ' Jets & Back Ramp
  ModLampz.MassAssign(1)= ColToArray(GIstring2)       ' Top Playfield
  ModLampz.MassAssign(2)= ColToArray(GIstring3)       ' Bottom Playfield
  ModLampz.MassAssign(3)= ColToArray(GIstring4)       ' Backglass Gi String 1
  ModLampz.MassAssign(4)= ColToArray(GIstring5)     ' Backglass GI String 2

'Turn off all lamps on startup
  lampz.Init      'This just turns state of any lamps to 1
  ModLampz.Init

'Immediate update to turn on GI, turn off lamps
  lampz.update
  ModLampz.Update

End Sub



'****************
' SolModCallback flasher Code by iaakki @ VPW
' Table and VRBackglass Flashers
'****************

Const SolDrawSpeed = 1.5            ' 1-5. Default 1 = no change.

dim sol17lvl
dim sol18lvl
dim sol19lvl
dim sol20lvl
dim sol21lvl
dim sol22lvl
dim sol23lvl
dim sol24lvl
dim sol25lvl
dim sol26lvl
dim sol27lvl
dim sol28lvl

sub sol17(lvl)
  dim aLvl : aLvl = lvl / 154
  if alvl > 1 then alvl = 1

  if aLvl < sol17lvl Then
    sol17lvl = sol17lvl / SolDrawSpeed
  else
    sol17lvl = aLvl
  end if
  sol17timer_timer
end sub


f17a.intensityscale = 0
f17a.state = 1
f17b.intensityscale = 0
f17b.state = 1

sub sol17timer_timer
  if Not sol17timer.enabled then
    If VRRoom > 0 and VRFlashBackglass = 1 Then
      BGFLSol17_1.visible = true
      BGFLSol17_2.visible = true
      BGFLSol17_3.visible = true
      BGFLSol17_4.visible = true
    End If
    sol17timer.enabled = true
  end if

  sol17lvl = 0.85 * sol17lvl - 0.01
  if sol17lvl < 0 then sol17lvl = 0

  If VRRoom > 0 and VRFlashBackglass = 1 Then
    BGFLSol17_1.opacity = 35 * sol17lvl^3
    BGFLSol17_2.opacity = 35 * sol17lvl^3
    BGFLSol17_3.opacity = 35 * sol17lvl^3
    BGFLSol17_4.opacity = 35 * sol17lvl^3
  End If
  f17a.intensityscale = 1   * sol17lvl
  f17b.intensityscale = 1   * sol17lvl


  if sol17lvl =< 0 Then
    If VRRoom > 0 and VRFlashBackglass = 1 Then
      BGFLSol17_1.visible = True
      BGFLSol17_2.visible = True
      BGFLSol17_3.visible = True
      BGFLSol17_4.visible = True
    End If
    sol17timer.enabled = false
  end if
end sub


sub sol18(lvl)
  dim aLvl : aLvl = lvl / 154
  if alvl > 1 then alvl = 1

  if aLvl < sol18lvl Then
    sol18lvl = sol18lvl / SolDrawSpeed
  else
    sol18lvl = aLvl
  end if

  sol18timer_timer
end sub

f18a.intensityscale = 0
f18a.state = 1
f18b.intensityscale = 0
f18b.state = 1
f18c.intensityscale = 0
f18c.state = 1

sub sol18timer_timer
  if Not sol18timer.enabled then
    If VRRoom > 0 and VRFlashBackglass = 1 Then
      BGFLsol18_1.visible = true
      BGFLsol18_2.visible = true
      BGFLsol18_3.visible = true
      BGFLsol18_4.visible = true
    End If
    sol18timer.enabled = true
  end if
  sol18lvl = 0.85 * sol18lvl - 0.01
  if sol18lvl < 0 then sol18lvl = 0
  If VRRoom > 0 and VRFlashBackglass = 1 Then
    BGFLsol18_1.opacity = 35 * sol18lvl^3
    BGFLsol18_2.opacity = 35 * sol18lvl^3
    BGFLsol18_3.opacity = 35 * sol18lvl^3
    BGFLsol18_4.opacity = 35 * sol18lvl^3
  End If
  f18a.intensityscale = 1   * sol18lvl
  f18b.intensityscale = 1   * sol18lvl
  f18c.intensityscale = 1   * sol18lvl
  if sol18lvl =< 0 Then
    If VRRoom > 0 and VRFlashBackglass = 1 Then
      BGFLsol18_1.visible = True
      BGFLsol18_2.visible = True
      BGFLsol18_3.visible = True
      BGFLsol18_4.visible = True
    End If
    sol18timer.enabled = false
  end if
end sub

sub sol19(lvl)
  dim aLvl : aLvl = lvl / 154
  if alvl > 1 then alvl = 1

  if aLvl < sol19lvl Then
    sol19lvl = sol19lvl / SolDrawSpeed
  else
    sol19lvl = aLvl
  end if

  sol19timer_timer
end sub

f19a.intensityscale = 0
f19a.state = 1
f19b.intensityscale = 0
f19b.state = 1
f19c.intensityscale = 0
f19c.state = 1

sub sol19timer_timer
  if Not sol19timer.enabled then
    If VRRoom > 0 and VRFlashBackglass = 1 Then
      BGFLsol19_1.visible = true
      BGFLsol19_2.visible = true
      BGFLsol19_3.visible = true
      BGFLsol19_4.visible = true
    End If
    sol19timer.enabled = true
  end if
  sol19lvl = 0.85 * sol19lvl - 0.01
  if sol19lvl < 0 then sol19lvl = 0
  If VRRoom > 0 and VRFlashBackglass = 1 Then
    BGFLsol19_1.opacity = 35 * sol19lvl^3
    BGFLsol19_2.opacity = 35 * sol19lvl^3
    BGFLsol19_3.opacity = 35 * sol19lvl^3
    BGFLsol19_4.opacity = 35 * sol19lvl^3
  End If
  f19a.intensityscale = 1   * sol19lvl
  f19b.intensityscale = 1   * sol19lvl
  f19c.intensityscale = 1   * sol19lvl
  if sol19lvl =< 0 Then
    If VRRoom > 0 and VRFlashBackglass = 1 Then
      BGFLsol19_1.visible = True
      BGFLsol19_2.visible = True
      BGFLsol19_3.visible = True
      BGFLsol19_4.visible = True
    End If
    sol19timer.enabled = false
  end if
end sub

sub sol20(lvl)
  dim aLvl : aLvl = lvl / 154
  if alvl > 1 then alvl = 1

  if aLvl < sol20lvl Then
    sol20lvl = sol20lvl / SolDrawSpeed
  else
    sol20lvl = aLvl
  end if

  sol20timer_timer
end sub

f20a.intensityscale = 0
f20a.state = 1
f20b.intensityscale = 0
f20b.state = 1
f20c.intensityscale = 0
f20c.state = 1

sub sol20timer_timer
  if Not sol20timer.enabled then
    If VRRoom > 0 and VRFlashBackglass = 1 Then
      BGFLsol20_1.visible = true
      BGFLsol20_2.visible = true
      BGFLsol20_3.visible = true
      BGFLsol20_4.visible = true
    End If
    sol20timer.enabled = true
  end if
  sol20lvl = 0.85 * sol20lvl - 0.01
  if sol20lvl < 0 then sol20lvl = 0
  If VRRoom > 0 and VRFlashBackglass = 1 Then
    BGFLsol20_1.opacity = 35 * sol20lvl^3
    BGFLsol20_2.opacity = 35 * sol20lvl^3
    BGFLsol20_3.opacity = 35 * sol20lvl^3
    BGFLsol20_4.opacity = 35 * sol20lvl^3
  End If
  f20a.intensityscale = 1   * sol20lvl
  f20b.intensityscale = 1   * sol20lvl
  f20c.intensityscale = 1   * sol20lvl
  if sol20lvl =< 0 Then
    If VRRoom > 0 and VRFlashBackglass = 1 Then
      BGFLsol20_1.visible = True
      BGFLsol20_2.visible = True
      BGFLsol20_3.visible = True
      BGFLsol20_4.visible = True
    End If
    sol20timer.enabled = false
  end if
end sub

sub sol21(lvl)
  dim aLvl : aLvl = lvl / 154
  if alvl > 1 then alvl = 1

  if aLvl < sol21lvl Then
    sol21lvl = sol21lvl / SolDrawSpeed
  else
    sol21lvl = aLvl
  end if

  sol21timer_timer
end sub

f21a.intensityscale = 0
f21a.state = 1
f21b.intensityscale = 0
f21b.state = 1
f21c.intensityscale = 0
f21c.state = 1

sub sol21timer_timer
  if Not sol21timer.enabled then
    If VRRoom > 0 and VRFlashBackglass = 1 Then
      BGFLsol21_1.visible = true
      BGFLsol21_2.visible = true
      BGFLsol21_3.visible = true
      BGFLsol21_4.visible = true
    End If
    sol21timer.enabled = true
  end if
  sol21lvl = 0.85 * sol21lvl - 0.01
  if sol21lvl < 0 then sol21lvl = 0
  If VRRoom > 0 and VRFlashBackglass = 1 Then
    BGFLsol21_1.opacity = 35 * sol21lvl^3
    BGFLsol21_2.opacity = 35 * sol21lvl^3
    BGFLsol21_3.opacity = 35 * sol21lvl^3
    BGFLsol21_4.opacity = 35 * sol21lvl^3
  End If
  f21a.intensityscale = 1   * sol21lvl
  f21b.intensityscale = 1   * sol21lvl
  f21c.intensityscale = 1   * sol21lvl
  if sol21lvl =< 0 Then
    If VRRoom > 0 and VRFlashBackglass = 1 Then
      BGFLsol21_1.visible = True
      BGFLsol21_2.visible = True
      BGFLsol21_3.visible = True
      BGFLsol21_4.visible = True
    End If
    sol21timer.enabled = false
  end if
end sub

sub sol22(lvl)
  dim aLvl : aLvl = lvl / 154
  if alvl > 1 then alvl = 1

  if aLvl < sol22lvl Then
    sol22lvl = sol22lvl / SolDrawSpeed
  else
    sol22lvl = aLvl
  end if

  sol22timer_timer
end sub

f22a.intensityscale = 0
f22a.state = 1
f22b.intensityscale = 0
f22b.state = 1
f22c.intensityscale = 0
f22c.state = 1
f22e.intensityscale = 0
f22e.state = 1

sub sol22timer_timer
  if Not sol22timer.enabled then
    If VRRoom > 0 and VRFlashBackglass = 1 Then
      BGFLsol22_1.visible = true
      BGFLsol22_2.visible = true
      BGFLsol22_3.visible = true
      BGFLsol22_4.visible = true
    End If
    sol22timer.enabled = true
  end if
  sol22lvl = 0.85 * sol22lvl - 0.01
  if sol22lvl < 0 then sol22lvl = 0
  If VRRoom > 0 and VRFlashBackglass = 1 Then
    BGFLsol22_1.opacity = 35 * sol22lvl^3
    BGFLsol22_2.opacity = 35 * sol22lvl^3
    BGFLsol22_3.opacity = 35 * sol22lvl^3
    BGFLsol22_4.opacity = 35 * sol22lvl^3
  End If
  f22a.intensityscale = 1   * sol22lvl
  f22b.intensityscale = 1   * sol22lvl
  f22c.intensityscale = 1   * sol22lvl
  f22e.intensityscale = 1   * sol22lvl
  if sol22lvl =< 0 Then
    If VRRoom > 0 and VRFlashBackglass = 1 Then
      BGFLsol22_1.visible = True
      BGFLsol22_2.visible = True
      BGFLsol22_3.visible = True
      BGFLsol22_4.visible = True
    End If
    sol22timer.enabled = false
  end if
end sub

sub sol23(lvl)
  dim aLvl : aLvl = lvl / 154
  if alvl > 1 then alvl = 1

  if aLvl < sol23lvl Then
    sol23lvl = sol23lvl / SolDrawSpeed
  else
    sol23lvl = aLvl
  end if

  sol23timer_timer
end sub

f23.intensityscale = 0
f23.state = 1
fh23.intensityscale = 0
fh23.state = 1

sub sol23timer_timer
  if Not sol23timer.enabled then
    If VRRoom > 0 and VRFlashBackglass = 1 Then
      BGFLsol23_1.visible = true
      BGFLsol23_2.visible = true
      BGFLsol23_3.visible = true
      BGFLsol23_4.visible = true
    End If
    sol23timer.enabled = true
  end if
  sol23lvl = 0.85 * sol23lvl - 0.01
  if sol23lvl < 0 then sol23lvl = 0
  If VRRoom > 0 and VRFlashBackglass = 1 Then
    BGFLsol23_1.opacity = 35 * sol23lvl^3
    BGFLsol23_2.opacity = 35 * sol23lvl^3
    BGFLsol23_3.opacity = 35 * sol23lvl^3
    BGFLsol23_4.opacity = 35 * sol23lvl^3
  End If
  f23.intensityscale = 1   * sol23lvl
  fh23.intensityscale = 1   * sol23lvl
  pf23.blenddisablelighting = sol23lvl^2 * 50
  if sol23lvl =< 0 Then
    If VRRoom > 0 and VRFlashBackglass = 1 Then
      BGFLsol23_1.visible = True
      BGFLsol23_2.visible = True
      BGFLsol23_3.visible = True
      BGFLsol23_4.visible = True
    End If
    sol23timer.enabled = false
  end if
end sub

sub sol24(lvl)
  dim aLvl : aLvl = lvl / 154
  if alvl > 1 then alvl = 1

  if aLvl < sol24lvl Then
    sol24lvl = sol24lvl / SolDrawSpeed
  else
    sol24lvl = aLvl
  end if

  sol24timer_timer
end sub

f24.intensityscale = 0
f24.state = 1
fh24.intensityscale = 0
fh24.state = 1

sub sol24timer_timer
  if Not sol24timer.enabled then
    If VRRoom > 0 and VRFlashBackglass = 1 Then
      BGFLSol24_1.visible = true
      BGFLSol24_2.visible = true
      BGFLSol24_3.visible = true
      BGFLSol24_4.visible = true
    End If
    sol24timer.enabled = true
  end if
  sol24lvl = 0.85 * sol24lvl - 0.01
  if sol24lvl < 0 then sol24lvl = 0
  If VRRoom > 0 and VRFlashBackglass = 1 Then
    BGFLSol24_1.opacity = 35 * sol24lvl^3
    BGFLSol24_2.opacity = 35 * sol24lvl^3
    BGFLSol24_3.opacity = 35 * sol24lvl^3
    BGFLSol24_4.opacity = 35 * sol24lvl^3
  End If
  f24.intensityscale = 1   * sol24lvl
  fh24.intensityscale = 1   * sol24lvl
  pf24.blenddisablelighting = sol24lvl^2 * 10
  if sol24lvl =< 0 Then
    If VRRoom > 0 and VRFlashBackglass = 1 Then
      BGFLSol24_1.visible = True
      BGFLSol24_2.visible = True
      BGFLSol24_3.visible = True
      BGFLSol24_4.visible = True
    End If
    sol24timer.enabled = false
  end if
end sub

sub sol25(lvl)
  dim aLvl : aLvl = lvl / 154
  if alvl > 1 then alvl = 1

  if aLvl < sol25lvl Then
    sol25lvl = sol25lvl / SolDrawSpeed
  else
    sol25lvl = aLvl
  end if

  sol25timer_timer
end sub

f25a.intensityscale = 0
f25a.state = 1
f25b.intensityscale = 0
f25b.state = 1
f25c.intensityscale = 0
f25c.state = 1
f25d.intensityscale = 0
f25d.state = 1
f25e.intensityscale = 0
f25e.state = 1
f25f.intensityscale = 0
f25f.state = 1

sub sol25timer_timer
  if Not sol25timer.enabled then
    sol25timer.enabled = true
  end if
  sol25lvl = 0.85 * sol25lvl - 0.01
  if sol25lvl < 0 then sol25lvl = 0
  f25a.intensityscale = 1   * sol25lvl
  f25b.intensityscale = 1   * sol25lvl
  f25c.intensityscale = 1   * sol25lvl
  f25d.intensityscale = 1   * sol25lvl
  f25e.intensityscale = 1   * sol25lvl
  f25f.intensityscale = 1   * sol25lvl
  if sol25lvl =< 0 Then
    sol25timer.enabled = false
  end if
end sub

sub sol26(lvl)
  dim aLvl : aLvl = lvl / 154
  if alvl > 1 then alvl = 1

  if aLvl < sol26lvl Then
    sol26lvl = sol26lvl / SolDrawSpeed
  else
    sol26lvl = aLvl
  end if

  sol26timer_timer
end sub

f26a.intensityscale = 0
f26a.state = 1
f26b.intensityscale = 0
f26b.state = 1
f26c.intensityscale = 0
f26c.state = 1
f26d.intensityscale = 0
f26d.state = 1
f26e.intensityscale = 0
f26e.state = 1

sub sol26timer_timer
  if Not sol26timer.enabled then
    sol26timer.enabled = true
  end if
  sol26lvl = 0.85 * sol26lvl - 0.01
  if sol26lvl < 0 then sol26lvl = 0
  f26a.intensityscale = 1   * sol26lvl
  f26b.intensityscale = 1   * sol26lvl
  f26c.intensityscale = 1   * sol26lvl
  f26d.intensityscale = 1   * sol26lvl
  f26e.intensityscale = 1   * sol26lvl
  if sol26lvl =< 0 Then
    sol26timer.enabled = false
  end if
end sub

sub sol27(lvl)
  dim aLvl : aLvl = lvl / 154
  if alvl > 1 then alvl = 1

  if aLvl < sol27lvl Then
    sol27lvl = sol27lvl / SolDrawSpeed
  else
    sol27lvl = aLvl
  end if

  sol27timer_timer
end sub

f27a.intensityscale = 0
f27a.state = 1
f27b.intensityscale = 0
f27b.state = 1
f27c.intensityscale = 0
f27c.state = 1

sub sol27timer_timer
  if Not sol27timer.enabled then
    If VRRoom > 0 and VRFlashBackglass = 1 Then
      BGFLsol27_1.visible = true
      BGFLsol27_2.visible = true
      BGFLsol27_3.visible = true
    End If
    sol27timer.enabled = true
  end if
  sol27lvl = 0.85 * sol27lvl - 0.01
  if sol27lvl < 0 then sol27lvl = 0
    If VRRoom > 0 and VRFlashBackglass = 1 Then
      If gi3lvl = 0 Then
        BGFLsol27_1.opacity = 90 * sol27lvl^3
        BGFLsol27_2.opacity = 90 * sol27lvl^3
        BGFLsol27_3.opacity = 90 * sol27lvl^3
      Else
        BGFLsol27_1.opacity = 30 * sol27lvl^3
        BGFLsol27_2.opacity = 30 * sol27lvl^3
        BGFLsol27_3.opacity = 30 * sol27lvl^3
      End If
    End If
  f27a.intensityscale = 1   * sol27lvl
  f27b.intensityscale = 1   * sol27lvl
  f27c.intensityscale = 1   * sol27lvl
  if sol27lvl =< 0 Then
    If VRRoom > 0 and VRFlashBackglass = 1 Then
      BGFLsol27_1.visible = True
      BGFLsol27_2.visible = True
      BGFLsol27_3.visible = True
    End If
    sol27timer.enabled = false
  end if
end sub

sub sol28(lvl)
  dim aLvl : aLvl = lvl / 154
  if alvl > 1 then alvl = 1
' debug.print lvl

  if aLvl < sol28lvl Then
    sol28lvl = sol28lvl / SolDrawSpeed
  else
    sol28lvl = aLvl
  end if

  sol28timer_timer
end sub

f28.intensityscale = 0
f28.state = 1
fh28.intensityscale = 0
fh28.state = 1

sub sol28timer_timer
  if Not sol28timer.enabled then
    If VRRoom > 0 and VRFlashBackglass = 1 Then
      BGFLsol28_1.visible = true
      BGFLsol28_2.visible = true
      BGFLsol28_3.visible = true
      BGFLsol28_4.visible = true
    End If
    sol28timer.enabled = true
  end if
  sol28lvl = 0.85 * sol28lvl - 0.01
  if sol28lvl < 0 then sol28lvl = 0
  If VRRoom > 0 and VRFlashBackglass = 1 Then
    BGFLsol28_1.opacity = 35 * sol28lvl^3
    BGFLsol28_2.opacity = 35 * sol28lvl^3
    BGFLsol28_3.opacity = 35 * sol28lvl^3
    BGFLsol28_4.opacity = 35 * sol28lvl^3
  End If
  f28.intensityscale = 1   * sol28lvl
  fh28.intensityscale = 1   * sol28lvl
  pf28.blenddisablelighting = sol28lvl^2 * 10
  if sol28lvl =< 0 Then
    If VRRoom > 0 and VRFlashBackglass = 1 Then
      BGFLsol28_1.visible = True
      BGFLsol28_2.visible = True
      BGFLsol28_3.visible = True
      BGFLsol28_4.visible = True
    End If
    sol28timer.enabled = false
  end if
end sub

dim ballbrightness
const ballbrightMax = 255
const ballbrightMin = 115

Set GICallback2 = GetRef("SetGI")


Sub SetGI(aNr, aValue)
  'debug.print "GI nro: " & aNr & " and step: " & aValue
  ModLampz.SetGI aNr, aValue 'Redundant. Could reassign GI indexes here
End Sub

dim gi2lvl,gi3lvl, gi2prevlvl
Sub GIupdates(ByVal aLvl) 'GI update odds and ends go here
  if Lampz.UseFunction then aLvl = LampFilter(aLvl) 'Callbacks don't get this filter automatically
  dim BOT
  BOT = GetBalls

  dim  gi0lvl, gi1lvl, gi4lvl, ii
  if Lampz.UseFunction then   'Callbacks don't get this filter automatically
    gi0lvl = LampFilter(ModLampz.Lvl(0))
    gi1lvl = LampFilter(ModLampz.Lvl(1))
    gi2lvl = LampFilter(ModLampz.Lvl(2))
    gi3lvl = LampFilter(ModLampz.Lvl(3))
    gi4lvl = LampFilter(ModLampz.Lvl(4))
  Else
    gi0lvl = ModLampz.Lvl(0)
    gi1lvl = ModLampz.Lvl(1)
    gi2lvl = ModLampz.Lvl(2)
    gi3lvl = ModLampz.Lvl(3)
    gi4lvl = ModLampz.Lvl(4)
  end if

  FlBumperFadeTarget(1) = gi0lvl
  FlBumperFadeTarget(2) = gi0lvl
  Pincab_Backglass.blenddisablelighting = 4 * gi3lvl

  if gi2lvl = 0 then                    'GI OFF, let's hide ON prims
    'for each kk in GI:kk.state = 0:Next
    if ballbrightness <> -1 then ballbrightness = ballbrightMin
  Elseif gi2lvl = 1 then                  'GI ON, let's hide OFF prims
    'for each kk in GI:kk.state = 1:Next
    if ballbrightness <> -1 then ballbrightness = ballbrightMax
  Else
    if gi2prevlvl = 0 Then                'GI has just changed from OFF to fading, let's show ON
      'fx_relay_on
      ballbrightness = ballbrightMin + 1
    elseif gi2prevlvl = 1 Then              'GI has just changed from ON to fading, let's show OFF
      'fx_relay_off
      ballbrightness = ballbrightMax - 1
    Else
      'no change
    end if
  end if

  For each ii in GI_Red:ii.IntensityScale = (1-gi2lvl) * gi1lvl:Next

  gi2prevlvl = gi2lvl

end sub


'Sub UpdateGi(nr,step)
' Dim ii
' Select Case nr
' Case 0    'Jets & Back Ramp
'   'string 1
'   If step=0 Then
'     For each ii in GIString1:ii.state=0:Next
'   Else
'     For each ii in GIString1:ii.state=1:Next
'   End If
'   For each ii in GIString1:ii.IntensityScale = 0.125 * step:Next
' Case 1    'Red GI Lights
'   'string 2
'   If step=0 Then
'     For each ii in GIString2:ii.state=0:Next
'   Else
'     For each ii in GIString2:ii.state=1:Next
'   End If
'   For each ii in GIString2:ii.IntensityScale = 0.125 * step:Next
' Case 2    'White GI Lights
'   'string 3
'   If step=0 Then
'     For each ii in GIString3:ii.state=0:Next
'     ballbrightness = ballbrightMin
'   Else
'     For each ii in GIString3:ii.state=1:Next
'     ballbrightness = ballbrightMax
'   End If
'   For each ii in GIString3:ii.IntensityScale = 0.125 * step:Next
' End Select
'   If Light4.state = 0 and Light5.state = 1 Then
'     For each ii in GIString2:ii.IntensityScale = 0.125 * step:Next
'     For each ii in GI_Red:ii.IntensityScale = 0.125 * step:Next
'     For each ii in GI_Red:ii.state = 1:Next
'   Else
'     For each ii in GIString2:ii.IntensityScale = 0.125 * step:Next
'     For each ii in GI_Red:ii.IntensityScale = 0.125 * step:Next
'     For each ii in GI_Red:ii.state = 0:Next
'   End If
'End Sub


'Lamp Filter
Function LampFilter(aLvl)
  LampFilter = aLvl^1.6 'exponential curve?
End Function

Dim GIoffMult : GIoffMult = 2 'adjust how bright the inserts get when the GI is off
Dim GIoffMultFlashers : GIoffMultFlashers = 2 'adjust how bright the Flashers get when the GI is off

'Helper functions

Function ColtoArray(aDict)  'converts a collection to an indexed array. Indexes will come out random probably.
  redim a(999)
  dim count : count = 0
  dim x  : for each x in aDict : set a(Count) = x : count = count + 1 : Next
  redim preserve a(count-1) : ColtoArray = a
End Function



Sub SetLamp(aNr, aOn)
  Lampz.state(aNr) = abs(aOn)
End Sub

Sub SetModLamp(aNr, aInput)
  ModLampz.state(aNr) = abs(aInput)/255
End Sub

'========================================================================================================================


'====================
'Class jungle nf
'====================

'No-op object instead of adding more conditionals to the main loop
'It also prevents errors if empty lamp numbers are called, and it's only one object
'should be g2g?

Class NullFadingObject : Public Property Let IntensityScale(input) : : End Property : End Class

'version 0.11 - Mass Assign, Changed modulate style
'version 0.12 - Update2 (single -1 timer update) update method for core.vbs
'Version 0.12a - Filter can now be accessed via 'FilterOut'
'Version 0.12b - Changed MassAssign from a sub to an indexed property (new syntax: lampfader.MassAssign(15) = Light1 )
'Version 0.13 - No longer requires setlocale. Callback() can be assigned multiple times per index
' Note: if using multiple 'LampFader' objects, set the 'name' variable to avoid conflicts with callbacks

Class LampFader
  Public FadeSpeedDown(140), FadeSpeedUp(140)
  Private Lock(140), Loaded(140), OnOff(140)
  Public UseFunction
  Private cFilter
  Public UseCallback(140), cCallback(140)
  Public Lvl(140), Obj(140)
  Private Mult(140)
  Public FrameTime
  Private InitFrame
  Public Name

  Sub Class_Initialize()
    InitFrame = 0
    dim x : for x = 0 to uBound(OnOff)  'Set up fade speeds
      FadeSpeedDown(x) = 1/100  'fade speed down
      FadeSpeedUp(x) = 1/80   'Fade speed up
      UseFunction = False
      lvl(x) = 0
      OnOff(x) = False
      Lock(x) = True : Loaded(x) = False
      Mult(x) = 1
    Next
    Name = "LampFaderNF" 'NEEDS TO BE CHANGED IF THERE'S MULTIPLE OF THESE OBJECTS, OTHERWISE CALLBACKS WILL INTERFERE WITH EACH OTHER!!
    for x = 0 to uBound(OnOff)    'clear out empty obj
      if IsEmpty(obj(x) ) then Set Obj(x) = NullFader' : Loaded(x) = True
    Next
  End Sub

  Public Property Get Locked(idx) : Locked = Lock(idx) : End Property   ''debug.print Lampz.Locked(100) 'debug
  Public Property Get state(idx) : state = OnOff(idx) : end Property
  Public Property Let Filter(String) : Set cFilter = GetRef(String) : UseFunction = True : End Property
  Public Function FilterOut(aInput) : if UseFunction Then FilterOut = cFilter(aInput) Else FilterOut = aInput End If : End Function
  'Public Property Let Callback(idx, String) : cCallback(idx) = String : UseCallBack(idx) = True : End Property
  Public Property Let Callback(idx, String)
    UseCallBack(idx) = True
    'cCallback(idx) = String 'old execute method
    'New method: build wrapper subs using ExecuteGlobal, then call them
    cCallback(idx) = cCallback(idx) & "___" & String  'multiple strings dilineated by 3x _

    dim tmp : tmp = Split(cCallback(idx), "___")

    dim str, x : for x = 0 to uBound(tmp) 'build proc contents
      'If Not tmp(x)="" then str = str & "  " & tmp(x) & " aLVL" & "  '" & x & vbnewline  'more verbose
      If Not tmp(x)="" then str = str & tmp(x) & " aLVL:"
    Next
    'msgbox "Sub " & name & idx & "(aLvl):" & str & "End Sub"
    dim out : out = "Sub " & name & idx & "(aLvl):" & str & "End Sub"
    'if idx = 132 then msgbox out 'debug
    ExecuteGlobal Out

  End Property

  Public Property Let state(ByVal idx, input) 'Major update path
    if Input <> OnOff(idx) then  'discard redundant updates
      OnOff(idx) = input
      Lock(idx) = False
      Loaded(idx) = False
    End If
  End Property

  'Mass assign, Builds arrays where necessary
  'Sub MassAssign(aIdx, aInput)
  Public Property Let MassAssign(aIdx, aInput)
    If typename(obj(aIdx)) = "NullFadingObject" Then 'if empty, use Set
      if IsArray(aInput) then
        obj(aIdx) = aInput
      Else
        Set obj(aIdx) = aInput
      end if
    Else
      Obj(aIdx) = AppendArray(obj(aIdx), aInput)
    end if
  end Property

  Sub SetLamp(aIdx, aOn) : state(aIdx) = aOn : End Sub  'Solenoid Handler

  Public Sub TurnOnStates() 'If obj contains any light objects, set their states to 1 (Fading is our job!)
    dim debugstr
    dim idx : for idx = 0 to uBound(obj)
      if IsArray(obj(idx)) then
        'debugstr = debugstr & "array found at " & idx & "..."
        dim x, tmp : tmp = obj(idx) 'set tmp to array in order to access it
        for x = 0 to uBound(tmp)
          if typename(tmp(x)) = "Light" then DisableState tmp(x)' : debugstr = debugstr & tmp(x).name & " state'd" & vbnewline
          tmp(x).intensityscale = 0.001 ' this can prevent init stuttering
        Next
      Else
        if typename(obj(idx)) = "Light" then DisableState obj(idx)' : debugstr = debugstr & obj(idx).name & " state'd (not array)" & vbnewline
        obj(idx).intensityscale = 0.001 ' this can prevent init stuttering
      end if
    Next
    ''debug.print debugstr
  End Sub
  Private Sub DisableState(ByRef aObj) : aObj.FadeSpeedUp = 1000 : aObj.State = 1 : End Sub 'turn state to 1

  Public Sub Init() 'Just runs TurnOnStates right now
    TurnOnStates
  End Sub

  Public Property Let Modulate(aIdx, aCoef) : Mult(aIdx) = aCoef : Lock(aIdx) = False : Loaded(aIdx) = False: End Property
  Public Property Get Modulate(aIdx) : Modulate = Mult(aIdx) : End Property

  Public Sub Update1()   'Handle all boolean numeric fading. If done fading, Lock(x) = True. Update on a '1' interval Timer!
    dim x : for x = 0 to uBound(OnOff)
      if not Lock(x) then 'and not Loaded(x) then
        if OnOff(x) then 'Fade Up
          Lvl(x) = Lvl(x) + FadeSpeedUp(x)
          if Lvl(x) >= 1 then Lvl(x) = 1 : Lock(x) = True
        elseif Not OnOff(x) then 'fade down
          Lvl(x) = Lvl(x) - FadeSpeedDown(x)
          if Lvl(x) <= 0 then Lvl(x) = 0 : Lock(x) = True
        end if
      end if
    Next
  End Sub

  Public Sub Update2()   'Both updates on -1 timer (Lowest latency, but less accurate fading at 60fps vsync)
    FrameTime = gametime - InitFrame : InitFrame = GameTime 'Calculate frametime
    dim x : for x = 0 to uBound(OnOff)
      if not Lock(x) then 'and not Loaded(x) then
        if OnOff(x) then 'Fade Up
          Lvl(x) = Lvl(x) + FadeSpeedUp(x) * FrameTime
          if Lvl(x) >= 1 then Lvl(x) = 1 : Lock(x) = True
        elseif Not OnOff(x) then 'fade down
          Lvl(x) = Lvl(x) - FadeSpeedDown(x) * FrameTime
          if Lvl(x) <= 0 then Lvl(x) = 0 : Lock(x) = True
        end if
      end if
    Next
    Update
  End Sub

  Public Sub Update() 'Handle object updates. Update on a -1 Timer! If done fading, loaded(x) = True
    dim x,xx : for x = 0 to uBound(OnOff)
      if not Loaded(x) then
        if IsArray(obj(x) ) Then  'if array
          If UseFunction then
            for each xx in obj(x) : xx.IntensityScale = cFilter(Lvl(x)*Mult(x)) : Next
          Else
            for each xx in obj(x) : xx.IntensityScale = Lvl(x)*Mult(x) : Next
          End If
        else            'if single lamp or flasher
          If UseFunction then
            obj(x).Intensityscale = cFilter(Lvl(x)*Mult(x))
          Else
            obj(x).Intensityscale = Lvl(x)
          End If
        end if
        if TypeName(lvl(x)) <> "Double" and typename(lvl(x)) <> "Integer" then msgbox "uhh " & 2 & " = " & lvl(x)
        'If UseCallBack(x) then execute cCallback(x) & " " & (Lvl(x)) 'Callback
        If UseCallBack(x) then Proc name & x,Lvl(x)*mult(x) 'Proc
        If Lock(x) Then
          if Lvl(x) = 1 or Lvl(x) = 0 then Loaded(x) = True 'finished fading
        end if
      end if
    Next
  End Sub
End Class

'version 0.11 - Mass Assign, Changed modulate style
'version 0.12 - Update2 (single -1 timer update) update method for core.vbs
'Version 0.12a - Filter can now be publicly accessed via 'FilterOut'
'Version 0.12b - Changed MassAssign from a sub to an indexed property (new syntax: lampfader.MassAssign(15) = Light1 )
'Version 0.13 - No longer requires setlocale. Callback() can be assigned multiple times per index
'Version 0.13a - fixed DynamicLamps hopefully
' Note: if using multiple 'DynamicLamps' objects, change the 'name' variable to avoid conflicts with callbacks

Class DynamicLamps 'Lamps that fade up and down. GI and Flasher handling
  Public Loaded(50), FadeSpeedDown(50), FadeSpeedUp(50)
  Private Lock(50), SolModValue(50)
  Private UseCallback(50), cCallback(50)
  Public Lvl(50)
  Public Obj(50)
  Private UseFunction, cFilter
  private Mult(50)
  Public Name

  Public FrameTime
  Private InitFrame

  Private Sub Class_Initialize()
    InitFrame = 0
    dim x : for x = 0 to uBound(Obj)
      FadeSpeedup(x) = 0.01
      FadeSpeedDown(x) = 0.01
      lvl(x) = 0.0001 : SolModValue(x) = 0
      Lock(x) = True : Loaded(x) = False
      mult(x) = 1
      Name = "DynamicFaderNF" 'NEEDS TO BE CHANGED IF THERE'S MULTIPLE OBJECTS, OTHERWISE CALLBACKS WILL INTERFERE WITH EACH OTHER!!
      if IsEmpty(obj(x) ) then Set Obj(x) = NullFader' : Loaded(x) = True
    next
  End Sub

  Public Property Get Locked(idx) : Locked = Lock(idx) : End Property
  'Public Property Let Callback(idx, String) : cCallback(idx) = String : UseCallBack(idx) = True : End Property
  Public Property Let Filter(String) : Set cFilter = GetRef(String) : UseFunction = True : End Property
  Public Function FilterOut(aInput) : if UseFunction Then FilterOut = cFilter(aInput) Else FilterOut = aInput End If : End Function

  Public Property Let Callback(idx, String)
    UseCallBack(idx) = True
    'cCallback(idx) = String 'old execute method
    'New method: build wrapper subs using ExecuteGlobal, then call them
    cCallback(idx) = cCallback(idx) & "___" & String  'multiple strings dilineated by 3x _

    dim tmp : tmp = Split(cCallback(idx), "___")

    dim str, x : for x = 0 to uBound(tmp) 'build proc contents
      'debugstr = debugstr & x & "=" & tmp(x) & vbnewline
      'If Not tmp(x)="" then str = str & "  " & tmp(x) & " aLVL" & "  '" & x & vbnewline  'more verbose
      If Not tmp(x)="" then str = str & tmp(x) & " aLVL:"
    Next

    dim out : out = "Sub " & name & idx & "(aLvl):" & str & "End Sub"
    'if idx = 132 then msgbox out 'debug
    ExecuteGlobal Out
  End Property


  Public Property Let State(idx,Value)
    'If Value = SolModValue(idx) Then Exit Property ' Discard redundant updates
    If Value <> SolModValue(idx) Then ' Discard redundant updates
      SolModValue(idx) = Value
      Lock(idx) = False : Loaded(idx) = False
    End If
  End Property

  Public Property Get state(idx) : state = SolModValue(idx) : end Property

  'Mass assign, Builds arrays where necessary
  'Sub MassAssign(aIdx, aInput)
  Public Property Let MassAssign(aIdx, aInput)
    If typename(obj(aIdx)) = "NullFadingObject" Then 'if empty, use Set
      if IsArray(aInput) then
        obj(aIdx) = aInput
      Else
        Set obj(aIdx) = aInput
      end if
    Else
      Obj(aIdx) = AppendArray(obj(aIdx), aInput)
    end if
  end Property

  'solcallback (solmodcallback) handler
  Sub SetLamp(aIdx, aInput) : state(aIdx) = aInput : End Sub  '0->1 Input
  Sub SetModLamp(aIdx, aInput) : state(aIdx) = aInput/255 : End Sub '0->255 Input
  Sub SetGI(aIdx, ByVal aInput) : if aInput = 8 then aInput = 7 end if : state(aIdx) = aInput/7 : End Sub '0->8 WPC GI input

  Public Sub TurnOnStates() 'If obj contains any light objects, set their states to 1 (Fading is our job!)
    dim debugstr
    dim idx : for idx = 0 to uBound(obj)
      if IsArray(obj(idx)) then
        'debugstr = debugstr & "array found at " & idx & "..."
        dim x, tmp : tmp = obj(idx) 'set tmp to array in order to access it
        for x = 0 to uBound(tmp)
          if typename(tmp(x)) = "Light" then DisableState tmp(x) ': debugstr = debugstr & tmp(x).name & " state'd" & vbnewline

        Next
      Else
        if typename(obj(idx)) = "Light" then DisableState obj(idx) ': debugstr = debugstr & obj(idx).name & " state'd (not array)" & vbnewline

      end if
    Next
    'debug.print debugstr
  End Sub

  Private Sub DisableState(ByRef aObj) : aObj.FadeSpeedUp = 1000 : aObj.State = 1 : End Sub 'turn state to 1

  Public Sub Init() 'just call turnonstates for now
    TurnOnStates
  End Sub

  Public Property Let Modulate(aIdx, aCoef) : Mult(aIdx) = aCoef : Lock(aIdx) = False : Loaded(aIdx) = False: End Property
  Public Property Get Modulate(aIdx) : Modulate = Mult(aIdx) : End Property

  Public Sub Update1()   'Handle all numeric fading. If done fading, Lock(x) = True
    'dim stringer
    dim x : for x = 0 to uBound(Lvl)
      'stringer = "Locked @ " & SolModValue(x)
      if not Lock(x) then 'and not Loaded(x) then
        If lvl(x) < SolModValue(x) then '+
          'stringer = "Fading Up " & lvl(x) & " + " & FadeSpeedUp(x)
          Lvl(x) = Lvl(x) + FadeSpeedUp(x)
          if Lvl(x) >= SolModValue(x) then Lvl(x) = SolModValue(x) : Lock(x) = True
        ElseIf Lvl(x) > SolModValue(x) Then '-
          Lvl(x) = Lvl(x) - FadeSpeedDown(x)
          'stringer = "Fading Down " & lvl(x) & " - " & FadeSpeedDown(x)
          if Lvl(x) <= SolModValue(x) then Lvl(x) = SolModValue(x) : Lock(x) = True
        End If
      end if
    Next
    'tbF.text = stringer
  End Sub

  Public Sub Update2()   'Both updates on -1 timer (Lowest latency, but less accurate fading at 60fps vsync)
    FrameTime = gametime - InitFrame : InitFrame = GameTime 'Calculate frametime
    dim x : for x = 0 to uBound(Lvl)
      if not Lock(x) then 'and not Loaded(x) then
        If lvl(x) < SolModValue(x) then '+
          Lvl(x) = Lvl(x) + FadeSpeedUp(x) * FrameTime
          if Lvl(x) >= SolModValue(x) then Lvl(x) = SolModValue(x) : Lock(x) = True
        ElseIf Lvl(x) > SolModValue(x) Then '-
          Lvl(x) = Lvl(x) - FadeSpeedDown(x) * FrameTime
          if Lvl(x) <= SolModValue(x) then Lvl(x) = SolModValue(x) : Lock(x) = True
        End If
      end if
    Next
    Update
  End Sub

  Public Sub Update() 'Handle object updates. Update on a -1 Timer! If done fading, loaded(x) = True
    dim x,xx
    for x = 0 to uBound(Lvl)
      if not Loaded(x) then
        if IsArray(obj(x) ) Then  'if array
          If UseFunction then
            for each xx in obj(x) : xx.IntensityScale = cFilter(abs(Lvl(x))*mult(x)) : Next
          Else
            for each xx in obj(x) : xx.IntensityScale = Lvl(x)*mult(x) : Next
          End If
        else            'if single lamp or flasher
          If UseFunction then
            obj(x).Intensityscale = cFilter(abs(Lvl(x))*mult(x))
          Else
            obj(x).Intensityscale = Lvl(x)*mult(x)
          End If
        end if
        'If UseCallBack(x) then execute cCallback(x) & " " & (Lvl(x)*mult(x)) 'Callback
        If UseCallBack(x) then Proc name & x,Lvl(x)*mult(x) 'Proc
        If Lock(x) Then
          Loaded(x) = True
        end if
      end if
    Next
  End Sub
End Class


'Lamp Filter
Function LampFilter(aLvl)
  LampFilter = aLvl^1.6 'exponential curve?
End Function


'Helper functions
Sub Proc(string, Callback)  'proc using a string and one argument
  'On Error Resume Next
  dim p : Set P = GetRef(String)
  P Callback
  If err.number = 13 then  msgbox "Proc error! No such procedure: " & vbnewline & string
  if err.number = 424 then msgbox "Proc error! No such Object"
End Sub

Function AppendArray(ByVal aArray, aInput)  'append one value, object, or Array onto the end of a 1 dimensional array
  if IsArray(aInput) then 'Input is an array...
    dim tmp : tmp = aArray
    If not IsArray(aArray) Then 'if not array, create an array
      tmp = aInput
    Else            'Append existing array with aInput array
      Redim Preserve tmp(uBound(aArray) + uBound(aInput)+1) 'If existing array, increase bounds by uBound of incoming array
      dim x : for x = 0 to uBound(aInput)
        if isObject(aInput(x)) then
          Set tmp(x+uBound(aArray)+1 ) = aInput(x)
        Else
          tmp(x+uBound(aArray)+1 ) = aInput(x)
        End If
      Next
      AppendArray = tmp  'return new array
    End If
  Else 'Input is NOT an array...
    If not IsArray(aArray) Then 'if not array, create an array
      aArray = Array(aArray, aInput)
    Else
      Redim Preserve aArray(uBound(aArray)+1) 'If array, increase bounds by 1
      if isObject(aInput) then
        Set aArray(uBound(aArray)) = aInput
      Else
        aArray(uBound(aArray)) = aInput
      End If
    End If
    AppendArray = aArray 'return new array
  End If
End Function

'******************************************************
'****  END LAMPZ
'******************************************************


'****************************************************************
'   Cabinet Mode
'****************************************************************

If CabinetMode Then
  PinCab_Rails.visible = 0
Else
  PinCab_Rails.visible = 1
End If

'****************************************************************
'   Flipper Colour
'****************************************************************

If FlipColour > 0 Then
  If FlipColour = 1 Then
  LeftFlipper.material = "Rubber White"
  RightFlipper.material = "Rubber White"
  RightFlipper1.material = "Rubber White"
  LeftFlipper.rubbermaterial = "Rubber Red"
  RightFlipper.rubbermaterial = "Rubber Red"
  RightFlipper1.rubbermaterial = "Rubber Red"
  End If
  If FlipColour = 2 Then
  LeftFlipper.material = "Rubber White"
  RightFlipper.material = "Rubber White"
  RightFlipper1.material = "Rubber White"
  LeftFlipper.rubbermaterial = "Rubber Blue"
  RightFlipper.rubbermaterial = "Rubber Blue"
  RightFlipper1.rubbermaterial = "Rubber Blue"
  End If
Else
  LeftFlipper.material = "Rubber Black"
  RightFlipper.material = "Rubber Black"
  RightFlipper1.material = "Rubber Black"
  LeftFlipper.rubbermaterial = "Rubber Red"
  RightFlipper.rubbermaterial = "Rubber Red"
  RightFlipper1.rubbermaterial = "Rubber Red"
End If

If RailColour Then
  PinCab_Rails.material = "Metal_Black_Powdercoat"
Else
  PinCab_Rails.material = "Metal0.8"
End If

If BallBright Then
  table1.BallImage = "ball_HDR_brightest"
Else
  table1.BallImage = "ball_HDR_bright"
End If

'****************************************************************
'   VR Mode
'****************************************************************
DIM VRThings

If VRRoom > 0 Then
  ScoreText.visible = 0
  PinCab_Rails.visible = 1
  If VRRoom = 1 Then
    for each VRThings in VR_Cab:VRThings.visible = 1:Next
  End If
  If VRRoom = 2 Then
    for each VRThings in VR_Cab:VRThings.visible = 1:Next
    VR_Roof.image = "Pirate_VR_Roof"
    VR_Wall_Left.image = "Pirate_VR_Wall_Left"
    VR_Wall_Right.image = "Pirate_VR_Wall_Right"
    VR_Floor.image = "Pirate_VR_Floor"
  End If
  If VRRoom = 3 Then
    for each VRThings in VR_Cab:VRThings.visible = 0:Next
    PinCab_Backbox.visible = 1
    PinCab_Backglass.visible = 1
    DMD.visible = 1
  End If
  If VRFlashBackglass = 1 Then
    SetBackglass
    for each VRThings in GIstring4:VRThings.visible = 1:Next
    for each VRThings in GIstring5:VRThings.visible = 1:Next
    PinCab_Backglass.visible = 0
    BGDark.visible = 1
  Else
    for each VRThings in GIstring4:VRThings.visible = 0:Next
    for each VRThings in GIstring5:VRThings.visible = 0:Next
  End If
Else
    for each VRThings in VR_Cab:VRThings.visible = 0:Next
    TimerVRPlunger.Enabled = False
    TimerVRPlunger2.Enabled = False
End if


Dim Postmove
If Difficulty = 0 Then
  for each Postmove in Hardness:Postmove.y = 1435:Next
Elseif Difficulty = 1 Then
  for each Postmove in Hardness:Postmove.y = 1425:Next
Elseif Difficulty = 2 Then
  for each Postmove in Hardness:Postmove.y = 1415:Next
End If


'***************************************************************
'****  VPW DYNAMIC BALL SHADOWS by Iakki, Apophis, and Wylte
'***************************************************************

'****** INSTRUCTIONS please read ******

'****** Part A:  Table Elements ******
'
' Import the "bsrtx7" and "ballshadow" images
' Import the shadow materials file (3 sets included) (you can also export the 3 sets from this table to create the same file)
' Copy in the BallShadowA flasher set and the sets of primitives named BallShadow#, RtxBallShadow#, and RtxBall2Shadow#
' * with at least as many objects each as there can be balls, including locked balls
' Ensure you have a timer with a -1 interval that is always running

' Create a collection called DynamicSources that includes all light sources you want to cast ball shadows
'***These must be organized in order, so that lights that intersect on the table are adjacent in the collection***
'
' This is because the code will only project two shadows if they are coming from lights that are consecutive in the collection
' The easiest way to keep track of this is to start with the group on the left slingshot and move clockwise around the table
' For example, if you use 6 lights: A & B on the left slingshot and C & D on the right, with E near A&B and F next to C&D, your collection would look like EBACDF
'
'                               E
' A    C                          B
'  B    D     your collection should look like    A   because E&B, B&A, etc. intersect; but B&D or E&F do not
'  E      F                         C
'                               D
'                               F

' *** User Options - Uncomment here or move to top

Const fovY          = 0   'Offset y position under ball to account for layback or inclination (more pronounced need further back)
Const DynamicBSFactor     = 0.99  '0 to 1, higher is darker
Const AmbientBSFactor     = 0.8 '0 to 1, higher is darker
Const AmbientMovement   = 2   '1 to 4, higher means more movement as the ball moves left and right
Const Wideness        = 15  'Sets how wide the dynamic ball shadows can get (20 +5 thinness should be most realistic for a 50 unit ball)
Const Thinness        = 5   'Sets minimum as ball moves away from source



Function DistanceFast(x, y)
  dim ratio, ax, ay
  ax = abs(x)         'Get absolute value of each vector
  ay = abs(y)
  ratio = 1 / max(ax, ay)   'Create a ratio
  ratio = ratio * (1.29289 - (ax + ay) * ratio * 0.29289)
  if ratio > 0 then     'Quickly determine if it's worth using
    DistanceFast = 1/ratio
  Else
    DistanceFast = 0
  End if
end Function

Function max(a,b)
  if a > b then
    max = a
  Else
    max = b
  end if
end Function


'****** End Part B:  Code and Functions ******


'****** Part C:  The Magic ******
Dim sourcenames, currentShadowCount
sourcenames = Array ("","","","","","","","","","","","")
currentShadowCount = Array (0,0,0,0,0,0,0,0,0,0,0,0)

' *** Trim or extend these to match the number of balls/primitives/flashers on the table!
dim objrtx1(12), objrtx2(12)
dim objBallShadow(12)
Dim BallShadowA
BallShadowA = Array (BallShadowA0,BallShadowA1,BallShadowA2,BallShadowA3,BallShadowA4,BallShadowA5,BallShadowA6,BallShadowA7,BallShadowA8,BallShadowA9,BallShadowA10,BallShadowA11)

DynamicBSInit

sub DynamicBSInit()
  Dim iii

  for iii = 0 to tnob                 'Prepares the shadow objects before play begins
    Set objrtx1(iii) = Eval("RtxBallShadow" & iii)
    objrtx1(iii).material = "RtxBallShadow" & iii
    objrtx1(iii).z = iii/1000 + 0.01
    objrtx1(iii).visible = 0

    Set objrtx2(iii) = Eval("RtxBall2Shadow" & iii)
    objrtx2(iii).material = "RtxBallShadow2_" & iii
    objrtx2(iii).z = (iii)/1000 + 0.02
    objrtx2(iii).visible = 0

    currentShadowCount(iii) = 0

    Set objBallShadow(iii) = Eval("BallShadow" & iii)
    objBallShadow(iii).material = "BallShadow" & iii
    UpdateMaterial objBallShadow(iii).material,1,0,0,0,0,0,AmbientBSFactor,RGB(0,0,0),0,0,False,True,0,0,0,0
    objBallShadow(iii).Z = iii/1000 + 0.04
    objBallShadow(iii).visible = 0

    BallShadowA(iii).Opacity = 100*AmbientBSFactor
    BallShadowA(iii).visible = 0
  Next
end sub


Sub DynamicBSUpdate
  Dim falloff:  falloff = 150     'Max distance to light sources, can be changed if you have a reason
  Dim ShadowOpacity, ShadowOpacity2
  Dim s, Source, LSd, currentMat, AnotherSource, BOT
  BOT = GetBalls

  'Hide shadow of deleted balls
  For s = UBound(BOT) + 1 to tnob
    objrtx1(s).visible = 0
    objrtx2(s).visible = 0
    objBallShadow(s).visible = 0
    BallShadowA(s).visible = 0
  Next

  If UBound(BOT) < lob Then Exit Sub    'No balls in play, exit

'The Magic happens now
  For s = lob to UBound(BOT)

' *** Normal "ambient light" ball shadow
  'Layered from top to bottom. If you had an upper pf at for example 80 and ramps even above that, your segments would be z>110; z<=110 And z>100; z<=100 And z>30; z<=30 And z>20; Else invisible

    If AmbientBallShadowOn = 1 Then     'Primitive shadow on playfield, flasher shadow in ramps
      If BOT(s).Z > 30 Then             'The flasher follows the ball up ramps while the primitive is on the pf
        If BOT(s).X < tablewidth/2 Then
          objBallShadow(s).X = ((BOT(s).X) - (Ballsize/10) + ((BOT(s).X - (tablewidth/2))/(Ballsize/AmbientMovement))) + 5
        Else
          objBallShadow(s).X = ((BOT(s).X) + (Ballsize/10) + ((BOT(s).X - (tablewidth/2))/(Ballsize/AmbientMovement))) - 5
        End If
        objBallShadow(s).Y = BOT(s).Y + BallSize/10 + fovY
        objBallShadow(s).visible = 1

        BallShadowA(s).X = BOT(s).X
        BallShadowA(s).Y = BOT(s).Y + BallSize/5 + fovY
        BallShadowA(s).height=BOT(s).z - BallSize/4   'This is technically 1/4 of the ball "above" the ramp, but it keeps it from clipping
        BallShadowA(s).visible = 1
      Elseif BOT(s).Z <= 30 And BOT(s).Z > 20 Then  'On pf, primitive only
        objBallShadow(s).visible = 1
        If BOT(s).X < tablewidth/2 Then
          objBallShadow(s).X = ((BOT(s).X) - (Ballsize/10) + ((BOT(s).X - (tablewidth/2))/(Ballsize/AmbientMovement))) + 5
        Else
          objBallShadow(s).X = ((BOT(s).X) + (Ballsize/10) + ((BOT(s).X - (tablewidth/2))/(Ballsize/AmbientMovement))) - 5
        End If
        objBallShadow(s).Y = BOT(s).Y + fovY
        BallShadowA(s).visible = 0
      Else                      'Under pf, no shadows
        objBallShadow(s).visible = 0
        BallShadowA(s).visible = 0
      end if

    Elseif AmbientBallShadowOn = 2 Then   'Flasher shadow everywhere
      If BOT(s).Z > 30 Then             'In a ramp
        BallShadowA(s).X = BOT(s).X
        BallShadowA(s).Y = BOT(s).Y + BallSize/5 + fovY
        BallShadowA(s).height=BOT(s).z - BallSize/4   'This is technically 1/4 of the ball "above" the ramp, but it keeps it from clipping
        BallShadowA(s).visible = 1
      Elseif BOT(s).Z <= 30 And BOT(s).Z > 20 Then  'On pf
        BallShadowA(s).visible = 1
        If BOT(s).X < tablewidth/2 Then
          BallShadowA(s).X = ((BOT(s).X) - (Ballsize/10) + ((BOT(s).X - (tablewidth/2))/(Ballsize/AmbientMovement))) + 5
        Else
          BallShadowA(s).X = ((BOT(s).X) + (Ballsize/10) + ((BOT(s).X - (tablewidth/2))/(Ballsize/AmbientMovement))) - 5
        End If
        BallShadowA(s).Y = BOT(s).Y + Ballsize/10 + fovY
        BallShadowA(s).height=BOT(s).z - BallSize/2 + 5
      Else                      'Under pf
        BallShadowA(s).visible = 0
      End If
    End If

' *** Dynamic shadows
    If DynamicBallShadowsOn Then
      If BOT(s).Z < 30 Then 'And BOT(s).Y < (TableHeight - 200) Then 'Or BOT(s).Z > 105 Then    'Defining when and where (on the table) you can have dynamic shadows
        For Each Source in DynamicSources
          LSd=DistanceFast((BOT(s).x-Source.x),(BOT(s).y-Source.y)) 'Calculating the Linear distance to the Source
          If LSd < falloff and Source.state=1 Then            'If the ball is within the falloff range of a light and light is on
            currentShadowCount(s) = currentShadowCount(s) + 1   'Within range of 1 or 2
            if currentShadowCount(s) = 1 Then           '1 dynamic shadow source
              sourcenames(s) = source.name
              currentMat = objrtx1(s).material
              objrtx2(s).visible = 0 : objrtx1(s).visible = 1 : objrtx1(s).X = BOT(s).X : objrtx1(s).Y = BOT(s).Y + fovY
  '           objrtx1(s).Z = BOT(s).Z - 25 + s/1000 + 0.01            'Uncomment if you want to add shadows to an upper/lower pf
              objrtx1(s).rotz = AnglePP(Source.x, Source.y, BOT(s).X, BOT(s).Y) + 90
              ShadowOpacity = (falloff-LSd)/falloff                 'Sets opacity/darkness of shadow by distance to light
              objrtx1(s).size_y = Wideness*ShadowOpacity+Thinness           'Scales shape of shadow with distance/opacity
              UpdateMaterial currentMat,1,0,0,0,0,0,ShadowOpacity*DynamicBSFactor^2,RGB(0,0,0),0,0,False,True,0,0,0,0
              If AmbientBallShadowOn = 1 Then
                currentMat = objBallShadow(s).material                  'Brightens the ambient primitive when it's close to a light
                UpdateMaterial currentMat,1,0,0,0,0,0,AmbientBSFactor*(1-ShadowOpacity),RGB(0,0,0),0,0,False,True,0,0,0,0
              Else
                BallShadowA(s).Opacity = 100*AmbientBSFactor*(1-ShadowOpacity)
              End If

            Elseif currentShadowCount(s) = 2 Then
                                  'Same logic as 1 shadow, but twice
              currentMat = objrtx1(s).material
              set AnotherSource = Eval(sourcenames(s))
              objrtx1(s).visible = 1 : objrtx1(s).X = BOT(s).X : objrtx1(s).Y = BOT(s).Y + fovY
  '           objrtx1(s).Z = BOT(s).Z - 25 + s/1000 + 0.01              'Uncomment if you want to add shadows to an upper/lower pf
              objrtx1(s).rotz = AnglePP(AnotherSource.x, AnotherSource.y, BOT(s).X, BOT(s).Y) + 90
              ShadowOpacity = (falloff-(((BOT(s).x-AnotherSource.x)^2+(BOT(s).y-AnotherSource.y)^2)^0.5))/falloff
              objrtx1(s).size_y = Wideness*ShadowOpacity+Thinness
              UpdateMaterial currentMat,1,0,0,0,0,0,ShadowOpacity*DynamicBSFactor^3,RGB(0,0,0),0,0,False,True,0,0,0,0

              currentMat = objrtx2(s).material
              objrtx2(s).visible = 1 : objrtx2(s).X = BOT(s).X : objrtx2(s).Y = BOT(s).Y + fovY
  '           objrtx2(s).Z = BOT(s).Z - 25 + s/1000 + 0.02              'Uncomment if you want to add shadows to an upper/lower pf
              objrtx2(s).rotz = AnglePP(Source.x, Source.y, BOT(s).X, BOT(s).Y) + 90
              ShadowOpacity2 = (falloff-LSd)/falloff
              objrtx2(s).size_y = Wideness*ShadowOpacity2+Thinness
              UpdateMaterial currentMat,1,0,0,0,0,0,ShadowOpacity2*DynamicBSFactor^3,RGB(0,0,0),0,0,False,True,0,0,0,0
              If AmbientBallShadowOn = 1 Then
                currentMat = objBallShadow(s).material                  'Brightens the ambient primitive when it's close to a light
                UpdateMaterial currentMat,1,0,0,0,0,0,AmbientBSFactor*(1-max(ShadowOpacity,ShadowOpacity2)),RGB(0,0,0),0,0,False,True,0,0,0,0
              Else
                BallShadowA(s).Opacity = 100*AmbientBSFactor*(1-max(ShadowOpacity,ShadowOpacity2))
              End If
            end if
          Else
            currentShadowCount(s) = 0
            BallShadowA(s).Opacity = 100*AmbientBSFactor
          End If
        Next
      Else                  'Hide dynamic shadows everywhere else
        objrtx2(s).visible = 0 : objrtx1(s).visible = 0
      End If
    End If
  Next
End Sub
'****************************************************************
'****  END VPW DYNAMIC BALL SHADOWS by Iakki, Apophis, and Wylte
'****************************************************************

'******************************************************
'           LUT
'******************************************************


Sub SetLUT  'AXS
  Table1.ColorGradeImage = "LUT" & LUTset
end sub

Sub LUTBox_Timer
  LUTBox.TimerEnabled = 0
  LUTBox.Visible = 0
End Sub

Sub ShowLUT
  LUTBox.visible = 1
  Select Case LUTSet
    Case 0: LUTBox.text = "Fleep Natural Dark 1"
    Case 1: LUTBox.text = "Fleep Natural Dark 2"
    Case 2: LUTBox.text = "Fleep Warm Dark"
    Case 3: LUTBox.text = "Fleep Warm Bright"
    Case 4: LUTBox.text = "Fleep Warm Vivid Soft"
    Case 5: LUTBox.text = "Fleep Warm Vivid Hard"
    Case 6: LUTBox.text = "Skitso Natural and Balanced"
    Case 7: LUTBox.text = "Skitso Natural High Contrast"
    Case 8: LUTBox.text = "3rdaxis Referenced THX Standard"
    Case 9: LUTBox.text = "CalleV Punchy Brightness and Contrast"
    Case 10: LUTBox.text = "HauntFreaks Desaturated"
      Case 11: LUTBox.text = "Tomate washed out"
        Case 12: LUTBox.text = "VPW original 1on1"
        Case 13: LUTBox.text = "bassgeige"
        Case 14: LUTBox.text = "blacklight"
        Case 15: LUTBox.text = "B&W Comic Book"
    Case 16: LUTBox.text = "Skitso New ColorLut"
  End Select
  LUTBox.TimerEnabled = 1
End Sub

Sub SaveLUT
  Dim FileObj
  Dim ScoreFile

  Set FileObj=CreateObject("Scripting.FileSystemObject")
  If Not FileObj.FolderExists(UserDirectory) then
    Exit Sub
  End if

  if LUTset = "" then LUTset = 16 'failsafe

  Set ScoreFile=FileObj.CreateTextFile(UserDirectory & "BRoseLUT.txt",True)
  ScoreFile.WriteLine LUTset
  Set ScoreFile=Nothing
  Set FileObj=Nothing
End Sub
Sub LoadLUT
  Dim FileObj, ScoreFile, TextStr
  dim rLine

  Set FileObj=CreateObject("Scripting.FileSystemObject")
  If Not FileObj.FolderExists(UserDirectory) then
    LUTset=16
    Exit Sub
  End if
  If Not FileObj.FileExists(UserDirectory & "BRoseLUT.txt") then
    LUTset=16
    Exit Sub
  End if
  Set ScoreFile=FileObj.GetFile(UserDirectory & "BRoseLUT.txt")
  Set TextStr=ScoreFile.OpenAsTextStream(1,0)
    If (TextStr.AtEndOfStream=True) then
      Exit Sub
    End if
    rLine = TextStr.ReadLine
    If rLine = "" then
      LUTset=16
      Exit Sub
    End if
    LUTset = int (rLine)
    Set ScoreFile = Nothing
      Set FileObj = Nothing
End Sub


'******************************************************
'*******  Set Up Backglass Flashers *******
'******************************************************

Sub SetBackglass()
  Dim obj

  For Each obj In BackglassLow
    obj.x = obj.x
    obj.height = - obj.y + 65
    obj.y = 140 'adjusts the distance from the backglass towards the user
    obj.rotx=-86.5
  Next


  For Each obj In BackglassMid
    obj.x = obj.x
    obj.height = - obj.y + 65
    obj.y = 120 'adjusts the distance from the backglass towards the user
    obj.rotx=-86.5
  Next


  For Each obj In BackglassHigh
    obj.x = obj.x
    obj.height = - obj.y + 65
    obj.y = 100 'adjusts the distance from the backglass towards the user
    obj.rotx=-86.5
  Next
End Sub

'******************* VR Plunger **********************

Sub TimerVRPlunger_Timer
  If VR_Primary_plunger.Y < 115 then
       VR_Primary_plunger.Y = VR_Primary_plunger.Y + 5
  End If
End Sub

Sub TimerVRPlunger2_Timer
  VR_Primary_plunger.Y = -21.57699 + (5* Plunger.Position) -20
End Sub


